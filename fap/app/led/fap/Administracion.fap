Formulario Administracion permiso=administrador{

	Menu {
		Enlace "Mis Solicitudes" pagina=Solicitudes 
		
		Grupo "Administración" {
			Enlace "Usuarios" pagina=Usuarios
			Enlace "Convocatoria" pagina=Convocatoria
			Enlace "Gestor Documental" pagina=Aed
			Enlace "Tramites Verificables" pagina=TramitesVerificables
			Enlace "Gestionar Baremación" pagina=ActivarBaremacion
			Enlace "Cesion de Datos" permiso=adminOrGestor pagina=GenerarFichCesiones
			Enlace "Requerimientos" pagina=ConfiguracionQuartz
			Enlace "Tipos Certificados" pagina=ConfigurarCertificados
			Enlace "Tabla de tablas" pagina=TablaDeTablas
			Enlace "Codigos Requerimientos y Exclusión" pagina=Codigos
			Enlace "Emails" pagina=Emails
			Enlace "Logs" pagina=BuscarLgs
			Enlace "Informe" accion="fap.InformeController.index"
			Enlace "Consultas a BBDD" pagina=ConsultasBBDD
			Enlace "Incidencias" accion="fap.IncidenciasController.index"
			Enlace "Properties" pagina=Propiedades
			Enlace "Configurar Jobs" pagina=Jobs
			Enlace "Visibilidad de Estados" pagina=EstadosVisibles
			Enlace "Configurar mensajes" pagina=ConfigurarMensajes
			Enlace "Resoluciones" pagina=CargarTiposAreasResoluciones
			Enlace "Gráficas" pagina=ServiciosWebApp
			Enlace "Migración de Aportaciones" pagina=ActualizarAportaciones
		}
		
		Enlace "Ayuda" pagina=Soporte crear
		Enlace "Salir" accion=logout
    }

    Pagina ServiciosWebApp noForm titulo="Servicios web de la aplicación" campo=Aplicacion  permiso=administrador {
		Grupo permiso=noEditable titulo="Datos de la aplicación" {
			Texto campo=Aplicacion.nombreApp titulo="Nombre de la aplicación" 
			Texto campo=Aplicacion.urlApp titulo="URL de la aplicación"
		}
		
		Tabla serviciosWeb titulo="Servicios web activos" campo=Aplicacion.serviciosWeb
		paginaEditar=GraficasWS
		{
			Columna titulo="ID" campo=ServiciosWeb.id ancho="35"
			Columna titulo="Nombre del servicio web" campo=ServiciosWeb.servicioWebInfo.nombre expandir
			Columna titulo="URL del servicio web" campo=ServiciosWeb.servicioWebInfo.urlWS expandir
			Columna titulo="Descripción" campo=ServiciosWeb.servicioWebInfo.descripcion expandir
		}
		
		Form formBtnRecargaWS {
			Boton recargasWS titulo="Recargar Servicios Web" ayuda="Pulse este botón para obligar a actualizar los servicios web de la aplicación" (popover)
		}
		
		Tabla historialServiciosWeb titulo="Historial de servicios web" campo=Aplicacion.serviciosWeb
		paginaEditar=GraficasWS
		{
			Columna titulo="ID" campo=ServiciosWeb.id ancho="35"
			Columna titulo="Nombre del servicio web" campo=ServiciosWeb.servicioWebInfo.nombre expandir
			Columna titulo="URL del servicio web" campo=ServiciosWeb.servicioWebInfo.urlWS expandir
			Columna titulo="Descripción" campo=ServiciosWeb.servicioWebInfo.descripcion expandir
		}
	}
	
	Pagina GraficasWS noForm titulo="Gráficas servicios web" campo=Aplicacion.serviciosWeb {
		Wiki {"Aquí se mostrará el SuperTag"}
		Form formBtn {
			Boton btnRecargasDatos titulo="Recargar datos" ayuda="Pulse este botón para obligar a actualizar los datos de los servicios web" (popover)
			Boton btnVolver titulo="Volver a la lista de Servicios Web" pagina=ServiciosWebApp
		}

	}


    Pagina ActualizarAportaciones permiso=administrador perteneceA="Administracion"{
    	Grupo titulo="Convertir Aportaciones al nuevo modelo"{
	    	Wiki {"Será necesario ejecutar esta acción UNA única vez, si la aplicación utilizaba una versión de FAP anterior a la versión 2.1.1RC12, justo en el momento de la primera ejecución de la aplicación con la nueva versión"}
	    	Form botonActualizarAportaciones {
	    		Boton actualizar titulo="Migrar Aportaciones v2.x a v2.1.1RC12 y superiores" type=danger
	    	}
	    }
    }
    

    Pagina TramitesVerificables titulo="Tramites Verificables" campo=VerificacionTramites perteneceA="Administracion"{
    	Tabla tramitesVerificablesTbl titulo="Tramites Supceptibles de ser Verificables" campo=VerificacionTramites.tramites popupEditar=PopUpTramiteVerificable popupLeer=PopUpTramiteVerificable recargarPagina{
    		Columna campo=TramitesVerificables.nombre titulo="Nombre" expandir
    		Columna campo=TramitesVerificables.uriTramite titulo="Uri Tramite" expandir
    		Columna campo=TramitesVerificables.verificable titulo="¿Verificable?"
    	}
    	Combo tramitePorDefecto campo=VerificacionTramites.uriTramitePorDefecto titulo="Tramite Por Defecto" requerido
    }
    
    Popup PopUpTramiteVerificable campo=VerificacionTramites.tramites titulo="Configurar Tramite Verificable"{
    	Grupo noEditable borde=false permiso=noEditable{
    		Texto titulo="Nombre" campo=TramitesVerificables.nombre
    		Texto titulo="Uri" campo=TramitesVerificables.uriTramite
    	}
    	Check titulo="Verificable" campo=TramitesVerificables.verificable
    }
    

    // Pagina Sobreescrita
    Pagina ConsultasBBDD titulo="Consultas" perteneceA="Administracion"{
    	Grupo titulo = "Consultas a la Base de Datos de la aplicación"{
    		Tabla consultasdb campo=Consulta titulo="Consultas" popup=PopUpConsultas {
    			Columna campo=Consulta.consulta titulo="Consulta" expandir
    			Columna campo=Consulta.descripcion titulo="Descripción" expandir
    			Columna campo=Consulta.tipo titulo="Tipo" expandir
    		}
    	}
    }
    
    Popup PopUpConsultas titulo="Consulta a la Base de Datos" campo=Consulta maximizar{
    	AreaTexto areaDescripcion titulo="Descripción" campo=Consulta.descripcion requerido
    	AreaTexto areaConsulta titulo="Consulta" campo=Consulta.consulta requerido
    	Combo comboTipo titulo="Tipo" campo=Consulta.tipo requerido
    }
    
    Pagina Jobs titulo="Configurar Jobs" campo=AdministracionFapJobs perteneceA="Administracion"{
    	Wiki{"*Esta Página permite habilitar/deshabilitar los Jobs que están activos del módulo FAP.*"}
    	
    	Grupo titulo="Job para la Actualización de las Notificaciones" mostrarSiExpresion"properties.FapProperties.getBoolean(\"fap.notificacion.activa\")"{
    		Wiki{"Este Job es el encargado de conectarse cada X MINUTOS (según la configuración de la aplicación) al servicio web de Notificaciones que esté configurado para actualizar la información relevante de las notificaciones que existen en base de datos asociadas a la aplicación."}
    		Check cActualizarNotificaciones titulo="Actualizar Notificaciones" campo=AdministracionFapJobs.actualizarNotificaciones alineadoAIzquierdas
    		Grupo borde=false mostrarSiCheck cActualizarNotificaciones = true{
	    		RadioBooleano rActualizarNotificaciones titulo="Intervalo de tiempo" campo=AdministracionFapJobs.valorPropioActualizarNotificaciones alineadoAIzquierdas botonIzquierdo="Por Defecto" botonDerecho="Personalizar"
	    		Grupo borde=false mostrarSiRadioBooleano rActualizarNotificaciones = true{
	    			BarraDeslizante bdActualizarNotificaciones titulo="Seleccione el intervalo de tiempo (en minutos)" campo=AdministracionFapJobs.valorActualizarNotificaciones minimo=0 maximo=60 valorDefecto=1 alineadoAIzquierdas
	    		}
	    	}
    	}
    	
    	Grupo titulo="Job para la Compresión de los Logs"{
    		Wiki{"Esta Job es el encargado de comprimir los Logs de días anteriores. Cada X HORAS, el Job se encarga de comprimir los ficheros de Logs del día anterior para así tener un backup de tamaño reducido."}
    		Check cComprimirLogs titulo="Comprimir Logs" campo=AdministracionFapJobs.comprimirLogs alineadoAIzquierdas
    		Grupo borde=false mostrarSiCheck cComprimirLogs = true{
	    		RadioBooleano rComprimirLogs titulo="Intervalo de tiempo" campo=AdministracionFapJobs.valorPropioComprimirLogs alineadoAIzquierdas botonIzquierdo="Por Defecto" botonDerecho="Personalizar"
	    		Grupo borde=false mostrarSiRadioBooleano rComprimirLogs = true{
	    			BarraDeslizante bdComprimirLogs titulo="Seleccione el intervalo de tiempo (en horas)" campo=AdministracionFapJobs.valorComprimirLogs minimo=0 maximo=24 valorDefecto=24 alineadoAIzquierdas
	    		}
	    	}
    	}
    	
    	Grupo titulo="Job para Eliminar Archivos Temporales" mostrarSiExpresion"properties.FapProperties.getBoolean(\"fap.delete.temporals\")"{
    		Wiki{"Esta Job es el encargado de eliminar, cada X HORAS, los archivos temporales que se hayan ido creando por la aplicación y que sean más antiguos a una fecha configurada."}
    		Check cEliminarTemporales titulo="Eliminar Archivos Temporales" campo=AdministracionFapJobs.eliminarTemporales alineadoAIzquierdas
    		Grupo borde=false mostrarSiCheck cEliminarTemporales = true{
	    		RadioBooleano rEliminarTemporales titulo="Intervalo de tiempo" campo=AdministracionFapJobs.valorPropioEliminarTemporales alineadoAIzquierdas botonIzquierdo="Por Defecto" botonDerecho="Personalizar"
	    		Grupo borde=false mostrarSiRadioBooleano rEliminarTemporales = true{
	    			BarraDeslizante bdEliminarTemporales titulo="Seleccione el intervalo de tiempo (en horas)" campo=AdministracionFapJobs.valorEliminarTemporales minimo=0 maximo=24 valorDefecto=5 alineadoAIzquierdas
	    		}
	    	}
    	}
    	
    	Grupo titulo="Job para Notificar Alertas de las Anotaciones" {
    		Wiki{"Este Job es el encargado de notificar las alertas de las anotaciones creadas, comprobando cada X HORAS si existe alguna nueva."}
    		Check cNotificarAlertasAnotaciones titulo="Notificar Alertas Anotaciones" campo=AdministracionFapJobs.notificarAlertasAnotaciones alineadoAIzquierdas
    		Grupo borde=false mostrarSiCheck cNotificarAlertasAnotaciones = true{
	    		RadioBooleano rNotificarAlertasAnotaciones titulo="Intervalo de tiempo" campo=AdministracionFapJobs.valorPropioNotificarAlertasAnotaciones alineadoAIzquierdas botonIzquierdo="Por Defecto" botonDerecho="Personalizar"
	    		Grupo borde=false mostrarSiRadioBooleano rNotificarAlertasAnotaciones = true{
	    			BarraDeslizante bdNotificarAlertasAnotaciones titulo="Seleccione el intervalo de tiempo (en horas)" campo=AdministracionFapJobs.valorNotificarAlertasAnotaciones minimo=0 maximo=24 valorDefecto=24 alineadoAIzquierdas
	    		}
	    	}
    	}
    	
    	Grupo titulo="Actualización manual de Notificaciones" {
    		Boton btnNotificaciones titulo="Actualizar"
    	}
    }
    
    Pagina ConfigurarMensajes titulo="Configurar mensajes"  perteneceA="Administracion"{
    	Tabla mensajesPagina campo=ConfigurarMensaje titulo="Mensajes configurados" paginaEditar=RellenarMensajes {
    		Columna campo=ConfigurarMensaje.formularioNombreText titulo="Página" expandir
    		Columna campo=ConfigurarMensaje.tipoMensaje titulo="Tipo Mensaje" expandir
    		Columna campo=ConfigurarMensaje.habilitarText titulo="Habilitado" expandir
    	}
    }
    
    Pagina RellenarMensajes titulo="Rellenar mensaje" campo=ConfigurarMensaje perteneceA="Administracion" {
    	Accion editar redirigir = ConfigurarMensajes
    	Grupo permiso = paginaAConfigurar borde=false{
    		Texto paginaAconfigurar campo=ConfigurarMensaje.nombrePagina titulo="Página"
    	} 
    	Combo tipoMensaje campo=ConfigurarMensaje.tipoMensaje titulo="Tipo" ayuda="Seleccionar si se quiere configurar un wiki u otro tipo de mensaje" (popover) requerido
    	
    	Grupo mostrarSiCombo tipoMensaje="wiki" borde=false {
    		Texto tituloMensaje campo=ConfigurarMensaje.tituloMensaje titulo="Título del Wiki" requerido
    	}
    	
    	AreaTexto contenido campo=ConfigurarMensaje.contenido titulo="Contenido del mensaje" ancho="500px" ayuda="Contenido del mensaje que podremos colocar en la página de la aplicación" (popover) requerido
    	Check habilitado campo=ConfigurarMensaje.habilitar titulo="Habilitar la aparición del mensaje en la página" ayuda="Será necesario habilitar en caso de querer mostrar en la página el mensaje" (popover)
		Combo posicion campo=ConfigurarMensaje.msjFinal titulo = "Posición del mensaje en la página" requerido
   	}
    
    Pagina ActivarBaremacion titulo="Activar Baremación" noForm perteneceA="Administracion"{
    	Grupo IniciarTipoEvaluacion titulo="Iniciar el Tipo de Evaluación" permiso=iniciarTipoEvaluacion{
    		Wiki {"Para cargar el tipo de Evaluación, es necesario pulsar el siguiente botón. El tipo de Evaluación sólo se cargará una única vez, téngalo en cuenta."}
    		Form iniciarTipoEvaluacionFormBaremacion {
    			Boton iniciarTipoEvaluacionBoton titulo="Cargar Tipo Evaluación"
    		}
    	}
    	Grupo IniciarBaremacion titulo="Iniciar la Evaluación de la Baremación" permiso=iniciarBaremacion{
	    	Wiki {"Para activar la baremación, por primera vez, es necesario pulsar el siguiente botón, que generará todos los datos oportunos, activando las secciones de la aplicación destinadas a la Baremación."}
	    	Form activarFormBaremacion{
	    		Boton activarBaremacionBoton titulo="Iniciar Evaluación de Baremación"
	    	}
	    }
	    Grupo ActualizarBaremacion titulo="Actualizar los datos variables de la Baremación" permiso=actualizarBaremacion{
	    	Wiki {"Si ha cambiado (actualizado o insertado) nuevos datos en los ficheros de criterios, conceptosEconomicos o datosAdicionales, es necesario que pulse el siguiente botón, para que se actualice el modelo de la aplicación según los nuevos cambios."}
	    	Form actualizarFormBaremacion{
	    		Boton actualizarBaremacionBoton titulo="Actualizar Baremación"
	    	}
	    }
    }
	
	Pagina Usuarios titulo="Administración de Usuarios" perteneceA="Administracion"{
		Wiki {"Para asignar roles seleccione el usuario y pulse en editar"}

		Tabla listaUsuarios titulo="Usuarios" campo=Agente
			popup=PopupUsuario
		{
			Columna campo=Agente.id titulo="Id" ancho="25"
			Columna campo=Agente.username titulo="Usuario" expandir
			Columna campo=Agente.name titulo="Nombre completo" expandir
			Columna campo=Agente.roles titulo="Roles" expandir
			Columna campo=Agente.email titulo="Email" ancho="150"
			Columna campo=Agente.cargo titulo="Cargo" expandir
			Columna campo=Agente.funcionario titulo="Funcionario" ancho="80"
			Columna campo=Agente.usuarioldap titulo="Usuario LDAP" expandir
		}
	}
	
	Popup PopupUsuario campo=Agente titulo="Usuario" {
		Texto campo=Agente.username titulo="Username" requerido
		Grupo permiso=accesoNoCertificado borde=false{
			Texto campo=Agente.passwordAntiguo titulo="Contraseña Antigua" password
			Texto campo=Agente.password titulo="Contraseña Nueva" password duplicar
			Enlace generarPass titulo="Generar contraseña" estilo="btn btn-link" codigojs="usuarioGenerarPass(hiddenPass);"
		}
		Combo campo=Agente.roles titulo="Roles" busqueda requerido 
		Texto campo=Agente.email titulo="Email" requerido
		Texto campo=Agente.cargo titulo="Cargo"
		Texto campo=Agente.usuarioldap titulo="Usuario LDAP"
		Check campo=Agente.funcionario titulo="Funcionario habilitado"
	}
		
	Pagina Aed noForm permiso=administrador titulo="Gestor Documental" perteneceA="Administracion"{
		
		Grupo titulo="Configurar" {
			Wiki {
"Antés de poder subir un archivo es necesario que el gestor documental esté configurado. La configuración del
servicio depende de la implementación que tengas configurada. Por ejemplo, la configuración de la implementación
del Aed crea una carpeta en el servidor donde almacenará los documentos temporales. La implementación de sistema de ficheros
crea la carpeta"
			}
			
			Form configurar {
				Boton configurar titulo="Configurar gestor documental"	
			}
		}
		
		Grupo titulo="Tipos de documentos" {
			Wiki {
"Actualiza a partir del servicio web del Aed la definición de los trámites y los tipos de documentos asociados a cada trámite.
Actualiza las tablas **ConfiguracionAed**, **Trámite** y **TiposDocumentos**.
También actualiza la tabla de tablas, añadiendo los tipos de documentos en la tabla **tiposdocumentos**.
"}

			Form actualizarTramites {
				Boton actualizarTramites titulo="Actualizar trámites"
			}
			
			Wiki {
                "Actualiza los metadatos de los tipos de documentos."
            }
			Form actualizarMetadatos {
                Boton actualizarMetadatos titulo="Actualizar metadatos"
            }
			
			Tabla tramites campo=Tramite popupLeer=PopupTramite titulo="Trámites pertenecientes al procedimiento"{
				Columna titulo="Nombre" campo=Tramite.nombre ancho="250"
				Columna titulo="URI" campo=Tramite.uri expandir
			}
		}
	}
	
	Popup PopupTramite campo=Tramite {
		Texto campo=Tramite.nombre titulo="Nombre"
		Texto campo=Tramite.uri titulo="URI"
		
		Tabla documentos campo=Tramite.documentos titulo="Documentos pertenecientes al trámite" {
			Columna titulo="Nombre" campo=TipoDocumento.nombre ancho="250"
			Columna titulo="Aportado Por" campo=TipoDocumento.aportadoPor ancho="250"
			Columna titulo="Obligatoriedad" campo=TipoDocumento.obligatoriedad ancho="250"
			Columna titulo="URI" campo=TipoDocumento.uri ancho="600"
		}	
	}
	
	Pagina Convocatoria campo=Convocatoria titulo="Datos Convocatoria" perteneceA="Administracion"{
		Combo campo=Convocatoria.estado titulo="Estado de la convocatoria"
		
		Grupo grpExpedienteConvocatoria titulo ="Expediente de la Convocatoria" permiso=convocatoriaTieneExpediente {
			Texto campo=Convocatoria.expedienteAed.idAed titulo="Identificador del Expediente"
		}
	}
	
	Pagina ConfiguracionQuartz titulo="Configuración del Quartz" perteneceA="Administracion"{
		Grupo configQuartz titulo="Configuración del quartz" {
			Wiki {"El quartz es el proceso que se encarga de comprobar los accesos a la sede cuando se emiten los requerimientos y asignar correctamente las fechas de acceso y de vencimiento de los requerimientos."}
			
			Check campo=Quartz.execute titulo="Permitir ejecutar el quartz"
			Check campo=Quartz.mostrarTodasSolicitudes titulo="Mostrar los datos de todas las solicitudes (para pruebas)"
			Check campo=Quartz.mostrarSolicitudesRequeridas titulo="Mostrar información de las solicitudes en estado REQUERIDA"
			Check campo=Quartz.ejecutarCambioDeFecha titulo="Permitir comprobar si el usuario ha entrado en la sede y ejecutar el cambio de fecha de vencimiento"
			Check campo=Quartz.cambiarEstadoPlazoVencido titulo="Permitir el cambio de estado a PLAZO_VENCIDO de las solicitudes fuera de plazo"
			Check campo=Quartz.sendMail titulo="Habilitar el envio de mail a falta de 5 y 2 días para la fecha de vencimiento"
			Boton ejecutarQuartz titulo="Ejecutar Quartz"
		}
	}


	Pagina TablaDeTablas noForm titulo="Tabla de tablas" permiso=administrador perteneceA="Administracion" {
		Wiki {"La tabla de tablas almacena la información de la listas. "}

		Tabla tabladetablas titulo="Tabla de tablas" campo=TableKeyValue popup=PopupTablaDeTablas {
			Columna campo=TableKeyValue.table titulo="Tabla"
			Columna campo=TableKeyValue.key titulo="Clave"
			Columna campo=TableKeyValue.value titulo="Valor" expandir
		}
		
		Tabla tabladetablasdependency titulo="Tabla de tablas de dependencias" campo=TableKeyValueDependency popup=PopupTablaDeTablasDependency {
			Columna campo=TableKeyValueDependency.table titulo="Tabla"
			Columna campo=TableKeyValueDependency.dependency titulo="Dependencia"
			Columna campo=TableKeyValueDependency.key titulo="Clave" expandir
		}

		Grupo titulo="Actualizar desde fichero" {
			Wiki {"**Estas operaciones pueden tardar un tiempo bastante considerable**, sobre todo las dos últimas de Actualización sin Borrado previo. Si quiere agilizar el tiempo de computo puede desactivar la carga de la Tabla Municipios en las acciones de Actualización."}
			Grupo titulo="Resetear Tabla de Tablas"{
				Form actualizarDesdeFichero {
					Wiki {"
	**Perderá todas las modificaciones** que ha hecho y recuperará los valores puestos en la generación.
	
	**IMPORTANTE** : Los tipos de documentos no se recuperan de valores puesto de la generación, se recuperan a partir del AED.
	Después de pulsar este botón vaya a Administración/Aed/Actualizar tipos de documentos desde el AED.
	                "}
	
					Boton botonCargarTablaDeTablas titulo="Borrar todas y leer desde ficheros" type=danger
				}
			}
			Grupo titulo="Actualizar Tabla de Tablas"{
				Form actualizarDesdeFicheroModicando {
					Wiki {"
	**Perderá todas las modificaciones** que ha hecho y se actualizarán a los valores nuevos. Para agilizar la carga puede desactivar la actualización de la tabla Muncipios por defecto de FAP, que acapara la mayoría del tiempo.
	                "}
					Boton botonCargarTablaDeTablasModificando titulo="Actualizar Valores desde fichero" type=warning
					Boton botonCargarTablaDeTablasModificandoSinMunicipios titulo="Actualizar Valores desde fichero Sin Cargar Municipios" type=warning
				}
			}
			Grupo titulo="Actualizar Creando sólo los Nuevos Elementos de la Tabla de Tablas"{
				Form actualizarDesdeFicheroSoloCreando {
					Wiki {"
	**Sólo se crearan los nuevos valores** que se encuentren, respetando los que ya estaban. Para agilizar la carga puede desactivar la actualización de la tabla Muncipios por defecto de FAP, que acapara la mayoría del tiempo.
	                "}
					Boton botonCargarTablaDeTablasSoloCreando titulo="Crear Nuevos desde fichero"
					Boton botonCargarTablaDeTablasSoloCreandoSinMunicipios titulo="Crear Nuevos desde fichero Sin Cargar Municipios"
				}
			}
		}
	}

	Popup PopupTablaDeTablas campo=TableKeyValue {
		Texto campo=TableKeyValue.table titulo="Tabla"
		Texto campo=TableKeyValue.key titulo="Clave"
		Texto campo=TableKeyValue.value titulo="Valor"
	}
	
	Popup PopupTablaDeTablasDependency campo=TableKeyValueDependency {
		Texto campo=TableKeyValueDependency.table titulo="Tabla"
		Texto campo=TableKeyValueDependency.dependency titulo="Dependencia"
		Texto campo=TableKeyValueDependency.key titulo="Clave"
	}

	Pagina Emails titulo="Configuración de Correos Electrónicos" noForm perteneceA="Administracion"{
		Tabla emails titulo="Plantillas de emails" campo=Mail popup=PopupEmail {
			Columna campo=Mail.idMail titulo="Identificador"
			Columna campo=Mail.content titulo="Contenido" expandir
		}
        
		Grupo titulo="Actualizar desde fichero" {
			Form actualizarDesdeFichero {
				Wiki {"**Perderá todas las modificaciones** que ha hecho y recuperará los valores puestos en los ficheros .yaml situados en: 'app/emails/initial-data/'."}
				Boton botonCargarMails titulo="Borrar todos y leer desde ficheros"
			}
		}
	}

    Popup PopupEmail campo=Mail titulo="Email" {
        Wiki {"
Dentro de los campos puede utilizar variables con $ {nombreVariable} y otros elementos que puede consultar en la \"documentación\":http://www.playframework.org/documentation/1.2.1/templates#syntax.

En los campos **Para** y **CC** puede poner varios destinatarios separados por \",\".
        "}

        Texto campo=Mail.idMail titulo="Identificador" requerido
        Texto campo=Mail.sender titulo="De"
        Texto campo=Mail.sendTo titulo="Para" requerido
        Texto campo=Mail.cc titulo="CC"
        Texto campo=Mail.bcc titulo="BCC"
        Texto campo=Mail.subject titulo="Asunto" requerido
        AreaTexto campo=Mail.content titulo="Mensaje" requerido
		AreaTexto campo=Mail.footer titulo="Pie de mensaje"

    }
    
	Popup PopupConsulta campo=Consulta titulo="Consulta" {
		Accion borrar crearSiempre
		Accion crear crearSiempre
		Accion editar crearSiempre
		
		AreaTexto campo=Consulta.descripcion titulo="Descripcion" requerido
		AreaTexto campo=Consulta.consulta titulo="Consulta" requerido
		Combo campo=Consulta.tipo titulo="Tipo" requerido
	}

	Pagina Propiedades titulo="Propiedades de la aplicación"  perteneceA="Administracion"{
		Tabla tablaPropiedades titulo="Tabla de properties" campo=Propiedades popup=PopupProperties {
			Columna campo=Propiedades.descripcion titulo="Descripción"
			Columna campo=Propiedades.clave titulo="Clave"
			Columna campo=Propiedades.valor titulo="Valor" expandir
		}
	}

	Popup PopupProperties campo=Propiedades titulo="Properties"{
		Texto campo=Propiedades.descripcion titulo="Descripción" requerido
		Texto campo=Propiedades.clave titulo="Clave" requerido
	}
    
    Pagina EstadosVisibles titulo="Visibilidad de Estados" perteneceA="Administracion" {
    	Tabla tabladeEstadosSolicitud titulo="Estados de la solicitud visibles para los usuarios" campo=VisibilidadEstadoUsuario popupEditar=PopupVisibilidadDeSolicitudes {
	        Columna campo=VisibilidadEstadoUsuario.estadoInterno titulo="Estado Interno"
	        Columna campo=VisibilidadEstadoUsuario.estadoUsuario titulo="Estado Visible por el Usuario" expandir
	    }
    	
    }
    
    Popup PopupVisibilidadDeSolicitudes campo=VisibilidadEstadoUsuario {
    	Grupo permiso=noEditable borde=false {
	    	Texto campo=VisibilidadEstadoUsuario.estadoInterno titulo="Estado Interno"
	    }
	    Combo comboEstadoInterno campo=VisibilidadEstadoUsuario.estadoUsuario titulo="Estado que ve el usuario" requerido
	}
	
	Pagina Codigos titulo="Códigos de Requerimientos y de Exclusión" perteneceA="Administracion"{
		Wiki{"Para actualizar estos códigos debe dirigirse a la página 'Aed' y actualizar todos los trámites."}
		Tabla tablacodrequerimientos titulo="Códigos de Requerimientos" campo=TiposCodigoRequerimiento popupLeer=PopupVerCodReq{
			Columna titulo="Código" campo=TiposCodigoRequerimiento.codigo
			Columna titulo="Descripcion" campo=TiposCodigoRequerimiento.descripcionCorta
			Columna titulo="Uri Trámite" campo=TiposCodigoRequerimiento.uriTramite
			Columna titulo="Uri Tipo Documento" campo=TiposCodigoRequerimiento.uriTipoDocumento expandir
		}
		Tabla tablacodexclusion titulo="Códigos de Exclusión" campo=TipoCodigoExclusion popupLeer=PopupVerCodExc{
			Columna titulo="Código" campo=TipoCodigoExclusion.codigo
			Columna titulo="Descripcion" campo=TipoCodigoExclusion.descripcionCorta expandir
		}
	} 
	
	Popup PopupVerCodReq campo=TiposCodigoRequerimiento titulo="Codigo de Requerimiento"{
		Texto codigo campo=TiposCodigoRequerimiento.codigo titulo="Código"
		Texto descripcionCorta campo=TiposCodigoRequerimiento.descripcionCorta titulo="Descripción Corta"
		AreaTexto descripcion campo=TiposCodigoRequerimiento.descripcion titulo="Descripción"
		Texto uriTramite campo=TiposCodigoRequerimiento.uriTramite titulo="Uri Trámite"
		Texto uriTipoDocumento campo=TiposCodigoRequerimiento.uriTipoDocumento titulo="Uri Tipo de Documentos"
	}
	
	Popup PopupVerCodExc campo=TipoCodigoExclusion titulo="Codigo de Exclusión"{
		Texto codigo campo=TipoCodigoExclusion.codigo titulo="Código"
		Texto descripcionCorta campo=TipoCodigoExclusion.descripcionCorta titulo="Descripción Corta"
		AreaTexto descripcion campo=TipoCodigoExclusion.descripcion titulo="Descripción"
	}
 
	Pagina BuscarLgs titulo="Búsqueda de Logs" campo=BusquedaLogs noForm  perteneceA="Administracion"{
		Form buscar {
			Wiki {
				"**En esta página podrá decidir como quiere hacer la búsqueda de los Logs, tanto por atributos como por fecha.** </br>"
			}
			
			Check busquedaAtributos titulo="Búsqueda por atributos" campo=BusquedaLogs.buquedaPorAtributos alineadoAIzquierdas
			Grupo mostrarSiCheck busquedaAtributos=true borde=false {
				Fecha titulo="Fecha" campo=BusquedaLogs.fechaLog
				Texto titulo="Tipo" campo=BusquedaLogs.tipoLog
				Texto titulo="Mensaje" campo=BusquedaLogs.mensajeLog
				Texto titulo="Usuario" campo=BusquedaLogs.usuario
				Texto titulo="Clase" campo=BusquedaLogs.claseLog
			}
			
			Wiki {
				"</br>"
			}
		
			Texto titulo="Número de filas que quiere seleccionar: " campo=BusquedaLogs.numeroFilasSeleccionadas
		
			Wiki {
				"</br>"
			}
		
			Boton botonBuscar titulo="Buscar"
		}
	}
	
	Pagina MostrarLogs titulo="Logs" noForm  perteneceA="Administracion"{
		Tabla logs campo=Logs titulo="Logs" {
			Columna titulo="Fecha" campo=Logs.time expandir
			Columna titulo="Tipo" campo=Logs.level expandir
			Columna titulo="Mensaje" campo=Logs.message expandir
			Columna titulo="Usuario" campo=Logs.user expandir
			Columna titulo="Clase" campo=Logs.class_ expandir
		}
	}
	
	Pagina CargarTiposAreasResoluciones titulo="Resoluciones" noForm perteneceA="Administracion" {
		Grupo grpCargarAreas titulo="Cargar Áreas" {
			Form frmCargarAreas {
				Boton cargarAreas titulo="Actualizar"
			}
		}
		Grupo grpCargarTipos titulo="Cargar Tipos" {
			Form frmCargarTipos {
				Boton cargarTipos titulo="Actualizar"
			}
		}
	}

	
	Pagina ConfigurarCertificados titulo="Configurar Certificados" noForm perteneceA="Administracion"{
		Wiki{"Creación de nuevos tipos de certificados o edición de los existentes."}
		Tabla tablaTiposCertificados titulo="Tipos de Certificados" campo=TipoCertificado popup=PopupTipoCertificado{
			Columna titulo="Nombre" campo=TipoCertificado.nombre
			Columna titulo="Descripción" campo=TipoCertificado.descripcion
			Columna titulo="Plantilla" campo=TipoCertificado.nombrePlantilla
		}
	}
	
	Popup PopupTipoCertificado titulo="Tipo de Certificado" campo=TipoCertificado {
		Texto nombre campo=TipoCertificado.nombre titulo="Nombre" requerido
		Texto descripcion campo=TipoCertificado.descripcion titulo="Descripcion"
		Combo tipoDocumento campo=TipoCertificado.tipoDocumento titulo="Tipo de Documento" requerido
		Texto plantilla campo=TipoCertificado.nombrePlantilla titulo="Nombre Plantilla PDF" requerido
		Check firmado campo=TipoCertificado.necesitaFirma titulo="Requiere Firma del Emisor"
		Texto validez campo=TipoCertificado.validez titulo="Periodo de validez (en meses)"
		Fecha inicio campo=TipoCertificado.fechaInicio titulo="Fecha inicio de la emisión"
		Fecha final campo=TipoCertificado.fechaFin titulo="Fecha fin de la emisión"
	}


	
	//Muestra todas las peticiones de cesion de datos realizadas sobre las solicitudes de la aplicacion
	Pagina GenerarFichCesiones titulo= "Peticiones de Cesiones de Datos"{
		Tabla tblCesiones campo=PeticionCesiones titulo="Tabla de Peticiones de Cesiones" paginaCrear=NuevaCesion paginaEditar=EditarCesion paginaLeer=LeerCesion color: porDefecto{
			Columna titulo="Tipo" campo=PeticionCesiones.tipo
			Columna titulo="Fecha Generación" campo=PeticionCesiones.fechaGen
			Columna titulo="Fecha de Validez" campo=PeticionCesiones.fechaValidez
			Columna titulo="Estado" campo=PeticionCesiones.estado
		}
	}
	
	//Nueva petición de cesión de datos
	Pagina NuevaCesion campo=PeticionCesiones titulo="Nueva Petición de Cesión de Datos" noForm{
		Form formTipoCesion{
			Grupo permiso=permisoTipoElegido borde=true titulo="Filtrar expedientes"{
				Combo cmbTipo campo=PeticionCesiones.tipo titulo="Tipo de Cesión" requerido
				Boton btnGuardar titulo="Guardar"
			}
		}

		//Elegir de entre todas las solicitudes a cuales quiero generar la cesion de datos
		Grupo permiso=permisoTipo titulo="Expedientes autorizados"{
			Grupo borde=true titulo="Seleccionar Expedientes"{
				Combo cmbSeleccion campo=PeticionCesiones.seleccion titulo="Tipo de selección"
				Grupo borde=false mostrarSiCombo cmbSeleccion="certificadoFecha"{
				Fecha fechaLimite campo=PeticionCesiones.fechaCorte titulo="Fecha límite"
			} 
			Enlace seleccion titulo="Seleccionar" estilo="btn" codigojs="" //Sobreescrito
			}
			Tabla tblSolicitudes campo=SolicitudGenerica seleccionable="Generar Peticion" {
				Columna titulo="ID" campo=SolicitudGenerica.id
				Columna titulo="Expediente" campo=SolicitudGenerica.expedienteAed.idAed
				Columna titulo="Estado Interno" campo=SolicitudGenerica.estadoValue
				Columna titulo="Estado" campo=SolicitudGenerica.estado
				Columna titulo="Estado Usuario" campo=SolicitudGenerica.estadoUsuario
				Columna titulo="ID Solicitante" campo=SolicitudGenerica.solicitante.id
				Columna titulo="Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto
			}
		}
	}
	
	Pagina LeerCesion campo=PeticionCesiones titulo="Petición de Cesión de Datos"{
		Grupo titulo = "Petición de Cesión de Datos" permiso=noEditable{
			Combo cmbTipo campo=PeticionCesiones.tipo titulo="Tipo de Cesión"
			Grupo grupoFicheros titulo="Ficheros disponibles"{
				Grupo borde=false permiso=verPeticionCesion{
					Enlace descargarFicheroP campo=PeticionCesiones.fichPeticion.urlDescarga titulo="Descargar fichero de petición" destino="_blank"
				}
				Grupo borde=false permiso=verRespuestaCesion{
					Enlace descargarFicheroR campo=PeticionCesiones.fichRespuesta.urlDescarga titulo="Descargar fichero de respuesta" destino="_blank"
				}
			}
		}
		Enlace enlaceVolver titulo="Volver" pagina=GenerarFichCesiones estilo="btn"
	}
	
	Pagina EditarCesion campo=PeticionCesiones permiso=ultimaEditable noForm titulo="Petición de Cesión de Datos"{
		Accion editar redirigir=anterior
			Grupo titulo="Datos de cesion: Descargar fichero de petición" { 
				Enlace descargarFichero campo=PeticionCesiones.fichPeticion.urlDescarga titulo="Descargar" destino="_blank" estilo="btn"
			}
		Grupo titulo="Datos de Cesión: subir fichero de respuesta"{
			Form formCesionFichSubida{
					Grupo borde=false permiso=tipoAsignado{
						Grupo borde=false permiso=noEditable{
							Combo cmbTipo campo=PeticionCesiones.tipo titulo="Tipo de Cesión"
						}
						Boton btnGuardar titulo="Guardar" 
					}
				}
		
			Form formCesionFichRespuesta{
				Grupo borde=false permiso=responderPeticion{
					SubirArchivo subirArchivo campo=PeticionCesiones.fichRespuesta mimes="text/plain"
					Boton tratarFich titulo="Subir fichero de respuesta"
				}
			}
			
			Form formCesionAplicarCambios{
				Grupo borde=false permiso=aplicarCambios {
					Enlace verFichero campo=PeticionCesiones.fichRespuesta.urlDescarga titulo="Ver el fichero subido" destino="_blank" estilo="btn"
					Boton cambiarFichero titulo="Subir otro documento de respuesta"
					Boton aplicarCambios titulo="Aplicar cambios"
				}
			}
			Grupo permiso=cesionTerminada titulo="Cesion Finalizada"{
				Grupo borde=false permiso=noEditable{
					Combo cmbTipoFin campo=PeticionCesiones.tipo titulo="Tipo de Cesión"
				}
			}
		}
		//Boton btnVolver titulo="Volver"
		Enlace enlaceVolver titulo="Volver" pagina=GenerarFichCesiones estilo="btn"
	}

}
