// La entidad "TableKeyValue" la utilizaremos como tabla de tablas.
// Básicamnte para guardar las listas con clave-valor.
Entidad TableKeyValue noAuditable{
	String table column="t"
	String key column="k"
	String value column="v"
}

// La entidad "TableKeyValueDependency" la utilizaremos como tabla de tablas de las listas con dependencias.
// Básicamnte para guardar las listas con clave-valor-dependencia, para los combos dependientes.
Entidad TableKeyValueDependency noAuditable{
	String table column="t"
	String dependency column="d"
	String key column="k"
}

// Entidad de la que deberán extender las entidades únicas en la aplicación.
// Es decir, una única entidad para toda la aplicación.
Entidad Singleton superClass {
}

// Incluye lo básico que debería tener una solicitud. Deberíamos crear una entidad Solicitud,
// que extienda de esta, y añadirle lo que se quiera. Así, por defecto, la entidad Solicitud
// tendrá todos éstos campos por defecto.
Entidad SolicitudGenerica tableName = "solicitud" auditable{
	String estado
	Lista estadosSolicitud estadoValue transient
	Lista estadosSolicitud estadoUsuario transient
	Solicitante solicitante
	Documentacion documentacion
	Documentacion documentacionProceso
	Documentacion documentacionAportada
	Registro registro
	ExpedientePlatino expedientePlatino
	ExpedienteAed expedienteAed
	Aportaciones aportaciones
	Verificacion verificacion
	
	//Criterios economicos que rellena el solicitante
	ManyToMany CEconomico ceconomicos	
}

// Dátos básicos de un Solicitante (extiende de Persona, y por lo tanto tendrá también los atributos que tenga esa entidad)
Entidad Solicitante extends Persona auditable{
	Direccion domicilio
	String telefonoFijo
	String telefonoMovil
	String fax
	Email email
	String web
	// Es el solicitante el que puede tener representantes. Según el tipo de solicitante puede tener 1, o varios representantes
	Boolean representado
	RepresentantePersonaFisica representante
	OneToMany RepresentantePersonaJuridica representantes
	Boolean autorizaFuncionario
}

Entidad FuncionariosHabilitados extends Singleton {
	String texto
//	OneToMany Firmante firmantes transient
}

// Agentes que pueden acceder a la aplicación
Entidad Agente inheritance=joined auditable {
	String username
	String password
	Email email
	String name
	Lista multiple roles roles
	Lista roles rolActivo
	String acceso
	Boolean funcionario
}

// Entidad que contiene los campos necesarios para una dirección. Se establece como "embeddable", lo que indica
// que en cada entidad que incluya no creará una relación a una tabla dirección, sino que mapeará sus campos.
Entidad Direccion embeddable auditable{
	String calle
	String numero
	String otros
	String codigoPostal
	Lista municipios municipio
	Lista provincias provincia
	Lista paises pais
}

// Entidad que representa un Documento y todo lo necesario para representarlo
Entidad Documento {
	// uri del documento en el AED
	String uri
	Lista tiposDocumentos tipo
	String descripcion
	Boolean clasificado
	String hash
	DateTime fechaSubida
	DateTime fechaRegistro
	String urlDescarga transient
}

// La entidad Documentación agrupa aportaciones de documentos
Entidad Documentacion {
	// Lista de documentos
	OneToMany Documento documentos
	
	String uriDocOficial
	String urlDocOficial
	Boolean docOficialClasificado
	DateTime fechaFirma
	String firma transient
}

Entidad ObligatoriedadDocumentos extends Singleton {
	List<String> imprescindibles
	List<String> obligatorias
	List<String> automaticas
	List<String> manuales
}

// Representación de una persona (tanto física como jurídica)
Entidad Persona inheritance=joined auditable{
	PersonaFisica fisica
	PersonaJuridica juridica
	Lista TipoDePersona tipo
	String numeroId transient
	String nombreCompleto transient
}

// Representación de una persona física
Entidad PersonaFisica inheritance=joined auditable{
	String nombreCompleto transient
 	String numeroId transient
	String nombre
	String primerApellido
	String segundoApellido
	Nip nip
}

// Representación de una persona jurídica
Entidad PersonaJuridica inheritance=joined auditable{
	String entidad
	Cif cif
	Email email
	String telefonoFijo
	String telefonoMovil
	DateTime fechaFirma
}

Entidad RepresentantePersonaFisica extends Persona auditable{
	String telefonoFijo
	String telefonoMovil
	String fax
	Email email
}

// Un representante de persona jurídica además de lo que tiene un representante de persona física,
// tiene un tipo de representación
Entidad RepresentantePersonaJuridica extends RepresentantePersonaFisica auditable{
	Lista TiposRepresentantes tipoRepresentacion
}

// Representación de NIP
Entidad Nip embeddable auditable{
	String tipo
	String valor
}

// Participación de los agentes en las solicitudes.
// Indica de que forma participan los agentes de la aplicación (usuarios) en cada una de las solicitudes
Entidad Participacion auditable{
	ManyToOne Agente agente noConstruct
	ManyToOne SolicitudGenerica solicitud noConstruct
	Lista TiposParticipacion tipo
}

// Entidad sigleton que se utilizará para realizar unos u otros trabajos cada cierto tiempo
Entidad Quartz extends Singleton{
	Boolean execute
	Boolean mostrarTodasSolicitudes
	Boolean mostrarSolicitudesRequeridas
	Boolean ejecutarCambioDeFecha
	Boolean cambiarEstadoPlazoVencido
	Boolean sendMail
	String texto
}

// Contien información de registro de la solicitud en Platino
Entidad Registro {
	// Borrador de la solicitud
	Documento borrador
	// Documento oficial de la solicitud
	Documento oficial
	// Justificante del registro de la solicitud
	Documento justificante
	Documento autorizacionFuncionario
	
	InformacionRegistro informacionRegistro
	
	// Fase del registro en la que se encuentra
	FasesRegistro fasesRegistro
	
	//En el caso de que firme una persona jurídica
	//Se almacena si se firma con el certificado de empresa
	//o firman los representantes
	Lista tipoFirmaJuridica tipoFirma 
	 
	//Persona que han firmado o deben firmar	
	OneToMany Firmante firmantes
}

// Representación de firmantes
Entidad Firmante auditable{
	String nombre
	String idtipo
	String idvalor
	DateTime fechaFirma
	String tipo // fisica, juridica, representante
	String cardinalidad // unico, multiple
}

// Información del registro
Entidad InformacionRegistro auditable{
	DateTime fechaRegistro
	String unidadOrganica
	String numeroRegistro
	String numeroRegistroGeneral
}

// Representamos las fases del registro de una solicitud, para conocer por cual va,
// y no repetir fases.
Entidad FasesRegistro {
	Boolean borrador
	Boolean firmada
	Boolean expedientePlatino
	Boolean registro
	Boolean expedienteAed
	Boolean clasificarAed
	
	// Probando
	String fase
}

// Representación del expediente en platino
Entidad ExpedientePlatino auditable{
	String uri
	String numero
	DateTime fechaApertura
	Boolean creado
	String ruta	
}

// Representación del expediente en el AED
Entidad ExpedienteAed auditable{
	String idAed	
}

// Representación de las aportaciones de documentos actual y las ya registradas
Entidad Aportaciones {
	Aportacion actual
	OneToMany Aportacion registradas
}

// Entidad que representa una aportación de documentación realizada en un expediente
Entidad Aportacion {
	String estado
	OneToMany Documento documentos
	
	InformacionRegistro informacionRegistro
	/* Cuando aportamos sin registro, se establece ésta fecha */
	DateTime fechaAportacionSinRegistro
	
	Documento borrador
	Documento oficial
	Documento justificante
}

// Guardamos las incidencias realizadas
Entidad Incidencia {
    Email email
    String fecha
    String telefono
    String nombre
    String apellidos
    String asunto
    LongText texto
    Boolean enviada
}

Entidad SemillaExpediente extends Singleton{
	Long semilla
}


// Plantillas de mails automáticos a enviar
Entidad Mail tableName = "email" {
	String idMail
	String bcc
	String cc
	LongText content
	String footer
	String sendTo
	String sender
	String subject
}

Entidad Log nonPersist {
	String time
	String level
	String class_
	String user
	String message
	String trace
}

// Entidad que permite la verificación de documentación
Entidad Verificacion {
	String uriVerificacion
	String uriProcedimiento
	String uriTramite
	String expediente
	String estado
		
	OneToMany VerificacionDocumento documentos transient
		
	String uriExclusion
	String motivoExclusion
	OneToMany Exclusion codigosExclusion transient
		 
	OneToOne Requerimiento requerimientoProceso
	OneToMany Requerimiento requerimientos
		
	DateTime fechaCreacion
	DateTime fechaUltimaActualizacion	
}

// Permite controlar el documento de la verificación, y el estado de la misma
Entidad VerificacionDocumento {
	String uriDocumentoVerificacion
	String uriDocumento
		
	String uriTipoDocumento
	String etiquetaTipoDocumento
	String descripcion
		
	String estadoDocumentoVerificacion
	DateTime fechaPresentacion
	String identificadorMultiple
	Integer version

	LongText motivoRequerimiento
	OneToMany CodigoRequerimiento codigosRequerimiento
		
	//Si existe permite ver el documento
	Boolean existe
}

// Códigos de requerimiento utilizados en la verificación
Entidad CodigoRequerimiento {
	String codigo
	LongText descripcion
	String descripcionCorta
}

// Códigos de exclusión
Entidad Exclusion {
	String codigo
	LongText descripcion
	String descripcionCorta
}

Entidad Requerimiento auditable{
	String firma transient					//Guarda firma del requerimiento
	String urlDocRequerimiento transient		//Guarda url del doc oficial, publicado en el servlet
	String estado 					 	//Rellenar a mano
		
	LongText motivo
		
	String firmante							//ver como enlazarlo con usuarios
		
	DateTime fechaRegistroSalida
	String numeroRegistroSalida
	String numeroGeneralRegistroSalida
	String oficinaRegistroSalida
	DateTime fechaAcuse
	
	Documento oficial			// documento de requerimiento oficial, este es el que se firma
	Documento justificante		// justificante del registro de salida
	
//	String uriDocRequerimiento				//uri del documento de requerimiento oficial, este es el que se firma
//	Boolean docRequerimientoClasificado
//	String urlDescargaDocRequerimiento transient
//	String uriDocJustificanteRequerimiento	//uri del jsutificante del registro de salida
//	Boolean docJustificanteRequerimientoClasificado
//	String urlDescargaJustificanteRequerimiento transient	
	String uriRequerimiento					//uri del requerimiento (identificador del servicio verificacion)

	//Estos campos estan añadidos a partir del wsdl
	DateTime fechaAcceso		  //fecha de cuando el usuario entra en la sede por primera vez desde el requerimiento 
	DateTime fechaNotificacion  //fecha del registro de salida
	DateTime fechaVencimiento   //fecha calculada justo cuando se hace la notificacion fechaAcceso + 10N + 10H. Recalculada cuando el usuario entra en la sede por
										  // primera vez fechaAcceso + 10H. 
	Boolean accesoaSede

	DateTime fechaDisposicionSede
	DateTime fechaAcuseSede
	DateTime fechaRechazoSede
	Boolean postal
	DateTime fechaEnvioPostal
	DateTime fechaAcusePostal
}

Entidad Consulta {
	LongText descripcion
	LongText consulta
	Lista tipoConsulta tipo
}

Entidad Propiedades {
	String descripcion
	String clave
	String valor transient
}

// Estado de la convocatoria en la aplicación
Entidad Convocatoria extends Singleton {
	Lista estadoConvocatoria estado default = "presentacion"
}

Entidad TipoDocumento {
	String uri
	String nombre
	String aportadoPor
	String obligatoriedad
}

Entidad Tramite {
	String uri
	String nombre
	OneToMany TipoDocumento documentos	
}