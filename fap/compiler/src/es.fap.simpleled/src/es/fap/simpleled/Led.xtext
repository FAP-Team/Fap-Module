grammar es.fap.simpleled.Led with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate led "http://www.fap.es/simpleled/Led"

Model:
	(
		(entidades += Entity) |
	 	(formularios += Formulario) |
	 	(listas += Lista) |
	 	(permisos += Permiso) |
	 	(servicioWeb += ServicioWeb)
	 ) *
;

QN: // Qualified Name
	IDS ('.' IDS)*
;

IDS:  
	ID | "Solicitud" | "Nip" | "Direccion" | "DireccionMapa" | "Persona" | "PersonaFisica" |
	"PersonaJuridica" | "provincia" | "pais" | "valor" | "Solicitante" |
	"agente" | "documento" | "password" | "aportadoPor" | "texto" | "isla" | "CCC" | "sexo"
;

Entity:
	elemento='Entidad' name=IDS
		(
		("extends" extends=[Entity|IDS])?
		& ('tableName' '=' tableName=STRING)? 
		& (nonPersist?="nonPersist")?
		& (embedded?="embeddable")?
		& (inheritance?="inheritance" "=" "joined")?
		& (superClass?="superClass")?
		& (noConstruct?="noConstruct")?
		& (auditable?="auditable" | noAuditable?="noAuditable")?
		)
	'{'
		attributes+=Attribute*
	'}'
;

Attribute:
	type=Type name=IDS
		(
		("default" "=" defaultValue=STRING)?
		& (isTransient?="transient")?
		& (hasLength?="length" "=" length=INT)?
		& (required?="requerido")?	// NOTA: No se comprobar√° en la Entidad, sino en los "validators"
		& (noConstruct?="noConstruct")? // No crea el new para ese atributo
		//& (noCascade?="noCascade")?
		& ("cascadeType" "=" cascade=CascadeType)?
		& ("checkWith" "=" checkWith=STRING)? // Manual validator
		& ("column" "=" column=STRING)?
		& ("validate" "=" validate=STRING)?  // validacion por regexp
		)
;

CascadeType:
	type = ("ALL" | "NONE") | simpleType = CascadeListSimpleType
;

CascadeListSimpleType:
	list += CascadeSimpleType+
;

CascadeSimpleType:
	"PERSIST" | "DETACH" | "MERGE" | "REFRESH" | "REMOVE"
;

Type:
	simple=SimpleType | special=SpecialType | compound=CompoundType
;

SimpleType:
	type = ("String" | "Long" | "Integer" | "Boolean" | "Double" | "LongText" | "boolean")
;

SpecialType:
	type = ("Cif" | "Telefono" | "Email" | "Moneda" | "DateTime")
;

CompoundType:
	((tipoReferencia=AttributeReferences)? entidad=[Entity|IDS])
	| ('Lista' (multiple?="multiple")? lista=[Lista|IDS])
	| (collectionType=tipoColeccion "<" collectionReferencia=SimpleType ">")
;

tipoColeccion:
	type= ("Set" | "List")
;	

AttributeReferences:
	type= ("OneToOne" | "OneToMany" | "ManyToOne" | "ManyToMany")
; 

Formulario:
	elemento='Formulario' name=IDS
	(
		("campo" "=" campo=Campo)?
		& ("permiso" "=" permiso=[Permiso])?
		& (inicial?="inicial")?
	)
	'{'
		(menu = Menu)?
		(paginas += Pagina  |  popups += Popup)*
	'}'
;

Menu:
	elemento='Menu' '{'
		elementos +=MenuElemento+
	'}'
;

MenuElemento:
	MenuGrupo | MenuEnlace
;	

MenuGrupo:
	elemento='Grupo' titulo=STRING 
	("permiso" "=" permiso=[Permiso])?  '{'
		elementos +=MenuElemento*
	'}'
;

MenuEnlace:
	elemento='Enlace' titulo=STRING
	(
		("permiso" "=" permiso=[Permiso])?
		& (
			('pagina' '=' pagina=PaginaAccion) |
			('popup' '=' popup=PopupAccion) |
			('accion' '=' (accion=STRING | accionLogout?="logout" | accionLogin?="login")) |
			('url' '=' url=STRING) |
			(anterior?="anterior")
		)?
	)
;

PaginaAccion:
	pagina=[Pagina|IDS] accion=("leer" | "crear" | "editar" | "borrar")?
;

PopupAccion:
	popup=[Popup|IDS] accion=("leer" | "crear" | "editar" | "borrar")?
;

Accion:
	elemento="Accion" name=("crear"  | "editar" | "borrar")
	(
		("boton" "=" boton=STRING)?
		& ('redirigir' '=' (anterior?="anterior" | redirigir=PaginaAccion | redirigirUrl=STRING))?
		& ("mensajeOk" "=" mensajeOk=STRING)?
		& (noBorrarEntidad?='noBorrarEntidad')?
		& (crearSiempre?="crearSiempre")?
	)
;

Pagina:
	elemento='Pagina' name=IDS
	(
		("titulo" "=" titulo=STRING)?
		& ("campo" "=" campo=Campo)?
		& ("permiso" "=" permiso=[Permiso])?
		& (noForm?='noForm')?
		& (noAutenticar?='noAutenticar')?
		& ((guardarParaPreparar?="guardarParaPreparar") | (guardarParaPrepararCopia?="guardarParaPrepararCopia"))?
		& (inicial?="inicial")?
		& (copia?="copia")?
		& (mensajeFinal?="mensajeFinal")?
		& ("perteneceA" "=" perteneceA=STRING)?
	)
	'{'
		elementos+=Elemento*
	'}'
;

Popup:
	elemento='Popup' name=ID
	(
		('titulo' '=' titulo=STRING)?
		& ("campo" "=" campo=Campo)?
		& ("permiso" "=" permiso=[Permiso])?
		& (copia?="copia")?
		& (maximizar?="maximizar")?
	)
	'{'
		elementos+=Elemento*
	'}'
;

Grupo:
	elemento='Grupo' (name=ID)? (
		('titulo' '=' titulo=STRING)? &
		(
			('mostrarSiCombo' siCombo=[Combo|QN] signoSiCombo=GrupoComboSigno siComboValues=Values) |
			('mostrarSiCheck' siCheck=[Check|QN] '=' siCheckValues=("true" | "false")) |
			('mostrarSiRadioBooleano' siRadioBooleano=[RadioBooleano|QN] '=' siRadioBooleanoValues=("true" | "false")) |
			('mostrarSiCampo' campo=Campo signoSiCampo=GrupoComboSigno siCampoValues=Values) |
			('mostrarSiExpresion' siExpresion=STRING)
		)? &
		('borde' '=' borde=("true" | "false"))? &
		('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)? &
		("permiso" "=" permiso=[Permiso])? &
		('enviarCamposOcultos' '=' (sendHiddenFieldsAlways?='true' | sendHiddenFieldsNever?='false'))?
	)
	'{'
		elementos+=Elemento*
	'}'
;	

GrupoComboSigno:
	'=' | '!='
;

AgruparCampos:
	elemento='AgruparCampos' '{'
		elementos+=Elemento*
	'}'
;	

Values:
	values+=STRING (',' values+=STRING)*
;

Elemento:
	Grupo | AgruparCampos | 
	Texto | Fecha | Combo | Tabla | Boton | Check | AgrupaBotones | RadioBooleano | BarraDeslizante |
	Nip | Solicitante | PersonaFisica | PersonaJuridica | Persona  | Direccion | DireccionMapa | Wiki |
	AreaTexto | Enlace | Form |
	SubirArchivo | EditarArchivo | FirmaSimple | FirmaMultiple |
	EntidadAutomatica | Accion | Codigo | CCC | VisorFactura | SubirFactura | EditarFactura | TextoOculto |
	GrupoRadioButtons
;

Campo:
	(entidad=[Entity|IDS] (atributos=CampoAtributos)?)
	| method=STRING
;

CampoAtributos:
	"." atributo=[Attribute|IDS] (atributos=CampoAtributos)?
;

TipoDeAyuda:
	'(' type=('texto' | 'popover') ')'
;

Texto:
	elemento='Texto' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('ancho' '=' ancho=STRING)?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& (requerido?='requerido')?
		& (duplicar?='duplicar')?
		& (password?='password')?
	)
;

TextoOculto:
	elemento='TextoOculto' (name=ID)? (
		('campo' '=' campo=Campo)
	)
;

AreaTexto:
	elemento='AreaTexto' (name=ID)? (
		  ('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('valor' '=' valor=STRING)?
		& ('filas' '=' filas=INT)?
		& ('ancho' '=' ancho=STRING)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& (requerido?='requerido')?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
	)
;

Check:
	elemento='Check' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& (alineadoAIzquierdas?='alineadoAIzquierdas')?
	)
;

GrupoRadioButtons:
	elemento='GrupoRadioButtons' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('valor' '=' valor=ID)?
		& ('titulo' '=' titulo=STRING)?
		& ('grupoTexto' '=' grupoTexto=[Attribute|IDS])?
		& (requerido ?= 'requerido')?
	)
	('{'
		radios+=RadioButton*
	'}')?
	
	
;

RadioButton:
	elemento='RadioButton' (name=ID)? (
		('titulo' '=' titulo=STRING)?
		('valor' '=' valor=STRING)
	)
;

RadioBooleano:
	elemento='RadioBooleano' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& ('botonIzquierdo' '=' botonIzquierdo=STRING)?
		& ('botonDerecho' '=' botonDerecho=STRING)?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& (alineadoAIzquierdas?='alineadoAIzquierdas')?
	)
;	

BarraDeslizante:
	elemento='BarraDeslizante' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('ancho' '=' ancho=STRING)?
		& (min?='minimo' '=' minimo=INT)?
		& (max?='maximo' '=' maximo=INT)?
		& (defecto?='valorDefecto' '=' valorDefecto=INT)?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& (alineadoAIzquierdas?='alineadoAIzquierdas')?
	)
;	

Enlace:
	elemento='Enlace' (name=ID)? (
		(('pagina' '=' pagina=PaginaAccion) | ('popup' '=' popup=PopupAccion) | ('campo' '=' campo=Campo) | ('url' '=' url=STRING) | (anterior?="anterior") | ('codigojs' '=' codigojs=STRING)) 
		& ('titulo' '=' titulo=STRING)?
		& ('destino' '=' destino=STRING)?
		& ('estilo' '=' estilo=STRING)?
	)
;

Wiki:
	elemento='Wiki' ('estilo' '=' estilo=STRING)? '{'
		wikiData+=STRING+
	'}'
;

AgrupaBotones:
	'AgrupaBotones' (type=('normal' | 'well'))?
	'{'
		(botones+=(Boton|Enlace))+
	'}'
;

Boton:
	elemento='Boton' (name=ID) (
	 	('titulo' '=' titulo=STRING)
	    & ('ancho' '=' ancho=STRING)? 
	    & ('pagina' '=' pagina=PaginaAccion)?
	    & ('popup' '=' popup=PopupAccion)?
	    & (anterior?="anterior")?
		& (waitPopup?="waitPopup")?	
		& (refrescar?="refrescar")?	
		& ('type' '=' type=('primary' | 'info' | 'success' | 'danger' | 'warning' | 'inverse'))?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& (claveIdFijo?="claveIdFijo")?	
		& (noSubmit?="noSubmit")?
	)
;

Fecha:
	elemento='Fecha' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& (requerido?='requerido')?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
	)
;

Combo:
	elemento='Combo' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& (requerido?='requerido')?
		& (busqueda?="busqueda")?
		& (mostrarClave?="mostrarClave")?
		& ('ancho' '=' ancho=STRING)? 
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& ('comboTexto' '=' comboTexto=[Attribute|IDS])? 
		& ('comboValor' '=' comboValor=[Attribute|IDS])? 
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& ('dependeDe' '=' dependeDe=[Combo])?
		& (ordenar = ('ordenarPorClave' | 'ordenarPorValor'))?
		& (eliminarDuplicados?="eliminarDuplicados")?
	)
;

Form:
	elemento='Form' (name=ID) (
		(autoEnviar?="autoEnviar")?
		& ("permiso" "=" permiso=[Permiso])?
		& ('redirigir' '=' redirigir=PaginaAccion)?
		& ('destino' '=' destino=STRING)?
	)
	'{'
		elementos+=Elemento+
	'}'
;	

FirmaMultiple:
	elemento='FirmaMultiple' name=ID
	(
		(documentos=FirmaDocumentos)
		& ('titulo' '=' titulo=STRING)?
        & ('alto' '=' alto=STRING)? 
        & ("permisoFilas" "=" permiso=[Permiso])?
		& (recargarPagina?="recargarPagina")?
		& ('metodoFilas' "=" metodoFilas=STRING)?
	   	& ('paginaLeer' '=' paginaLeer=[Pagina|IDS])?
	   	& ('nombreBotonVer' '=' nombreBotonVer=STRING)?
	   	& ('paginaCrear' '=' paginaCrear=[Pagina|IDS])?
   	    & ('popup' '=' popup=[Popup|IDS])?
   	    & ('popupLeer' '=' popupLeer=[Popup|IDS])?    
	   	& ('popupCrear' '=' popupCrear=[Popup|IDS])?
	   	& ('popupEditar' '=' popupEditar=[Popup|IDS])?
	   	& ('popupBorrar' '=' popupBorrar=[Popup|IDS])?
	)
	'{'
		columnas += Columna*
	'}'
;

FirmaDocumentos:
	"campo" "=" campo=Campo	
;

Tabla:
	elemento='Tabla' name=ID
	(
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
        & ('alto' '=' alto=STRING)? 
        & (columnasAutomaticas?="columnasAutomaticas" (('exclude' '=' exclude=ListaAtributos) | ('include' '=' include=ListaAtributos))?)?
        & ("permisoFilas" "=" permiso=[Permiso])?
		& (recargarPagina?="recargarPagina")?
		& ('seleccionable' "=" seleccionable=STRING & (mostrarTablaSeleccionados?='mostrarTablaSeleccionados')?)?
		& ('metodoFilas' "=" metodoFilas=STRING)?
	    & ('popup' '=' popup=[Popup|IDS])?
	    & ('popupCrear' '=' popupCrear=[Popup|IDS])?
	   	& ('popupBorrar' '=' popupBorrar=[Popup|IDS])?
	   	& ('popupEditar' '=' popupEditar=[Popup|IDS])?
	   	& ('popupLeer' '=' popupLeer=[Popup|IDS])?
	   	& ('pagina' '=' pagina=[Pagina|IDS])?
	    & ('paginaCrear' '=' paginaCrear=[Pagina|IDS])?
	   	& ('paginaBorrar' '=' paginaBorrar=[Pagina|IDS])?
	   	& ('paginaEditar' '=' paginaEditar=[Pagina|IDS])?
	   	& ('paginaLeer' '=' paginaLeer=[Pagina|IDS])?
	   	& (color=Color)?
	   	& ('nombreBotonEditar' '=' nombreBotonEditar=STRING)?
	   	& ('nombreBotonVer' '=' nombreBotonVer=STRING)?
	   	& ('nombreBotonBorrar' '=' nombreBotonBorrar=STRING)?
	   	& ('nombreBotonCrear' '=' nombreBotonCrear=STRING)?
	)
	'{'
		columnas += Columna*
	'}'
;

ListaAtributos:
	atributos+=[Attribute|IDS] (',' atributos+= [Attribute|IDS])*
;

Columna:
	elemento='Columna' ( 
	       ( ('campo' '=' campo=Campo) | ('funcion' '=' funcion=STRING) | ('funcionRaw' '=' funcionRaw=STRING) )
	       & ('titulo' '=' titulo=STRING)? 
	       & ('ancho' '=' ancho=STRING)? 
		   & ("permiso" "=" permiso=[Permiso])?
	       & (expandir?='expandir')?
	       & ("align" "=" position=("left"|"center"|"right"))?
	)
;

SubirArchivo:
	elemento='SubirArchivo' name=ID (
		('campo' '=' campo=Campo)
		& (requerido?='requerido')?
		& ('tramite' '=' tramite=STRING)?		
		& ('aportadoPor' '=' aportadoPor=STRING)?
		& (
			('mimes' '=' mimes+=STRING ("," mimes+=STRING)*) |
			('extensiones' '=' extensiones+=STRING ("," extensiones+=STRING)*)
		)?
		& (listarDocumentosSubidos?='listarDocumentosSubidos')?
	)
;

EditarArchivo:
	elemento='EditarArchivo' name=ID (
		('campo' '=' campo=Campo)
		& (requerido?='requerido')?
		& ('tramite' '=' tramite=STRING)?		
		& ('aportadoPor' '=' aportadoPor=STRING)?
	)
;

FirmaSimple:
	elemento="FirmaSimple" (name=ID)
	(
		("titulo" "=" titulo=STRING)
		& (documento=FirmaDocumento)
		& (firmantes=FirmaFirmantes)
		& (registroFirma=FirmaRegistro)?
		& (calcularFirmantes=CalcularFirmantes)
		& (setToTrue=FirmaSetTrue)?
	)
	(
		"setCamposTo" '{'
			setCampos+=FirmaSetCampo
			(',' setCampos+=FirmaSetCampo)*
		'}'
	)?
;

FirmaDocumento:
	"documento" "=" campo=Campo	
;

FirmaRegistro:
	"registroFirma" "=" campo=Campo	
;

FirmaFirmantes:
	"listaFirmantes" "=" campo=Campo
;

CalcularFirmantes:
	"calcularFirmantes" "=" campo=Campo
;

FirmaSetCampo:
	campo=Campo "to" value=STRING
;

FirmaSetTrue:
	"setTrue" "=" campo=Campo
;

Direccion:
	elemento=("Direccion" | "DireccionCanaria" |"DireccionNacional" |"DireccionInternacional") (name=ID)? (
		('campo' '=' campo=Campo)
		& ("titulo" "=" titulo=STRING)
		& (requerido?="requerido")?
	    & ('ancho' '=' ancho=STRING)?
	)
;

DireccionMapa:
	elemento=("DireccionMapa") (name=ID)? (
		('campo' '=' campo=Campo)
		& ("titulo" "=" titulo=STRING)
		& (requerido?="requerido")?
	    & ('ancho' '=' ancho=STRING)?
	)
;

Nip:
	elemento='Nip' (name=ID)? (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING) &
		(requerido?='requerido')?
	)
;

CCC:
	elemento='CCC' (name=ID)? (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING) &
		(requerido?='requerido')? &
		('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
	)
;

PersonaFisica:
	elemento='PersonaFisica' (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING)? &
		(requerido?='requerido')? &
		(setearTipoPadre?='setearTipoPadre')?
		& (noSexo?='noSexo' | sexo?='sexo')?
		& (noNacimiento?='noNacimiento' | nacimiento?='nacimiento' )?
	)
;

PersonaJuridica:
	elemento='PersonaJuridica' (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING) &
		(requerido?='requerido')? &
		("permiso" "=" permiso=[Permiso])?
	)
;

Persona:
	elemento='Persona' (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING)? &
		(requerido?='requerido')? &
		("permiso" "=" permiso=[Permiso])?
		& (noSexo?='noSexoPersonaFisica' | sexo?='sexoPersonaFisica')?
		& (noNacimiento?='noNacimientoPersonaFisica' | nacimiento?='nacimientoPersonaFisica')?
	)
;

Solicitante:
	elemento=('Solicitante' | 'SolicitantePersonaFisica' | 'SolicitantePersonaJuridica') (name=ID) (
		('campo' '=' campo=Campo) 
		& ('titulo' '=' titulo=STRING)?
		& (requerido?='requerido')?
		& ("permiso" "=" permiso=[Permiso])?
		& ((noRepresentante?='noRepresentante') | (representantePersonaFisica?='representantePersonaFisica'))?
		& (conDireccion?='conDireccion')?
		& (noEmail?='noEmail')?
		& (noSexo?='noSexo' | sexo?='sexo')?
		& (noNacimiento?='noNacimiento' | nacimiento?='nacimiento')?
		& (noTelefono?='noTelefono')?
	)
;

EntidadAutomatica:
	elemento='EntidadAutomatica' (name=ID) (	
		('campo' '=' campo=Campo) & 
		("permiso" "=" permiso=[Permiso])?
	)
;

Lista:
	elemento='Lista' name=IDS (enumerado?='crearEnumerado')? &
	(ocultar?='ocultar')?
	'{'
		elementos+=ElementoLista*
	'}'
;

ElementoLista:
	value=STRING |
	key=ElementoListaKey (':' value=STRING)? |
	key=ElementoListaKey ':' '{'
		elementosDependientes+=ElementoListaDependiente*
	'}'
;

ElementoListaDependiente:
	value=STRING |
	key=ElementoListaKey (':' value=STRING)?
;

ElementoListaKey:
	first=IdOrInt (("." resto+=IdOrInt)*)
;

IdOrInt:
	ID | INT | NEG_INT
;

Permiso:
	elemento='Permiso' name=ID "{"
		('mensaje' '=' mensaje=STRING)?
		(varSection = PermisoVarsSection)?
		(
			("return" ret = PermisoReturn) 	
			|
			(
				whens += PermisoWhen+
				("else" "return" else=PermisoReturn)? 			
			)
		)
	"}"
;

PermisoWhen:
	'when'
		rule = PermisoRule
	'return'
		ret = PermisoReturn
;

PermisoReturn:
	denegar?= "denegar" |
	(all?= "all" (":" grafico=Grafico)?) | 
	(pares += AccionesGrafico ("," pares += AccionesGrafico)*) |
	grafico=Grafico
;

AccionesGrafico:
	acciones=Acciones (":" grafico=Grafico)?
;

Acciones:
	(multiple?= "[" (acciones+="leer"? & acciones+="editar"? & acciones+="crear"? & acciones+="borrar"?) "]") |
	(acciones+="leer" | acciones+="editar" | acciones+="crear" | acciones+="borrar")
;

Grafico:
	permiso=("editable" | "visible" | "oculto")
;

PermisoVarsSection:
	'vars'
		vars+=PermisoVar+
;

PermisoVar:
	tipo=[Entity|IDS] name=IDS
		(':' 'sql' '(' sql=STRING (',' sqlParams=PermisoVarSqlParameters)? ')') ?
;

PermisoVarSqlParameters:
	sqlParams+=CampoPermiso (',' sqlParams+=CampoPermiso)*
;

CampoPermiso:
	(variable=[PermisoVar] | action?="_accion" | agente?="agente") ( nullCheck?="?"? atributos=CampoPermisoAtributos)?
;

CampoPermisoAtributos:
	"." atributo=[Attribute|IDS] (nullCheck?="?"? atributos=CampoPermisoAtributos)?
;

PermisoRule:
	PermisoRuleOr
;	

PermisoRuleOr returns PermisoRule:
	PermisoRuleAnd ({PermisoRuleOr.left=current} 'or' right=PermisoRuleAnd)*
;

PermisoRuleAnd returns PermisoRule:
	PermisoPrimary ({PermisoRuleAnd.left=current} 'and' right=PermisoPrimary)*
;

PermisoPrimary returns PermisoRule:
	PermisoRuleCheck |
	 '(' PermisoRuleOr ')' {PermisoPrimary.left=current}
;

PermisoRuleCheck:
	left=CampoPermiso (
		(simpleOp=PermisoRuleCheckSimpleOp right=PermisoRuleCheckRight)
		| (groupOp=PermisoRuleCheckGroupOp '(' rightGroup+=PermisoRuleCheckRight (',' rightGroup+=PermisoRuleCheckRight)* ')')
	)
	| 
	(not?="!"? permiso=[Permiso]
		(
			(op = "is" result=("editable" | "visible" | "oculto" | "_grafico")) |
			(op = "contains" result=("leer" | "editar" | "crear" | "borrar" | "_accion"))
		)
	)
;

PermisoRuleCheckSimpleOp:
	op = ('=' | '!=')
;

PermisoRuleCheckGroupOp:
	op = ('in' | 'not in')
;

PermisoRuleCheckRight:
	campo=CampoPermiso | str=STRING | booleano = ("true" | "false")  | nulo?='null' | action=("leer" | "editar" | "crear" | "borrar")
;

ServicioWeb:
	elemento='ServicioWeb' name=ID (descripcion=STRING)? "{"
		"campo" "=" campo=Campo
		(when=WSWhen)?
		"return" "=" "{" ret = WSReturn ("," retMore += WSReturn)* "}"
	"}"
;

WSWhen:
	'when' rule=WSRule
;

WSReturn:
	"titulo" "=" titulo=STRING ":" campoRet=Campo
;

WSRule:
	WSRuleOr
;

WSRuleOr returns WSRule:
	WSRuleAnd ({WSRuleOr.left=current} 'or' right=WSRuleAnd)*
;

WSRuleAnd returns WSRule:
	WSPrimary ({WSRuleAnd.left=current} 'and' right=WSPrimary)*
;

WSPrimary returns WSRule:
	WSRuleCheck |
	 '(' WSRuleOr ')' {WSPrimary.left=current}
;

WSRuleCheck:
	left=Campo (
		(simpleOp=WSRuleCheckSimpleOp right=WSRuleCheckRight)
		| (groupOp=WSRuleCheckGroupOp '(' rightGroup+=WSRuleCheckRight (',' rightGroup+=WSRuleCheckRight)* ')')
		| (compareOp=WSRuleCheckCompareOp right=WSRuleCheckRight)
	)
;

WSRuleCheckSimpleOp:
	op = ('=' | '!=')
;

WSRuleCheckGroupOp:
	op = ('in' | 'not in')
;

WSRuleCheckCompareOp:
	op = ('<' | '>' | '<='| '>=')
;

WSRuleCheckRight:
	str=STRING  | nulo?='null'
;

Codigo:
	elemento='Codigo'
	'{' 
		contenido=STRING
	'}' 
;

Color : 
	texto = "color" ":" 
	(codePrint = STRING
	| default = "porDefecto"
	|(
		(textoB = "siFilaBorrar" "=" claseB=STRING)?
		(textoE = "siFilaEditar" "=" claseE=STRING)?
		(textoL = "siFilaLeer" "=" claseL=STRING)?
	))
;

EditarFactura:
	elemento='EditarFactura' name=ID (
		('campo' '=' campo=Campo)
		& (requerido?='requerido')?
	)
;

SubirFactura:
	elemento='SubirFactura' name=ID (
		('campo' '=' campo=Campo)
		& (requerido?='requerido')?
	)
;

VisorFactura:
	elemento='VisorFactura' (name=ID)? (
		('campo' '=' campo=Campo)
	)
;

NEG_INT returns ecore::EInt: '-'INT;
