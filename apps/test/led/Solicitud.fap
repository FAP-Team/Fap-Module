Formulario Solicitud {
	
    Menu {
    	Enlace "Volver" pagina=Principal.Solicitudes
    	
    	Grupo "Combos" {
			Enlace "Combos por defecto" pagina=Combos
			Enlace "Combos sobreescritos" pagina=CombosOverwrite
			Enlace "Valores por defecto" pagina=ValoresPorDefecto
			Enlace "ErrorCombos" pagina=PaginaCombosError
		}
		
		Enlace "Verificacion" pagina=Solicitud.AccesoVerificaciones
		
		Grupo "Solicitantes" {
			Enlace "S. Normal" pagina=Solicitante
			Enlace "S. PersonaFisica" pagina=SolicitantePF
			Enlace "S. PersonaJuridica" pagina=SolicitantePJ
		}
		
		Grupo "Direccion"{
			Enlace "Direccion" pagina=Direcciones
			Enlace "Fecha" pagina=Fechas
		}
		
		Grupo "Tablas y PopUps" {
			Enlace "Tablas Simples" pagina=TablasSimples
		}
	
		Grupo "SubirArchivo" {
			Enlace "SubirArchivoAed" pagina=SubirArchivoAedTest 
		}
		
		Grupo "Test Grupos" {
			Enlace "Ejemplos Grupos I" pagina=EjemplosdeGrupos
		}
		

		Grupo "Permisos" {
			Enlace "Permiso pagina" pagina=PermisoPagina permiso=administrador
			Enlace "Permiso grupo" pagina=PermisoPagina permiso=administrador
			Enlace "Permiso no editable" pagina=PermisoNoEditable permiso=noEditable 
		}

		Grupo "Envío de Mails"{
			Enlace "Envío de Mails" pagina=EnvioMails
		}
		
		Grupo "Otras" {
			Enlace "Presentacion" pagina=Presentacion
			Enlace "Documentacion" pagina=Documentacion
		}
		
		Grupo "Instruccion" {
			Enlace "Aportación" pagina=Solicitud.Aportacion
			Enlace "Verificación" pagina=Solicitud.Verificacion
		}
		
		Enlace "Salir" accion="fap.SecureController.logout"
    }
    
    Pagina Solicitante {
    	Solicitante persona campo=Solicitud.solicitante
    	Texto email campo=Solicitud.solicitante.email
    }
    
    Pagina SolicitantePF {
		Wiki {"El representante de este solicitante sólo puede ser de tipo Persona Fisica"}
		SolicitantePersonaFisica personaSolicitantePersonaFisica campo=Solicitud.pf titulo="Solicitante Persona Fisica" representantePersonaFisica
	}

	Pagina SolicitantePJ {
		SolicitantePersonaJuridica personaSolicitantePersonaJuridica campo=Solicitud.pj titulo="Solicitante Persona Juridica"
	}
		
	Pagina Fechas {
		Fecha campo=Solicitud.fechas.fechaRequerida titulo="Fecha Obligatoria" ancho="30px" anchoTitulo="60px" requerido
		Fecha campo=Solicitud.fechas.fecha titulo="Fecha" requerido
	}
	
	Pagina Direcciones {
		Direccion campo=Solicitud.direccionTest.direccion
	}
	
	Pagina Combos inicial {

		Grupo titulo="Campos sin sobreescribir los controladores manuales" {
			Combo campo=Solicitud.comboTest.list titulo="Combo de Tabla de Tablas"
			Combo campo=Solicitud.comboTest.listSinDuplicados titulo="Combo de Tabla de Tablas sin elementos repetidos" eliminarDuplicados
			Combo campo=Solicitud.comboTest.listMultiple titulo="Combo Múltiple de Tabla de Tablas"
			Combo campo=Solicitud.comboTest.ref comboTexto=nombre titulo="Combo de Referencias" ordenarPorValor
			Combo campo=Solicitud.comboTest.refMultiple comboTexto=nombre titulo="Combo de Referencias Múltiples"
		}
				
		Tabla comboTestRef campo=ComboTestRef popup=ComboTestRef titulo="Referencias de combos (Los combos no se actualizan automáticamente)"{
			Columna campo=ComboTestRef.id
			Columna campo=ComboTestRef.nombre expandir
		}
	}

	Pagina CombosOverwrite {
		
		Grupo titulo="Combos sobreescritos en el controlador manual" {
			Combo lista campo=Solicitud.comboTest.listOverwrite titulo="Combo manual (String)" requerido
			Combo listaLong campo=Solicitud.comboTest.listOverwriteLong titulo="Combo manual (Long)"
			Combo listaMultiple campo=Solicitud.comboTest.listMultipleOverwrite titulo="Combo múltiple manual (Set<String>)"
			Combo listaMultipleLong campo=Solicitud.comboTest.listMultipleOverwriteLong titulo="Combo múltiple manual (Long<Long>)"
			Combo referencia campo=Solicitud.comboTest.refOverwrite comboTexto=nombre titulo="Combo de Referencias (Filtrando las que tienen en el nombre a)"
			Combo referenciaMultiple campo=Solicitud.comboTest.refMultipleOverwrite comboTexto=nombre titulo="Combo de Referencias Múltiples (Filtrando las que tienen en el nombre a)"
			
			Combo wsjson campo=Solicitud.comboTest.wsjson titulo="Valor del combo obtenido desde un servicio web en JSON"
			Combo wsxml campo=Solicitud.comboTest.wsxml titulo="Valor del combo obtenido desde un servicio web en XML"
		}
				
		Tabla comboTestRef campo=ComboTestRef popup=ComboTestRef titulo="Referencias de combos (Los combos no se actualizan automáticamente)"{
			Columna campo=ComboTestRef.id
			Columna campo=ComboTestRef.nombre expandir
		}
	}

	Popup ComboTestRef campo=ComboTestRef {
		Texto campo=ComboTestRef.nombre
	}
	
	Pagina PaginaCombosError {
		Wiki { "Error en los combos. Debe autocompletar los combos elegidos desde flash al estar el campo de texto vacío." }
		Texto campo=Solicitud.solicitante.telefonoFijo titulo="Telefono" requerido
		Combo campo=Solicitud.comboError comboTexto=nombre titulo="Error del combo"
		Combo campo=Solicitud.comboErrorMany comboTexto=nombre titulo="Error del combo Many"
	}
	
	Pagina ValoresPorDefecto {
		Texto mString campo=Solicitud.valoresPorDefectoTest.mString titulo="String"
		Texto mLong campo=Solicitud.valoresPorDefectoTest.mLong titulo="Long"
		Texto mInteger campo=Solicitud.valoresPorDefectoTest.mInteger titulo="Integer"
		Check mBoolean campo=Solicitud.valoresPorDefectoTest.mBoolean titulo="Boolean"
		Texto mDouble campo=Solicitud.valoresPorDefectoTest.mDouble titulo="Double"
		Texto mLongText campo=Solicitud.valoresPorDefectoTest.mLongText titulo="LongText"
		
		Texto mLongText2 campo=Solicitud.valoresPorDefectoTest.mLongText titulo="LongText 3" password
		
		Texto mEmbeddable campo=Solicitud.valoresPorDefectoTest.mEmbebida.algo titulo="String embeddable" 
	}
	
	Pagina TablasSimples {
		Tabla tablaNombres campo=Solicitud.tablaDeNombres titulo="Solicitantes"
		popupCrear=popupNombre
		popupVer=popupNombre
		popupModificar=popupNombre
		popupBorrar=popupNombre
		columnasAutomaticas exclude=id, fecha
		{
		}
		Tabla tablaNombres2 campo=Solicitud.peta titulo="Solicitantes"
		popupCrear=popupNombre2
		popupVer=popupNombre2
		popupModificar=popupNombre2
		popupBorrar=popupNombre2
		columnasAutomaticas
		{
		}
		
		
	}
	
	Popup popupNombre2 campo=Solicitud.peta crear modificar borrar ver {
		Texto campo=Peta.n titulo="Nombre"
		Texto campo=Peta.d titulo="Apellido"
		Texto campo=Peta.otro titulo="Otro contacto"
		Combo campo=Peta.petador titulo="Petador"
		Fecha campo=Peta.fecha titulo="Fecha"
	}
	
	
	Popup popupNombre campo=Solicitud.tablaDeNombres crear modificar borrar ver {
		Texto campo=TablaDeNombres.nombre titulo="Nombre"
		Texto campo=TablaDeNombres.apellido titulo="Apellido"
	}
	
	Pagina SubirArchivoAedTest noForm {
		Form anadirDocumentacion campo=Solicitud.documentacion.documentos {		
			SubirArchivoAed fileAportacion campo=Documento requerido tramite="Tramite" aportadoPor="Ciudadano" //extensiones="pdf", "png"
			Boton subirDocumento titulo="Añadir documentación"
		}		
		Tabla documentos 
			    titulo="Documentos" 
		    	campo=Solicitud.documentacion.documentos
				popupModificar=PopUpDocEditar  
			{
				Columna campo=Documento.fechaSubida titulo="Fecha" ancho="80"
				Columna campo=Documento.descripcion titulo="Tipo" expandir
				Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
			}
	}
	
	Popup PopUpDocEditar modificar borrar ver titulo="Documentación" campo=Solicitud.documentacion.documentos {
		EditarArchivoAed documentoEditable campo=Documento requerido tramite="Tramite" aportadoPor="Ciudadano"
	}
	
	Pagina EjemplosdeGrupos {
		Grupo testMostrarSiExpresion titulo="Grupo 1: Mostrar Si Expression" mostrarSiExpresion "4 == 4"{
			Texto ifTexto titulo="Texto" campo=Solicitud.testGrupo.nombre
 		}
 		
 		Grupo test2MostrarSiExpresion titulo="Grupo 1: Mostrar Si Campo" mostrarSiCampo Solicitud.solicitante.email = "pepe@pepe.com", "juan@juan.com" {
			Texto if2Texto titulo="Texto" campo=Solicitud.testGrupo.nombre
 		}
 		
 		Check checkMostrar1 campo=Solicitud.testGrupo.myCheck titulo="Prueba visibilidad conjunta"
 		Combo comboMostrarSi campo=Solicitud.comboTest.list titulo="Prueba para mostrarSiCombo"
 		
 		Grupo titulo="Grupo 1: Mostrar Si Check" mostrarSiCheck checkMostrar1 = true {
 			Wiki {"El check está seleccionado"}
 			Texto campo=Solicitud.solicitante.telefonoFijo titulo="Telefono" requerido
 		}
 		
 		Grupo titulo="Grupo 1: Mostrar Si Combo" mostrarSiCombo comboMostrarSi = "a", "b" {
 			Wiki {"El combo tiene un valor seleccionado igual a 'A' o 'B'"}
 			Texto campo=Solicitud.solicitante.telefonoMovil titulo="Telefono Móvil" requerido
 		}
 		
	}

	Pagina Documentacion noForm titulo="Documentación" permiso=solicitudEditable {
 
            Tabla documentacion titulo="Documentación" campo=Solicitud.documentacion.documentos
                  popupBorrar=PopUpDocumentacion
                  popupModificar=PopUpDocumentacion
                  popupVer=PopUpDocumentacion
            {
                  Columna campo=Documento.fechaSubida titulo="Fecha" ancho="80"
                  Columna campo=Documento.descripcion titulo="Tipo" expandir
                  Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
            }
            
           Grupo titulo="Añadir documentación" permiso=solicitudEditableDocumentacion {
                  Form anadirDocumentacion campo=Solicitud.documentacion.documentos {
                        SubirArchivoAed fileAportacion campo=Documento requerido aportadoPor="CIUDADANO" tramite="SOLICITUD"
                        Boton subirDocumento titulo="Añadir documentación"
                  }
            }

    }
    
       Popup PopUpDocumentacion modificar borrar ver titulo="Documentación" campo=Solicitud.documentacion.documentos {
            EditarArchivoAed modificarDocumento campo=Documento requerido
      }

	Pagina PermisoPagina permiso=administrador{
		Wiki {"Si puedes ver esta página tienes que ser administrador."}
	}
	
	Pagina PermisoGrupo permiso=administrador {
		Wiki {"La página tiene un grupo que es sólo visible para administradores"}
		Grupo permiso=administrador {
			Wiki {"Este grupo es sólo visible para administradores"}	
		}
	}
	
	Pagina PermisoNoEditable permiso=noEditable {
		Wiki {"Esta página es no editable"}	
	}

	Pagina EnvioMails noForm{
		Wiki {"Pulse en botón para enviar un mail de prueba siguiendo la configuracion descrita en la pagina de Administración"}	
		Form enviarMail {
        	Boton enviarBoton titulo="Enviar Mail"
        }
    }
	Pagina Presentacion{
		Form myFormPres {
			FirmaSimple firmaSimple titulo="Firmar" documento=Solicitud.registro.oficial
		}
	}
}
