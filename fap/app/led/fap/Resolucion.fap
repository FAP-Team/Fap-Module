Lista resolucionesDefinidas crearEnumerado {
	simpleTotal: "Simple Total"
	multipleTotal: "Múltiple Todos Expedientes"
	multipleParcialExpedientes: "Multiple Seleccionando Expedientes"
}

Lista tipoResolucion crearEnumerado {
	provisional: "Provisional"
	definitiva: "Definitiva"
}

Lista modalidadResolucion crearEnumerado {
	resolucionSimple: "Simple"
	resolucionMultiple: "Multiple"
}

// Estados por los que va pasando la resolución
Lista estadoResolucion crearEnumerado {
	borrador: "Borrador"	// Ha sido creada, y establecido su tipo y modalidad
	creada: "Creada"
	preparada: "Preparada"
	pendienteFirmaJefeServicio: "Pendiente firma Jefe Servicio"
	firmadaJefeServicio: "Firmada Jefe Servicio"
	pendienteFirmaDirector: "Pendiente firma Director"
	firmada: "Firmada"
	registrada: "Registrada"
	publicada: "Publicada"
	finalizada: "Finalizada"
}

Lista estadoTipoMultiple crearEnumerado {
	parcial: "Selecionar manualmente los expedientes"
	total: "Todos los expedientes"
}

Lista estadoLineaResolucion crearEnumerado {
	concedida: "Concedida"
	excluida: "Excluida"
}

// Una línea de resolución afecta a un expediente (o Solicitud)
Entidad LineaResolucion {
	SolicitudGenerica solicitud noConstruct
	Lista estadoLineaResolucion estado
}

// Entidades de la resolución
Entidad ResolucionFAP {
	/// Tipo de los posibles entre los definidos de la resolución
	Lista resolucionesDefinidas tipoDefinidoResolucion
	
	Lista modalidadResolucion modalidad		// Simple, Multiple
	Lista tipoResolucion tipo				// Provisional, Definitiva
	Lista estadoTipoMultiple tipoMultiple	// Parcial, Total
	
	Lista estadoResolucion estado			// Borrador, creada, .... finalizada
	
	// Descripción de la Resolución
	String descripcion length=2500
	
	// modalidad=Simple   --> Una sola línea de resolución
	// modalidad=Múltiple --> Varias líneas de resolución
	OneToMany LineaResolucion lineasResolucion
	
	Boolean conBaremacion default = 'false'
	
	
	Integer  folio_inicio							// Folio de inicio en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer  folio_final							// Folio final en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer  numero									// Número de resolución. Valor devuelto por SW de Registro de Resoluciones
	DateTime fechaResolucion						// Fecha de la resolución
	
	//DateTime fechaFinAceptacion						// Debería coincidir con la fecha fin reformulacion y fin alegacion si existe
	//DateTime fechaPublicacion						// Fecha de Publicación en el tablón de anuncios de la sede
	//DateTime fechaBOC								// Fecha de Publicación en el BOC
	DateTime fechaIncioPreparacion		// Fecha en la que se empieza a preparar la resolución y se crean las líneas
	//DateTime fechaFinPreparacion					// Fecha en la que se finaliza la resolución
	//Documento docResolucion							// Documento de Resolución
	//OneToMany Documento docAnexoResolucion //Anexos individuales de resolucion
	OneToMany Documento docConsultaPortafirmasResolucion //Documentos de Consulta Portafirmas.

	Registro registro
	
		//Long IDPortafirma					//ID devuelto por el Servicio WEb del Portafirmas
		//String estadoPortafirmas			//??Pendiente de revisar
	//SolicitudPortafirma solicitudFirmaJefeServicio
	//SolicitudPortafirma solicitudFirmaDirector

	//Lista estadoResolucionConcesion estado		//Estado de la resolución: 1.0:Creada,2.0:Preparada, 3.0:En_Firma, 4.0:Firmada, 5.0:Registrada, 6.0:Publicada
		//OneToMany LineaResolucion lineasResolucion
		// OneToMany ExpedientesAlegados expedientesAlegados
		// OneToMany ExpedientesNoAceptados expedientesNoAceptados
	//Agente destinatarioPortafirma transient

	//Para firmar por jefe de servicio
	//Firmantes firmantesJefeDeServicio
		////Para poder firmar y crear el el metodo calcular firmantes
		//Solicitante solicitanteJefeServicio transient


}


Formulario Principal {
	Pagina Resoluciones titulo="Lista de Resoluciones"{
		Tabla listaResoluciones titulo="Resoluciones" campo=ResolucionFAP paginaCrear=NuevaResolucion paginaEditar=EditarResolucion
		{
			Columna campo=ResolucionFAP.id titulo="ID"
			Columna campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resolución"
			Columna campo=ResolucionFAP.tipo titulo="Tipo"
			Columna campo=ResolucionFAP.modalidad titulo="Modalidad"
		}
	}
	
	Pagina NuevaResolucion titulo ="Resolución" campo=ResolucionFAP perteneceA="Principal" {
		Accion crear redirigir=EditarResolucion
		Grupo gIniciarTipo titulo="Seleccionar Resolución" {
			Combo campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resolución"
			Combo campo=ResolucionFAP.tipo titulo="Tipo"
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripción" requerido
			Check campo=ResolucionFAP.conBaremacion titulo="¿Tiene Baremación?"
		}
	}
	
	Pagina EditarResolucion titulo="Resolución" campo=ResolucionFAP perteneceA="Principal" noForm {
		
		Grupo mostrarSiCampo ResolucionFAP.modalidad = "multiple" borde=false {
			Grupo titulo="Expedientes a Resolver" mostrarSiCampo ResolucionFAP.tipoMultiple = "parcial"{
			
				Form formSeleccionarExpedientes {
					Grupo titulo="Seleccionar Expedientes"{
						Combo cmbTipo campo=ResolucionFAP.tipoMultiple titulo="Seleccionar" requerido
						Boton btnGuardar titulo="Guardar"
					}
				}
			
				Tabla tablaExpedientes campo=SolicitudGenerica seleccionable="Seleccionar" {
					Columna titulo="ID" campo=SolicitudGenerica.id ancho="40"
					Columna titulo="Expediente" campo=SolicitudGenerica.expedienteAed.idAed
					Columna titulo="Estado Interno" campo=SolicitudGenerica.estadoValue ancho="120"
					Columna titulo="Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
				}
			}
		}
		
    	Grupo gCerrarResolucion titulo="Preparar la Resolución" {
			Wiki { "Si su resolución está lista para firmar, pulse el siguiente botón." }
			Form prepararResolucion {
				Boton  btnPrepararResolucion titulo="Cerrar Resolución"
			}
		}
		
		Grupo gPrepararSolicitudFirmaPortafirma titulo="Preparar Solicitud de Firma (PORTAFIRMAS)" {
			Enlace titulo="Obtener documento de Resolución" estilo="btn" campo=ResolucionFAP.registro.borrador.urlDescarga
			Tabla documentosPortafirma titulo="Documentos de Consulta Portafirmas" campo=ResolucionFAP.docConsultaPortafirmasResolucion paginaCrear=DocConsultaPortafirmaCrear paginaEditar=DocConsultaPortafirmaEditar {
				Columna campo=Documento.id titulo="Id" ancho="40"
				Columna campo=Documento.tipo titulo="Tipo"
				Columna campo=Documento.urlDescarga titulo="Ver"
			}
		}
		
		// Si el Agente es un JefeDeServicio, puede firmar directamente
		Grupo grSolicitarFirmaPorJefeSevicio borde=false /*permiso=jefeServicio*/ {
			Grupo grFirmaJefeServicio borde=false {
     			Form frmFirmarJefeServicio redirigir=EditarResolucion {
					FirmaSimple firFirmarResolucion titulo="Firmar Resolucion" documento=ResolucionFAP.registro.oficial
						listaFirmantes=ResolucionFAP.registro.firmantes.todos calcularFirmantes="resolucionFAP.calcularFirmantes(AgenteController.getAgente())"
				}
				Wiki {"Nota: La operación de firmar y registrar puede tardar varios minutos."}
			}
		}
		
		Grupo gDatosResolucion titulo="Datos de la Resolución actual" permiso=noEditable {
			Combo campo=ResolucionFAP.estado titulo="Estado"
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripción"
			Combo campo=ResolucionFAP.tipo titulo="Tipo de Resolución" requerido
			Combo campo=ResolucionFAP.modalidad titulo="Modalidad de la Resolución" requerido
			Check campo=ResolucionFAP.conBaremacion titulo="¿Tiene Baremación?"
			Tabla lineasResolucion campo=ResolucionFAP.lineasResolucion titulo="Lineas Resolución" {
				Columna campo=LineaResolucion.id titulo="Id"
				Columna campo=LineaResolucion.solicitud.expedienteAed.idAed titulo="Expediente"
				Columna campo=LineaResolucion.solicitud.estado titulo="Estado Solicitud"
				Columna campo=LineaResolucion.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
				Columna campo=LineaResolucion.solicitud.solicitante.nombreCompleto titulo="Solicitante"
				Columna campo=LineaResolucion.estado titulo="Estado"
			}
			Texto campo=ResolucionFAP.folio_inicio titulo="Folio Inicial"
			Texto campo=ResolucionFAP.folio_final titulo="Folio Final"
			Texto campo=ResolucionFAP.numero titulo="Numero"
			Fecha campo=ResolucionFAP.fechaResolucion titulo="Fecha Resolución"
			Wiki { " Faltan más cosas ................ " }
		}
		
		
	}
	
	Pagina DocConsultaPortafirmaCrear campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion crear redirigir=EditarResolucion
		SubirArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
	}
	
	Pagina DocConsultaPortafirmaEditar campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion editar redirigir=EditarResolucion
		EditarArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
	}
}

Permiso sinIniciarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion = null
		or resolucion.tipo = null
	return
		[editar crear]
	when
		resolucion.tipo != null
	return
		visible
}