h1. Tabla

h2. Descripción

Muestra una tabla con los valores de una lista dada. 

h2. Parámetros

* **_[name]_** : Identificador de la tabla.
* **campo = _[campo]_** : Atributo donde se encuentra la lista de elementos a mostrar en la tabla.
* **titulo = _[string]_** (opcional) : Etiqueta que se mostrará como cabecera de la tabla, si no se especifica se mostrará el nombre del campo.
* **columnasAutomaticas** (opcional) : Simplifica la creación de columnas generando automáticamente una columna para cada uno de los atributos de la entidad especificada en el campo.
* **exclude = _[atributos]_** (opcional) : Complemento de columnas automáticas que permite la exclusión de aquellos atributos de las columnas de la tabla que se especifiquen. [atributos], será una lista de atributos de la entidad, que define la tabla, separados por ','.
* **include = _[atributos]_** (opcional) : Complemento de columnas automáticas que permite la inclusión en las columnas de la tabla, sólo de aquellos atributos que se especifiquen. [atributos], será una lista de atributos de la entidad, que define la tabla, separados por ','.
* **alto = _[string]_** (opcional) : Permite especificar el alto de la tabla.
* **seleccionable = _[string]_** (opcional) : Se añade un check a cada elemento de la tabla que permite seleccionarlo, además se añade un botón en la tabla (con el nombre que se le indique en este parámetro) que obtendrá la lista de elementos seleccionados y llamara al método del controlador.
* **popup = _[popup]_** (opcional) : Asigna un popup a la tabla, creando todos los botones disponibles para abrirlo. "Ver documentación Popup":dsl-popup.
* **popupLeer = _[popup]_** (opcional) : Asigna un popup a la tabla, creando el botón Ver para abrirlo. "Ver documentación Popup":dsl-popup.
* **popupEditar = _[popup]_** (opcional) : Asigna un popup a la tabla, creando el botón Editar para abrirlo. "Ver documentación Popup":dsl-popup.
* **popupCrear = _[popup]_** (opcional) : Asigna un popup a la tabla, creando el botón Crear para abrirlo. "Ver documentación Popup":dsl-popup.
* **popupBorrar = _[popup]_** (opcional) : Asigna un popup a la tabla, creando el botón Borrar para abrirlo. "Ver documentación Popup":dsl-popup.
* **permisoFilas = _[permiso]_** (opcional) : Permite asignar permisos a la tabla, estos permisos se utilizaran para filtrar las filas que se muestran en la tabla.
* **recargarPagina** (opcional) : Si se define, hace que la pagina se recargue al pulsar el botón actualizar de la tabla. (Por defecto solo se actualiza la tabla).
* **pagina = _[pagina]_** (opcional) : Asigna una pagina a la tabla, creando todos los botones disponibles. "Ver documentación Pagina":dsl-pagina.
* **paginaLeer = _[pagina]_** (opcional) : Asigna una pagina a la tabla, creando el botón Ver para abrirla. "Ver documentación Pagina":dsl-pagina.
* **paginaEditar = _[pagina]_** (opcional) : Asigna una pagina a la tabla, creando el botón Editar para abrirla. "Ver documentación Pagina":dsl-pagina.
* **paginaCrear = _[pagina]_** (opcional) : Asigna una pagina a la tabla, creando el botón Crear para abrirla. "Ver documentación Pagina":dsl-pagina.
* **paginaBorrar = _[pagina]_** (opcional) : Asigna una pagina a la tabla, creando el botón Borrar para abrirla. "Ver documentación Pagina":dsl-pagina.
* **Color: _[string | porDefecto | siFilaBorrar | siFilaLeer | siFilaEditar]_ = String** (opcional): Atributo que permite asignar una clase css para colorear las filas de una tabla de acuerdo a un código especificado, a sus permisos (siFilaLeer, siFilaBorrar, siFilaEditar) o por la opción por defecto (editable y no editable).

h2. Definición

bc. Tabla:
	elemento='Tabla' name=ID
	(
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('alto' '=' alto=STRING)?
		& (columnasAutomaticas?="columnasAutomaticas" (('exclude' '=' exclude=ListaAtributos) | ('include' '=' include=ListaAtributos))?)?
		& ("permisoFilas" "=" permiso=[Permiso])?
		& (recargarPagina?="recargarPagina")?
		& ('seleccionable' "=" seleccionable=STRING)?
		& ('popup' '=' popup=[Popup|QN])?
		& ('popupCrear' '=' popupCrear=[Popup|QN])?
	   	& ('popupBorrar' '=' popupBorrar=[Popup|QN])?
	   	& ('popupEditar' '=' popupEditar=[Popup|QN])?
	   	& ('popupLeer' '=' popupLeer=[Popup|QN])?
	   	& ('pagina' '=' pagina=[Pagina|QN])?
	   	& ('paginaCrear' '=' paginaCrear=[Pagina|QN])?
	   	& ('paginaBorrar' '=' paginaBorrar=[Pagina|QN])?
	   	& ('paginaEditar' '=' paginaEditar=[Pagina|QN])?
	   	& ('paginaLeer' '=' paginaLeer=[Pagina|QN])?
	    & (color=Color)?
	)
	'{'
		columnas += Columna*
	'}'
;


bc.	Color : 
		"Color:" 
		(codePrint = STRING
		| default = "porDefecto"
		|(
			(textoB = "siFilaBorrar" "=" claseB=STRING)?
			(textoE = "siFilaEditar" "=" claseE=STRING)?
			(textoL = "siFilaLeer" "=" claseL=STRING)?
		))
	;

h2. Columnas

Permite definir los atributos de la entidad que se mostraran en la tabla. La entidad que se especifique en este elemento debe corresponder con el tipo de la lista de entidades que se asigne a la tabla. "Ver documentación Columnas":dsl-columna. 

El parámetro _columnasAutomaticas_ simplifica la creación de columnas ya que dada una entidad (que se pasa como parámetro) genera automáticamente una columna para cada uno de sus atributos.

Además utilizando _exclude_ o _include_, podemos decir que esta generación automática no introduzca ciertos atributos o sólo introduzca unos determinados atributos, respectivamente. 

En caso de usar _columnasAutomaticas_ y la definición normal de _Columnas_, este último será más restrictivo, es decir, si se usa _columnasAutomaticas_ y después se define una _Columna_, se tendrán en cuenta los formatos de la _Columna_ definida, no de la generada por _columnasAutomaticas_.

h2. Ejemplos de uso

h3. Tablas utilizando Pop-Ups (Todas las acciones (crear, leer, editar y borrar) para el Pop-Up)

bc. Tabla listaUsuarios titulo="Usuarios" campo=Agente popup=PopupUsuario {
	Columna campo=Agente.id titulo="Id" ancho="25"
	Columna campo=Agente.username titulo="Usuario" expandir
	Columna campo=Agente.name titulo="Nombre completo" expandir
	Columna campo=Agente.roles titulo="Roles" expandir
	Columna campo=Agente.email titulo="Email" ancho="150"
}


h3. Tablas utilizando Pop-Ups (Sólo permite Editar y Leer las filas con Pop-Ups)


bc. Tabla conexionesBandaAnchaServ titulo="Conexiones de Banda Ancha en Servicio" campo=Solicitud.conexiones.conexionesBandaAncha
	popupEditar=PopUpConexionesBandaAncha
	popupLeer=PopUpConexionesBandaAncha
	recargarPagina {
		Columna campo=ConexionBandaAncha.mes titulo="Mes" expandir
		Columna campo=ConexionBandaAncha.directaGCyTF titulo="Conex. directas TF-GC" expandir
		Columna campo=ConexionBandaAncha.directaResto titulo="Conex. directas otras islas" expandir
		Columna campo=ConexionBandaAncha.indirecta titulo="Conex. indirectas" expandir
}

p.  

h3. Columnas automaticas utilizando exclude

bc. Tabla tablaNombres campo=Solicitud.tablaDeNombres titulo="Solicitantes"
	popupLeer=popupNombre	
	columnasAutomaticas exclude = id, apellido {
}

h3. Tablas utilizando Paginas

bc. ... 
Pagina CrearTablaPaginas campo=Solicitud.tablaPaginas.tpaginas{
	Accion crear redirigir=PaginaTablas
	Texto titulo="Nombre tabla con pagina" ...
	Texto titulo="Numero tabla con pagina " ...
}
...
Pagina PaginaTablas campo=Solicitud {
	Tabla TablaConPaginas campo=TablaPaginas.tpaginas paginaCrear=CrearTablaPaginas paginaEditar=ModificarTablaPaginas paginaBorrar=BorrarTablaPaginas titulo="Tabla con paginas" columnasAutomaticas include=id{}
}

h3. Tablas utilizando coloreado por defecto

bc. ...
Tabla tablaConceptos campo=Solicitud.misConceptos titulo="Tabla de mis Conceptos" popup=PopUpConceptos
    	Color: porDefecto
    	{
    		Columna titulo="Entero" campo=MiConcepto.unInteger
    		Columna titulo="Double" campo=MiConcepto.unDouble
    		Columna titulo="Moneda" campo=MiConcepto.unaMoneda
    	}
...

h3. Tablas utilizando coloreado de filas por permisos

bc. ...
Tabla tablaConceptos campo=Solicitud.misConceptos titulo="Tabla de mis Conceptos" popup=PopUpConceptos
    	Color: siFilaBorrar = "colorSiBorrar" 
    	siFilaEditar = "colorSiEditar"
    	siFilaLeer = "colorSiLeer"
    	{
    		Columna titulo="Entero" campo=MiConcepto.unInteger
    		Columna titulo="Double" campo=MiConcepto.unDouble
    		Columna titulo="Moneda" campo=MiConcepto.unaMoneda
    	}
...

h3. Tablas utilizando coloreado de filas definido por usuario

bc. ...
Tabla tablaConceptos campo=Solicitud.misConceptos titulo="Tabla de mis Conceptos" popup=PopUpConceptos 
    	Color: "var c = record.data.id; if (c < 8) { return \\'docFirmado\\'; } else if (c > 8) { return \\'docNoFirmado\\'; }"
    	{
    		Columna titulo="Entero" campo=MiConcepto.unInteger
    		Columna titulo="Double" campo=MiConcepto.unDouble
    		Columna titulo="Moneda" campo=MiConcepto.unaMoneda
    	}
...