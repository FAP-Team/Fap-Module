Formulario Solicitud campo=SolicitudGenerica {
	
	//Pagina visible para en usuario en fase de presentacion para que autorice la cesion de datos
	Pagina CesionDatos campo=SolicitudGenerica.cesion.autorizacionCesion permiso=editarSolicitud {
		Accion editar crearSiempre
		Wiki{"Pulse guardar despues de realizar los cambios"}
		Grupo grupoCesion titulo="Autorizo a la ACIISI a:"{
			Check checkAEAT campo=AutorizacionCesion.aeat 
			titulo="El acceso a la certificación acreditativa de hallarme al corriente de las obligaciones tributarias en la Agencia Estatal de la Administracion Tributaria (AEAT)"
			Check checkINSS campo=AutorizacionCesion.inssR001
			titulo="El acceso a la certificación acreditativa de hallarme al corriente en la Seguridad Social (INSS)"
			Check checkATC campo=AutorizacionCesion.atc
			titulo="El acceso a la certificación acreditativa de hallarme al corriente de las obligaciones tributarias en la Administración Tributaria Canaria (ATC)"
			Check checkObtenerTrab campo=AutorizacionCesion.inssA008
			titulo="Obtener de la Seguridad Social el Informe del número medio anual de trabajadores en situación de alta"
			Grupo borde=false mostrarSiCheck checkObtenerTrab = true{
				Tabla trabajadoresSS campo=AutorizacionCesion.trabajadores popup=PopupTrabajadores titulo="Solicitante"{
					Columna titulo="Régimen Seg. Social" campo=Trabajador.regimen ancho="200"
					Columna titulo="Cód. Cuenta Cotización" campo=Trabajador.codigoCuenta ancho="200"
				}
			}
			Check checkIDI campo=AutorizacionCesion.idi 
			titulo="A efectos de lo previsto en la Ley Orgánica 15/1999, de 13 de Diciembre, de Regulación del Tratamiento Automatizado de los Datos de Carácter Personal, la utilización de la información contenida en la solicitud y memoria anexa así como en los documentos que acompañan a la misma, para su difución en bases de datos de la Agencia Canaria de Investigación, Innovación y Sociedad de la Información y en otras bases de datos de I+D+I"
		}
	} 
	
	Popup PopupTrabajadores campo=SolicitudGenerica.cesion.autorizacionCesion.trabajadores titulo="Nueva cuenta de cotizacion"{ 
		Texto regimen campo=Trabajador.regimen titulo="Régimen de la Seguridad Social" requerido
		Texto codCuenta campo=Trabajador.codigoCuenta titulo="Código de cuenta de cotización" requerido
	}
	
	//Visible para el usuario en la fase de instrucción por administrador o gestor
	Pagina ListadoCesiones campo=SolicitudGenerica permiso=adminOrGestor{
		Accion editar crearSiempre
		Grupo permiso=instruccion{
			Tabla tablaCesiones titulo="Cesiones de datos" campo=SolicitudGenerica.cesion.cesiones popupLeer=PopupLeerListadoCesiones popupCrear=PopupListadoCesiones popupBorrar=PopupListadoCesiones{ // popupEditar=PopupListadoCesiones{
				Columna titulo="Tipo" campo=Cesiones.tipo ancho="100"
				Columna titulo="Fecha de Petición" campo=Cesiones.fechaPeticion ancho="100"
				Columna titulo="Fecha de Validez" campo=Cesiones.fechaValidez ancho="100"
				Columna titulo="Estado" campo=Cesiones.estado ancho="100"
				Columna titulo="Origen" campo=Cesiones.origen ancho="100"
			}
		}
	}
	
	Popup PopupListadoCesiones campo=SolicitudGenerica.cesion.cesiones permiso=adminOrGestor {		
		Combo comboTipo campo=Cesiones.tipo titulo="Tipo" requerido
		Fecha fechaConsulta campo=Cesiones.fechaPeticion titulo="Fecha de consulta" requerido
		Fecha fechaValidez campo=Cesiones.fechaValidez titulo="Fecha de validez" requerido
		Combo comboEstado campo=Cesiones.estado titulo="Estado" requerido
		Combo comboDoc campo=Cesiones.documento.uri titulo="Documento" requerido
	}
	
	Popup PopupLeerListadoCesiones campo=SolicitudGenerica.cesion.cesiones permiso=paginaAConfigurar {//SOLO LEER
		Combo comboTipo campo=Cesiones.tipo titulo="Tipo"
		Fecha fechaConsulta campo=Cesiones.fechaPeticion titulo="Fecha de consulta" 
		Fecha fechaValidez campo=Cesiones.fechaValidez titulo="Fecha de validez"
		Combo comboEstado campo=Cesiones.estado titulo="Estado"
		Combo comboDoc campo=Cesiones.documento.tipo titulo="Documento"
		Enlace enlaceDoc campo=Cesiones.documento.urlDescarga titulo="Ver el documento correspondiente" destino="_blank" 
	}
		
}