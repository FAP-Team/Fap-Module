Formulario Solicitud campo=SolicitudGenerica permiso=solicitudes{

	Pagina PCEconomicos titulo="Conceptos económicos"{

	}


	Pagina Aportacion {
		
		Enlace titulo="Consultar documentos ya aportados" pagina=AportacionAportados estilo="btn"
		Enlace titulo="Consultar recibos de aportaciones" pagina=AportacionRecibos estilo="btn"

		Tabla documentos 
		    titulo="Documentos nuevos pendientes de aportar" 
		    campo=SolicitudGenerica.aportaciones.actual.documentos
			popupBorrar=PopUpDocProcesos
			popupEditar=PopUpDocProcesos  
			popupLeer=PopUpDocProcesos
			paginaCrear=DocumentosAportacion
		{
			Columna campo=Documento.fechaSubida titulo="Fecha" ancho="80"
			Columna campo=Documento.descripcion titulo="Descripción" expandir
			Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
		}
		
		Form presentar redirigir=AportacionPresentar permiso=aportacionDocumentos {
			Wiki {"Debe registrar los documentos para que se consideren aportados a la solicitud."}
			Boton botonPresentar titulo="Preparar para registrar"
		}
		
	}
	
	Pagina DocumentosAportacion campo=SolicitudGenerica.aportaciones.actual.documentos{
		Accion crear redirigir=Aportacion
		SubirArchivo fileAportacion campo=Documento requerido tramite="Solicitud" aportadoPor="CIUDADANO"
	}
	
	Pagina AportacionAportados {		
		Tabla documentos titulo="Documentos Presentados" 
		    campo=SolicitudGenerica.documentacion.documentos
			popupLeer=PopUpDocPresentados
		{
			Columna campo=Documento.fechaSubida titulo="Fecha" ancho="80" 
			Columna campo=Documento.descripcion titulo="Descripción" expandir
			Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
		}
		
		Enlace titulo="Volver" pagina=Aportacion estilo="btn"
	}
	
	
	Pagina AportacionRecibos {
			
		Tabla recibosAportados titulo="Recibos de las aportaciones" campo=SolicitudGenerica.documentacionAportada.documentos permisoFilas=aportacionNoNull
		{
			Columna campo=Documento.fechaSubida titulo="Fecha" ancho="80"
			Columna campo=Documento.descripcion titulo="Descripción" expandir
			Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
		}
		
		Enlace titulo="Volver" pagina=Aportacion estilo="btn"
	}
	
	Popup PopUpDocPresentados titulo="Documentación" campo=SolicitudGenerica.documentacion.documentos{
		EditarArchivo documentoPresentado campo=Documento requerido
	}
	
	Popup PopUpDocProcesos titulo="Documentación" campo=SolicitudGenerica.aportaciones.actual.documentos{
		EditarArchivo documentoProceso campo=Documento requerido
	}
	
	Popup PopUpDocAportados titulo="Documentación" campo=SolicitudGenerica.documentacionAportada.documentos {
		EditarArchivo documentoAportado campo=Documento requerido
	}
		
	Pagina AportacionPresentar {
	  	Grupo titulo="Presentar Aportación de Documentos" {
			Wiki {"Puede obtener un borrador de la solicitud de aportación de documentación, no válido para su presentación y comprobar que la información introducida es correcta."}

			Enlace campo=SolicitudGenerica.aportaciones.actual.borrador.urlDescarga titulo="Obtener borrador" destino="_blank" estilo="btn"
		
			Form modificarBorrador redirigir=Aportacion permiso=aportacionModificar{
				Wiki {"Puede realizar cambios antes de su presentación definitiva."}
				Boton botonModificarBorrador titulo="Modificar datos"
			}
			
			Grupo mensajeIntermedio permiso=aportacionMensajeIntermedio borde=false {
				Wiki estilo ="box-warn box" {
					"Se produjo un error en la presentación de la aportación. Pulse otra vez el botón 'Firmar y presentar' para intentar finalizar el proceso."
				}
			}
			
			
			Wiki {"La solicitud de aportación será firmada y registrada. Puede obtener un recibo de la presentación de la solicitud pulsando el enlace 'Obtener recibo'." }
			
			Form presentar redirigir=AportacionRecibos {
				FirmaSimple firmarRepresentante titulo="Firmar y presentar"
					documento=SolicitudGenerica.aportaciones.actual.oficial
					listaFirmantes=SolicitudGenerica.registro.firmantes
					calcularFirmantes=SolicitudGenerica.solicitante
			}
			
			Grupo titulo="Incorporar Documentos sin Registro de Entrada" permiso=adminGestorRevisor {
				Form presentarSinRegistrar redirigir=AportacionRecibos {
					Fecha fechaAportacionSinRegistrar titulo="Fecha Incorporación" campo=SolicitudGenerica.aportaciones.actual.fechaAportacionSinRegistro requerido
					Boton aportarSinRegistrar titulo="Incorporar"
				}
			}
			

			Wiki {"Nota: La operación de firmar y presentar puede tardar varios minutos."}
		}
	}
	
		
	// ******************************************************************************************
	// VERIFICACION
	// *******************************************************************************************
	
	Pagina AccesoVerificaciones titulo="Verificaciones" noForm {
		Tabla verificaciones titulo="Verificaciones" campo=SolicitudGenerica.verificaciones
			pagina=PaginaVerificacionesAnteriores
		{
				Columna titulo="Expediente" campo=Verificacion.expediente ancho="150"
				Columna titulo="Motivo Exclusión" campo=Verificacion.requerimiento.motivo ancho="150" 
				Columna titulo="Fecha creación" campo=Verificacion.fechaCreacion ancho="150"
				Columna titulo="Fecha finalización" campo=Verificacion.fechaUltimaActualizacion ancho="150"
				Columna titulo="Estado" campo=Verificacion.estado expandir
		}
		Grupo gVerificacionNueva titulo="Crear una nueva verificación" permiso=noHayVerificacionEnCurso{
			Form verificacionNueva {
				Wiki {"Pulse el botón para crear una nueva verificación."}
				Boton botonIniciarVerificacion titulo="Nueva verificación"
			}
		}
		Grupo gVerificacionActual titulo="Acceder a la verificacion actual" permiso=hayVerificacionEnCurso {
			Form verificacionActual {
				Wiki {"Pulse el botón para acceder a la verificación que está actualmente en curso."}
				Boton botonIraVerificacion titulo="Ir a verificación" pagina=PaginaVerificacion
			}
		}
	}
	
	Pagina PaginaVerificacionesAnteriores campo=SolicitudGenerica.verificaciones{
		
	}
	
	Pagina PaginaVerificacion titulo="Verificación" campo=SolicitudGenerica.verificacionEnCurso noForm permiso=accederSiSolicituNoBorrador{
		Grupo datosVerificacion titulo="Datos generales de la verificación" permiso=noEditable{
			Texto expediente titulo="Expediente" campo=Verificacion.expediente
			Combo estadoS titulo="Estado Solicitud" campo=SolicitudGenerica.estado
			Texto motivoReq titulo="Motivo Requerimiento" campo=Verificacion.requerimiento.motivo
			Fecha fechaCreacion titulo="Fecha Creación" campo=Verificacion.fechaCreacion
			Fecha fechaActualizacion titulo="Fecha Actualizacion" campo=Verificacion.fechaUltimaActualizacion
			Combo estadoV titulo="Estado Verificación" campo=Verificacion.estado
		}
		
		Grupo giniciarVerificacion titulo="Iniciar la verificación" permiso=iniciarVerificacion {
			Form iniciarVerificacion {
				Combo getTramitesCombo titulo="Seleccione el trámite" campo=Verificacion.uriTramite requerido
				Wiki {"Si la solicitud está lista para iniciar la verificación, pulse el siguiente botón."}
				Boton botonIniciarVerificacion titulo="Iniciar la verificación"
			}
		}
		
		Form nuevosDocumentos permiso=hayNuevaDocumentacionVerificacion{
			Wiki{"Existen nuevos documentos aportados por el solicitante y que no están incluídos en la verificación actual.
				  Por favor, pulse el siguiente botón para añadirlos a la verificación que está actualmente en curso."}
			Boton adjuntarNuevosDocumentos titulo="Añadir nuevos documentos"
		}
		
		Grupo tiposProceso titulo="Verificación de Tipos de Documentos" permiso=verificarTiposDocumentos {
			Tabla verificacionTipos titulo="Documentación Aún No Verificada" campo=SolicitudGenerica.documentacion.documentos
				popupEditar=PopUpDocTipos popupLeer=PopUpDocTipos {
				Columna campo=Documento.fechaSubida titulo="Fecha de Subida" ancho="100"
				Columna campo=Documento.fechaRegistro titulo="Fecha de Registro" ancho="100"
				Columna campo=Documento.descripcion titulo="Tipo" expandir
				Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
			}
			Form verificaTipos {
				Boton finalizaVerificarTipos titulo="Finaliza verificación de tipos"
			}
		}
		
		Grupo verificacionProceso titulo="Verificación de la documentación" permiso=verificarDocumentos { 
		
			Grupo botonDescargaReciboSolitud borde=false{
				Enlace campo=SolicitudGenerica.registro.justificante.urlDescarga titulo="Descargar Recibo Solicitud" destino="_blank" estilo="btn"
			}	
		
			Tabla verificacionDocumentos titulo="Documentación" campo=Verificacion.documentos
				paginaLeer=PaginaDocumentoVerificacionLeer 
				paginaEditar=PaginaDocumentoVerificacionEditar 
			{
				//Columna titulo="Id" campo=VerificacionDocumento.id ancho="40"
				Columna titulo="Fecha Presentación" campo=VerificacionDocumento.fechaPresentacion ancho="80"
				Columna titulo="Descripción" campo=VerificacionDocumento.descripcion expandir
				Columna titulo="Tipo documento" campo=VerificacionDocumento.nombreTipoDocumento
				Columna titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion ancho="100"
				Columna titulo="Multiplicidad" campo=VerificacionDocumento.identificadorMultiple ancho="80"
				Columna campo=VerificacionDocumento.linkUrlDescarga titulo="Descargar" ancho="100"	
			}
			
			Form guardarMotivoRequerimientoGeneral {
				AreaTexto motivoGeneralRequerimiento titulo="Motivo General de Requerimiento" campo=SolicitudGenerica.verificacionEnCurso.requerimiento.motivo
				Boton guardarRequerimientoMotivoRequerimientoGeneralbtn titulo="Guardar Motivo Requerimiento"
			}
			
		
			Form gnuevoRequerimientoBorradorPreliminar destino="_blank"{ 
				Boton obtenerBorradorPreliminar titulo="Obtener Borrador Requerimiento"
			} 
		}
		
		Grupo nuevosDocumentosTipos titulo="Verificación de Tipos de Documentos" permiso=verificacionConNuevosDoc {
			Tabla verificacionTiposNuevosDoc titulo="Documentación Aún No Verificada" campo=SolicitudGenerica.verificacionEnCurso.nuevosDocumentos
				popupEditar=PopUpDocNuevosTipos popupLeer=PopUpDocNuevosTipos {
				Columna campo=Documento.fechaSubida titulo="Fecha de Subida" ancho="100"
				Columna campo=Documento.fechaRegistro titulo="Fecha de Registro" ancho="100"
				Columna campo=Documento.descripcion titulo="Tipo" expandir
				Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
			}
			Form verificaTiposNuevosDoc {
				Boton finalizaVerificarTipos titulo="Finaliza verificación de tipos"
			}
		}
		
		Enlace volverVerificaciones titulo="Volver" pagina=AccesoVerificaciones estilo="btn"
	}
	
	Popup PopUpDocTipos titulo="Documentación" campo=SolicitudGenerica.documentacion.documentos {
		EditarArchivo documentoProceso campo=Documento requerido
	}
	
	Popup PopUpDocNuevosTipos titulo="Documentación" campo=SolicitudGenerica.verificacionEnCurso.nuevosDocumentos {
		EditarArchivo documentoProceso campo=Documento requerido
	}
	
	Pagina PaginaDocumentoVerificacionLeer campo=SolicitudGenerica.verificacionEnCurso.documentos{
		Grupo botonDescarga borde=false mostrarSiExpresion "verificacionDocumento.uriDocumento != null"{
			Enlace campo=VerificacionDocumento.urlDescarga titulo="Descargar" destino="_blank" estilo="boton"
		}
		Texto titulo="Descripción" campo=VerificacionDocumento.descripcion ancho="420px"
		Texto tipo titulo="Tipo documento" campo=VerificacionDocumento.nombreTipoDocumento ancho="420px"
		Texto tipoUri titulo="Uri tipo documento" campo=VerificacionDocumento.uriTipoDocumento ancho="420px"
		Fecha fechaPresentacion titulo="Fecha de presentación" campo=VerificacionDocumento.fechaPresentacion
		Combo estado titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion
		Grupo requerimientosNoValido titulo="Requerimiento/s del documento" mostrarSiCombo estado="noValido","noPresentado"{
			Grupo requerimientoTexto borde=false mostrarSiCombo estado="noValido","noPresentado"{
				AreaTexto requerimientoTexto titulo="Motivo requerimiento" campo=VerificacionDocumento.motivoRequerimiento
			}
			Grupo requerimientoCodigos borde=false mostrarSiCombo estado="noValido" {
				Tabla requerimientosDocumento titulo="Requerimientos" campo=VerificacionDocumento.codigosRequerimiento
					popupLeer=PopUpDocumentoRequerimientosVerificacion
				{
					Columna titulo="Código" campo=CodigoRequerimiento.codigo ancho="150"
					Columna titulo="Descripción" campo=CodigoRequerimiento.descripcionCorta expandir
				}
			}
		}
		Enlace volverVerificacionActual titulo="Volver" pagina=PaginaVerificacion estilo="btn"
	}
	
	Pagina PaginaDocumentoVerificacionEditar campo=SolicitudGenerica.verificacionEnCurso.documentos{
		Accion editar
		Grupo botonDescarga borde=false mostrarSiExpresion "verificacionDocumento.uriDocumento != null"{
			Enlace campo=VerificacionDocumento.urlDescarga titulo="Descargar" destino="_blank" estilo="boton"
		}
		Grupo noEditable permiso=noEditable borde=false{
			Texto titulo="Descripción" campo=VerificacionDocumento.descripcion ancho="420px"
			Texto tipo titulo="Tipo documento" campo=VerificacionDocumento.nombreTipoDocumento ancho="420px"
			Texto tipoUri titulo="Uri tipo documento" campo=VerificacionDocumento.uriTipoDocumento ancho="420px"
			Fecha fechaPresentacion titulo="Fecha de presentación" campo=VerificacionDocumento.fechaPresentacion
		}
		Combo estado titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion
		Grupo requerimientosNoValido titulo="Requerimiento/s del documento" mostrarSiCombo estado="noValido","noPresentado"{
			Grupo requerimientoTexto borde=false mostrarSiCombo estado="noValido","noPresentado"{
				AreaTexto requerimientoTexto titulo="Motivo requerimiento" campo=VerificacionDocumento.motivoRequerimiento
			}
			Grupo requerimientoCodigos borde=false mostrarSiCombo estado="noValido" {
				Tabla requerimientosDocumento titulo="Requerimientos" campo=VerificacionDocumento.codigosRequerimiento
					popupCrear=PopUpDocumentoRequerimientosVerificacionEditar
					popupEditar=PopUpDocumentoRequerimientosVerificacionEditar
					popupLeer=PopUpDocumentoRequerimientosVerificacion
					popupBorrar=PopUpDocumentoRequerimientosVerificacion
				{
					Columna titulo="Código" campo=CodigoRequerimiento.codigo ancho="150"
					Columna titulo="Descripción" campo=CodigoRequerimiento.descripcionCorta expandir
				}
			}
		}
		Enlace volverVerificacionActual titulo="Volver" pagina=PaginaVerificacion estilo="btn"
		
	}
	
	Popup PopUpDocumentoRequerimientosVerificacion titulo="Requerimiento" campo=VerificacionDocumento.codigosRequerimiento {
		Combo descripcionCorta titulo="Descripción Corta" campo=CodigoRequerimiento.descripcionCorta requerido
		Texto codigo titulo="Código" campo=CodigoRequerimiento.codigo
		AreaTexto descripcion titulo="Descripción" campo=CodigoRequerimiento.descripcion
	}
	
	Popup PopUpDocumentoRequerimientosVerificacionEditar titulo="Requerimiento" campo=VerificacionDocumento.codigosRequerimiento {
		Combo descripcionCorta titulo="Descripción Corta" campo=CodigoRequerimiento.descripcionCorta requerido
		Grupo descripciones borde=false permiso=noEditable{
			Texto codigo titulo="Código" campo=CodigoRequerimiento.codigo
			AreaTexto descripcion titulo="Descripción" campo=CodigoRequerimiento.descripcion
		}
	}
	
	Pagina Exclusion noForm permiso=accederSiSolicituNoBorrador{
		Tabla exclusion titulo="Codigos de Exclusión" campo=SolicitudGenerica.exclusion.codigos
		popup=PopUpExclusion
		{
			Columna titulo="Código" campo=CodigoExclusion.codigo
			Columna titulo="Descripción corta" campo=CodigoExclusion.tipoCodigo.descripcionCorta expandir
		}
		Form finalizarExclusion {
			Boton finalizarExclusionBoton titulo="Finalizar Exclusión"
		} 
	}
	
	Popup PopUpExclusion campo=SolicitudGenerica.exclusion.codigos {

		Combo descripcionCorta campo=CodigoExclusion.tipoCodigo.descripcionCorta titulo="Descripción Corta"
		Grupo permiso=noEditable {
			Texto codigo campo=CodigoExclusion.codigo titulo="Código"
			AreaTexto descripcion campo=CodigoExclusion.tipoCodigo.descripcion titulo="Descripcion" 
		}
	}
	
}