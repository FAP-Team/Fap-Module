Lista resolucionesDefinidas crearEnumerado {
	simpleTotal: "Simple Total"
	multipleTotal: "M√∫ltiple Todos Expedientes"
	multipleParcialExpedientes: "Multiple Seleccionando Expedientes"
}

Lista tipoResolucion crearEnumerado {
	provisional: "Provisional"
	definitiva: "Definitiva"
}

Lista modalidadResolucion crearEnumerado {
	resolucionSimple: "Simple"
	resolucionMultiple: "Multiple"
}

// Estados por los que va pasando la resoluci√≥n
Lista estadoResolucion crearEnumerado {
	borrador: "Borrador"	// Ha sido creada, y establecido su tipo y modalidad
	creada: "Creada"
	preparada: "Preparada"
	pendienteFirmaJefeServicio: "Pendiente firma Jefe Servicio"
	firmadaJefeServicio: "Firmada Jefe Servicio"
	pendienteFirmaDirector: "Pendiente firma Director"
	firmada: "Firmada"
	registrada: "Registrada"
	publicada: "Publicada"
	finalizada: "Finalizada"
}

Lista estadoTipoMultiple crearEnumerado {
	parcial: "Selecionar manualmente los expedientes"
	total: "Todos los expedientes"
}

Lista estadoLineaResolucion crearEnumerado {
	concedida: "Concedida"
	excluida: "Excluida"
}

Lista prioridadesFirmaEnPortafirma crearEnumerado {
	baja: "Baja"
	media: "Media"
	alta: "Alta"
}

Lista estadoPortafirma crearEnumerado {
	Borrador: "Borrador"
	Validandofirmas: "Validando firmas"
	Pendiente: "Pendiente"
	Enviada: "Enviada"
	Firmadayfinalizada: "Firmada y finalizada"
	Rechazada: "Rechazada"
	Cerrada: "Cerrada"
	Pendientevalidacionfirmas: "Perndiente de validaci√≥n de firmas"
}

// Una l√≠nea de resoluci√≥n afecta a un expediente (o Solicitud)
Entidad LineaResolucion {
	SolicitudGenerica solicitud noConstruct
	Lista estadoLineaResolucion estado
}

// Entidades de la resoluci√≥n
Entidad ResolucionFAP {
	/// Tipo de los posibles entre los definidos de la resoluci√≥n
	Lista resolucionesDefinidas tipoDefinidoResolucion
	
	Lista modalidadResolucion modalidad		// Simple, Multiple
	Lista tipoResolucion tipo				// Provisional, Definitiva
	Lista estadoTipoMultiple tipoMultiple	// Parcial, Total

	Boolean firmarJefeServicio				// Debe ser firmada por el Jefe de Servicio
	String jefeDeServicio					// Jefe de Servicio Solicitado
	Boolean firmarDirector					// Debe ser firmada por el Director
	Boolean permitirPortafirma
	Boolean permitirRegistrar
	Boolean permitirPublicar
	
	String prioridadFirma
	DateTime fechaTopeFirma
	
	Lista estadoResolucion estado			// borrador, creada, .... finalizada
	
	// Descripci√≥n de la Resoluci√≥n
	String descripcion length=2500
	
	// modalidad=Simple   --> Una sola l√≠nea de resoluci√≥n
	// modalidad=M√∫ltiple --> Varias l√≠neas de resoluci√≥n
	OneToMany LineaResolucion lineasResolucion
	
	Boolean conBaremacion default = 'false'

	String tituloInterno					// Obligatorio.
	String sintesis
	String observaciones
	Integer folio_inicio					// Folio de inicio en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer folio_final						// Folio final en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer numero_folios
	Integer numero							// N√∫mero de resoluci√≥n. Valor devuelto por SW de Registro de Resoluciones
	DateTime fechaRegistroResolucion		// Fecha de la resoluci√≥n
	String areasResolucion
	String tiposResolucion

	String idSolicitudFirma
	String codigoResolucion

	//DateTime fechaFinAceptacion						// Deber√≠a coincidir con la fecha fin reformulacion y fin alegacion si existe
	//DateTime fechaPublicacion						// Fecha de Publicaci√≥n en el tabl√≥n de anuncios de la sede
	//DateTime fechaBOC								// Fecha de Publicaci√≥n en el BOC
	DateTime fechaIncioPreparacion		// Fecha en la que se empieza a preparar la resoluci√≥n y se crean las l√≠neas
	//DateTime fechaFinPreparacion					// Fecha en la que se finaliza la resoluci√≥n
	//Documento docResolucion							// Documento de Resoluci√≥n
	//OneToMany Documento docAnexoResolucion //Anexos individuales de resolucion
	OneToMany Documento docConsultaPortafirmasResolucion //Documentos de Consulta Portafirmas.

	Registro registro
	OneToMany Interesado destinatarios transient
	ExpedienteAed expedienteAed
	
		//Long IDPortafirma					//ID devuelto por el Servicio WEb del Portafirmas
		//String estadoPortafirmas			//??Pendiente de revisar
	SolicitudPortafirma solicitudFirmaJefeServicio
	SolicitudPortafirma solicitudFirmaDirector

	//Lista estadoResolucionConcesion estado		//Estado de la resoluci√≥n: 1.0:Creada,2.0:Preparada, 3.0:En_Firma, 4.0:Firmada, 5.0:Registrada, 6.0:Publicada
		//OneToMany LineaResolucion lineasResolucion
		// OneToMany ExpedientesAlegados expedientesAlegados
		// OneToMany ExpedientesNoAceptados expedientesNoAceptados
	//Agente destinatarioPortafirma transient

	//Para firmar por jefe de servicio
	//Firmantes firmantesJefeDeServicio
		////Para poder firmar y crear el el metodo calcular firmantes
		//Solicitante solicitanteJefeServicio transient

}

Entidad SolicitudPortafirma{
	String codSolicitudPf
	String estadoPf
	String tituloPf
	String descripcionPf
	String solicitantePf
	String destinatarioPf
	String emailNotificacionPf
	String comentarioPf
	String urlCorreoRedireccionPf
	String flujoAnteriorPf
}
	
	

Formulario Principal {
	Pagina Resoluciones titulo="Lista de Resoluciones" {
		Tabla listaResoluciones titulo="Resoluciones" campo=ResolucionFAP paginaCrear=NuevaResolucion paginaEditar=EditarResolucion
		{
			Columna campo=ResolucionFAP.id titulo="ID"
			Columna campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resoluci√≥n"
			Columna campo=ResolucionFAP.tipo titulo="Tipo"
			Columna campo=ResolucionFAP.modalidad titulo="Modalidad"
		}
	}
	
	Pagina NuevaResolucion titulo ="Resoluci√≥n" campo=ResolucionFAP perteneceA="Principal" {
		Accion crear redirigir=EditarResolucion
		Grupo gIniciarTipo titulo="Seleccionar Resoluci√≥n" {
			Combo campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resoluci√≥n"
			Combo campo=ResolucionFAP.tipo titulo="Tipo"
			Texto campo=ResolucionFAP.tituloInterno titulo="T√≠tulo" requerido
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripci√≥n" requerido
			AreaTexto campo=ResolucionFAP.observaciones titulo="Observaciones"
			AreaTexto campo=ResolucionFAP.sintesis titulo="S√≠ntesis"
			Combo areasResolucion campo=ResolucionFAP.areasResolucion titulo="√?rea resoluci√≥n"
			Combo tiposResolucion campo=ResolucionFAP.tiposResolucion titulo="Tipo resoluci√≥n"
			Check campo=ResolucionFAP.conBaremacion titulo="¬øTiene Baremaci√≥n?"
		}
	}

	Pagina EditarResolucion titulo="Resoluci√≥n" campo=ResolucionFAP perteneceA="Principal" noForm {
		
		Grupo titulo="Seleccionar expediente" permiso=seleccionarUnExpediente {
			Tabla tablaExpedientesUnico campo=SolicitudGenerica seleccionable="Crear Resolucion" {
				Columna titulo = "ID" campo=SolicitudGenerica.id ancho = "40"
				Columna titulo = "Expediente" campo=SolicitudGenerica.expedienteAed.idAed
				Columna titulo = "Estado Interno" campo=SolicitudGenerica.estadoValue ancho = "120"
				Columna titulo = "Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
			}
		}
		
		Grupo mostrarSiCampo ResolucionFAP.modalidad="resolucionMultiple" borde=false  {
			Grupo titulo="Expedientes a Resolver" permiso=seleccionarExpedientes {
			
				Form formSeleccionarExpedientes {
					Grupo titulo="Seleccionar Expedientes"{
						Combo cmbTipo campo=ResolucionFAP.tipoMultiple titulo="Seleccionar" requerido
						Boton btnGuardar titulo="Guardar"
					}
				}
			
				Tabla tablaExpedientes campo=SolicitudGenerica seleccionable="Seleccionar" {
					Columna titulo="ID" campo=SolicitudGenerica.id ancho="40"
					Columna titulo="Expediente" campo=SolicitudGenerica.expedienteAed.idAed
					Columna titulo="Estado Interno" campo=SolicitudGenerica.estadoValue ancho="120"
					Columna titulo="Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
				}
			}
			
			Grupo gCrearResolucion titulo="Preparar la Resoluci√≥n" permiso=noSeleccionarExpedientes {
				Wiki { "Si su resoluci√≥n est√° lista para crearla, pulse el siguiente bot√≥n." }
				Form frmCrearResolucion {
					Boton  btnCrearResolucion titulo="Crear Resoluci√≥n"
				}
			}
		}
		
    	Grupo gCerrarResolucion titulo="Preparar la Resoluci√≥n" permiso=prepararResolucion {
			Wiki { "Si su resoluci√≥n est√° lista para firmar, pulse el siguiente bot√≥n." }
			Tabla lineasResolucionAntesPreparar campo=ResolucionFAP.lineasResolucion titulo="Lineas Resoluci√≥n" popupEditar=ModificarEstadoExpediente popupLeer=VerExpediente {
				Columna campo=LineaResolucion.id titulo="Id"
				Columna campo=LineaResolucion.solicitud.expedienteAed.idAed titulo="Expediente"
				Columna campo=LineaResolucion.solicitud.estado titulo="Estado Solicitud"
				Columna campo=LineaResolucion.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
				Columna campo=LineaResolucion.solicitud.solicitante.nombreCompleto titulo="Solicitante"
				Columna campo=LineaResolucion.estado titulo="Estado"
			}
			Form prepararResolucion {
				Boton  btnPrepararResolucion titulo="Cerrar Resoluci√≥n"
			}
		}
		
		Grupo gPrepararSolicitudFirmaPortafirma titulo="Preparar Solicitud de Firma" permiso=solicitudFirmaResolucion {
			
			// Si el Agente es un JefeDeServicio, puede firmar directamente
			Grupo grSolicitarFirmaPorJefeSevicio borde=false permiso=portafirmaJefeServicio {
				Grupo grFirmaJefeServicio borde=false {
	     			Form frmFirmarJefeServicio redirigir=EditarResolucion {
						FirmaSimple firFirmarResolucion titulo="Firmar Resolucion" documento=ResolucionFAP.registro.oficial
							listaFirmantes=ResolucionFAP.registro.firmantes.todos calcularFirmantes="resolucionFAP.calcularFirmantes()"
					}
					Wiki {"Nota: La operaci√≥n de firmar y registrar puede tardar varios minutos."}
				}
			}
			
			Grupo gEnviarFirmaJSPortafirma permiso=firmaJefeServicio {
				
				Enlace titulo="Obtener documento de Resoluci√≥n" estilo="btn" campo=ResolucionFAP.registro.borrador.urlDescarga
				Tabla documentosPortafirma titulo="Documentos de Consulta Portafirmas" campo=ResolucionFAP.docConsultaPortafirmasResolucion paginaCrear=DocConsultaPortafirmaCrear paginaEditar=DocConsultaPortafirmaEditar {
					Columna campo=Documento.tipo titulo="Tipo"
					Columna campo=Documento.descripcionVisible titulo="Descripci√≥n" expandir
					Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Ver" ancho="80"
				}

				Form formSelectJefeServicio {
					Combo selectJefeServicio campo=ResolucionFAP.jefeDeServicio titulo="Jefe de Servicio" requerido
					Combo selectPrioridadFirma campo=ResolucionFAP.prioridadFirma titulo="Prioridad Firma" requerido
					Fecha fechaTopeFirma campo=ResolucionFAP.fechaTopeFirma titulo="Fecha Tope Firma" requerido
					Boton enviarFirmaJSPortafirma titulo="Solicitar firma al Jefe Servicio"
				} 
			}
			
			Grupo gEsperaFirmaJSPortafirma permiso=esperaFirmaJefeServicio titulo="Firma del Jefe de Servicio"{
				Grupo permiso=noEditable borde=false {
					Combo selectJefeServicio campo=ResolucionFAP.jefeDeServicio titulo="Jefe de Servicio Seleccionado"
				}
				Wiki { "_El jefe de servicio a√∫n no ha firmado en el portafirma_" } 
			}
			
			Grupo gEnviarFirmaDirectorPortafirma permiso=firmaDirector titulo="Firma del Director" {
				Form firmaDirectorPortafirma {
					Boton enviarFirmaDirectorPortafirma titulo="Solicitar firma al Director"
				}
			}
			
			Grupo gEsperaFirmaDirectorPortafirma permiso=esperaFirmaDirector titulo="Firma Director"{
				Wiki { "_El Director a√∫n no ha firmado en el portafirma. Si quiere comprobar si ya se firm√≥ darle al siguiente bot√≥n._" }
				Form frmComprobarFirmas {
					Boton comprobarFirmado titulo="Comprobar"
				}
			}
		}
		
		Grupo gRegistrarResolucion titulo="Registro de la Resoluci√≥n" permiso=registrarResolucion {
			Grupo borde=false {
				Enlace campo=ResolucionFAP.registro.oficial.urlDescarga titulo="Obtener Documento Resoluci√≥n" destino="_blank" estilo="btn"
			}
			
			Wiki { "Para proceder a registrar la resoluci√≥n pulse el siguiente bot√≥n: \"Registrar resoluci√≥n\"" }
			
			Form enviarRegistrarResolucion {
      			Boton btnRegistrarResolucion titulo="Registrar Resoluci√≥n"
    		}
		}
		
		Grupo gPublicarResolucion titulo="Publicar la Resoluci√≥n" permiso=publicarResolucion {
			Wiki { "Publicar resoluci√≥n" }
		}
		
		Grupo gFinalizarResolucion titulo="Finalizar la Resoluci√≥n" permiso=finalizarResolucion {
			Wiki { "Finalizar resoluci√≥n" }
		}
		
		Grupo gDatosResolucion titulo="Datos de la Resoluci√≥n actual" permiso=mostrarDatosResolucion {
			Combo campo=ResolucionFAP.estado titulo="Estado"
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripci√≥n"
			Combo campo=ResolucionFAP.tipo titulo="Tipo de Resoluci√≥n" requerido
			Combo campo=ResolucionFAP.modalidad titulo="Modalidad de la Resoluci√≥n" requerido
			Check campo=ResolucionFAP.conBaremacion titulo="¬øTiene Baremaci√≥n?"
			Tabla lineasResolucion campo=ResolucionFAP.lineasResolucion titulo="Lineas Resoluci√≥n" {
				Columna campo=LineaResolucion.id titulo="Id"
				Columna campo=LineaResolucion.solicitud.expedienteAed.idAed titulo="Expediente"
				Columna campo=LineaResolucion.solicitud.estado titulo="Estado Solicitud"
				Columna campo=LineaResolucion.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
				Columna campo=LineaResolucion.solicitud.solicitante.nombreCompleto titulo="Solicitante"
				Columna campo=LineaResolucion.estado titulo="Estado"
			}
			Texto campo=ResolucionFAP.folio_inicio titulo="Folio Inicial"
			Texto campo=ResolucionFAP.folio_final titulo="Folio Final"
			Texto campo=ResolucionFAP.codigoResolucion titulo="Numero"
			Fecha campo=ResolucionFAP.fechaRegistroResolucion titulo="Fecha Resoluci√≥n"
			Wiki { " Faltan m√°s cosas ................ " }
		}
	}
	
	Popup ModificarEstadoExpediente campo=ResolucionFAP.lineasResolucion {
		Grupo grNoEditable borde=false permiso=noEditable {
			Texto campo=LineaResolucion.solicitud.expedienteAed.idAed titulo="Expediente"
			Texto campo=LineaResolucion.solicitud.estado titulo="Estado Solicitud"
			Texto campo=LineaResolucion.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
			Texto campo=LineaResolucion.solicitud.solicitante.nombreCompleto titulo="Solicitante"
		}
		Grupo grEditable borde=false {
			Combo campo=LineaResolucion.estado titulo="Estado"
		}
	}
	
	Popup VerExpediente campo=ResolucionFAP.lineasResolucion {
		Texto campo=LineaResolucion.solicitud.expedienteAed.idAed titulo="Expediente"
		Texto campo=LineaResolucion.solicitud.estado titulo="Estado Solicitud"
		Texto campo=LineaResolucion.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
		Texto campo=LineaResolucion.solicitud.solicitante.nombreCompleto titulo="Solicitante"
		Combo campo=LineaResolucion.estado titulo="Estado"
	}

	Pagina DocConsultaPortafirmaCrear campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion crear redirigir=EditarResolucion
		SubirArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
	}
	
	Pagina DocConsultaPortafirmaEditar campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion editar redirigir=EditarResolucion
		EditarArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
	}
}

Permiso sinIniciarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion = null
		or resolucion.tipo = null
	return
		[editar crear]
	when
		resolucion.tipo != null
	return
		visible
}

Permiso mostrarDatosResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado != "borrador" and
		agente.rolActivo in ("administrador", "gestor", "jefeservicio")
	return 
		visible
}

Permiso seleccionarExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "borrador" and
		resolucion.tipoMultiple = "parcial" and
		agente.rolActivo in ("administrador", "gestor", "jefeservicio")
	return
		all
}

Permiso portafirmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo = "jefeservicio"
		and resolucion.firmarJefeServicio = "true"
		and resolucion.estado = "preparada"
	return
		all
}

Permiso seleccionarUnExpediente {
	vars
		ResolucionFAP resolucion
	when
		resolucion.modalidad="resolucionSimple" and
		resolucion.estado = "borrador" and
		agente.rolActivo in ("administrador", "gestor", "jefeservicio")
	return
		all
}

Permiso noSeleccionarExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "borrador" and
		resolucion.tipoMultiple = "total" and
		agente.rolActivo in ("administrador", "gestor", "jefeservicio")
	return
		all
}

Permiso solicitudFirmaResolucion {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador", "jefeservicio")
		and resolucion.estado in ("preparada", "pendienteFirmaJefeServicio", "firmadaJefeServicio", "pendienteFirmaDirector")
	return
		all
}

Permiso prepararResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "creada" and
		agente.rolActivo in ("administrador", "gestor", "jefeservicio")
	return
		all
}

/**
 * Visible el env√≠o a portafirma para el Jefe de Servicio
 */
Permiso firmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("preparada")
		and resolucion.firmarJefeServicio = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
	when
		resolucion.estado in ("preparada")
		and resolucion.firmarJefeServicio = "true"
	return
		visible
}

Permiso esperaFirmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("pendienteFirmaJefeServicio")
		and resolucion.firmarJefeServicio = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
	when
		resolucion.estado in ("pendienteFirmaJefeServicio")
		and resolucion.firmarJefeServicio = "true"
	return
		visible
}


/**
 * Visible el env√≠o a portafirma para el Director
 */
Permiso firmaDirector {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("firmadaJefeServicio")
		and resolucion.firmarDirector = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
	when
		resolucion.estado in ("firmadaJefeServicio")
		and resolucion.firmarDirector = "true"
	return
		visible
}

Permiso esperaFirmaDirector {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("pendienteFirmaDirector")
		and resolucion.firmarDirector = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
	when
		resolucion.estado in ("pendienteFirmaDirector")
		and resolucion.firmarDirector = "true"
	return
		visible
}

Permiso registrarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("firmada")
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
}

Permiso publicarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("registrada")
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
}

Permiso finalizarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("publicada")
		and agente.rolActivo in ("gestor", "administrador", "jefeservicio")
	return
		all
}