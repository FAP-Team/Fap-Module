Lista resolucionesDefinidas crearEnumerado {
	simpleTotal: "Simple Total"
	multipleTotal: "M√∫ltiple Todos Expedientes"
	multipleParcialExpedientes: "Multiple Seleccionando Expedientes"
	simpleEjecucion: "Simple Fase de Ejecuci√≥n"
	multipleEjecucion: "M√∫ltiple Fase de Ejecuci√≥n"
}

Lista tipoResolucion crearEnumerado {
	provisional: "Provisional"
	definitiva: "Definitiva"
}

Lista modalidadResolucion crearEnumerado {
	resolucionSimple: "Simple"
	resolucionMultiple: "Multiple"
}

Lista areasResolucion crearEnumerado {
	
}

Lista tiposResolucion crearEnumerado {
	
}

// Estados por los que va pasando la resoluci√≥n
Lista estadoResolucion crearEnumerado {
	borrador: "Borrador"	// Ha sido creada, y establecido su tipo y modalidad
	creada: "Creada"
	preparada: "Preparada"
	pendienteFirmaJefeServicio: "Pendiente firma Jefe Servicio"
	firmadaJefeServicio: "Firmada Jefe Servicio"
	pendienteFirmaDirector: "Pendiente firma Director"
	firmada: "Firmada"
	registrada: "Registrada"
	publicada: "Publicada"
	notificada: "Notificada"
	publicadaYNotificada: "Publicada y nofitificada"
	finalizada: "Finalizada"
	afectado: "Expediente afectado"
}

Lista estadosDocBaremacion crearEnumerado{
	generado: "Documentos Generado"
	clasificado: "Documentos Clasificado"
}

Lista estadoTipoMultiple crearEnumerado {
	parcial: "Selecionar manualmente los expedientes"
	total: "Todos los expedientes"
}

Lista estadoLineaResolucion crearEnumerado {
	concedida: "Concedida"
	excluida: "Excluida"
	denegada: "Denegada"
	afectada: "Expediente afectado"
}

Lista prioridadesFirmaEnPortafirma crearEnumerado {
	baja: "Baja"
	media: "Media"
	alta: "Alta"
}

Lista estadoPortafirma crearEnumerado {
	Borrador: "Borrador"
	Validandofirmas: "Validando firmas"
	Pendiente: "Pendiente"
	Enviada: "Enviada"
	Firmadayfinalizada: "Firmada y finalizada"
	Rechazada: "Rechazada"
	Cerrada: "Cerrada"
	Pendientevalidacionfirmas: "Perndiente de validaci√≥n de firmas"
}

Lista estadoResolucionPublicacion crearEnumerado {
	publicada: "Publicada"
	documentoGenerado: "Documento Resoluci√≥n Generado"
	
	estadoFecha: "Publicacion: Estado y Fecha"
	//copiaExpedientes: "Publicacion: Copiados Expedientes" ES publicada
	firmaBaremacionResolucion: "Firmados Documentos de Baremaci√≥n"
	
}

Lista estadoResolucionNotificacion crearEnumerado {
	notificada: "Notificada"
}

// Una l√≠nea de resoluci√≥n afecta a un expediente (o Solicitud)
Entidad LineaResolucionFAP {
	SolicitudGenerica solicitud noConstruct
	Lista estadoLineaResolucion estado
	Double puntuacionBaremacion
	Moneda importeTotal
	Moneda importeConcedido
	Documento docBaremacion 			// Documento visible para el solicitante de la Baremaci√≥n del Expediente
	Documento docEvaluacionCompleto		// Documento con la Evaluaci√≥n Completa
	Documento docEvaluacionCompletoConComentarios 	// Documento con la Evaluaci√≥n Completa con Comentarios de la Admin
	//Documento documentoOficioRemision 
	Registro registro // Oficio de remisi√≥n
	Boolean notificada default="false"
}

// Entidades de la resoluci√≥n
Entidad ResolucionFAP {
	/// Tipo de los posibles entre los definidos de la resoluci√≥n
	Lista resolucionesDefinidas tipoDefinidoResolucion
	
	Lista modalidadResolucion modalidad		// Simple, Multiple
	Lista tipoResolucion tipo				// Provisional, Definitiva
	Lista estadoTipoMultiple tipoMultiple	// Parcial, Total

	Boolean firmarJefeServicio				// Debe ser firmada por el Jefe de Servicio
	String jefeDeServicio					// Jefe de Servicio Solicitado
	Boolean firmarDirector					// Debe ser firmada por el Director
	Boolean permitirPortafirma
	Boolean permitirRegistrar
	Boolean permitirPublicar
	
	String prioridadFirma
	DateTime fechaTopeFirma
	
	Lista estadoResolucion estado			// borrador, creada, .... finalizada
	Lista estadoResolucionNotificacion estadoNotificacion
	Lista estadoResolucionPublicacion estadoPublicacion
	Lista estadosDocBaremacion estadoDocBaremacionResolucion
	Lista estadosDocBaremacion estadoInformeBaremacionConComentarios
	Lista estadosDocBaremacion estadoInformeBaremacionSinComentarios
	
	// Descripci√≥n de la Resoluci√≥n
	String descripcion length=2500
	
	// modalidad=Simple   --> Una sola l√≠nea de resoluci√≥n
	// modalidad=M√∫ltiple --> Varias l√≠neas de resoluci√≥n
	OneToMany LineaResolucionFAP lineasResolucion
	
	Boolean conBaremacion default = 'false'
	Boolean oficialEvaluacionCompleto default = 'false'

	String tituloInterno					// Obligatorio.
	String sintesis length=1000
	String observaciones length=1000
	Integer folio_inicio					// Folio de inicio en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer folio_final						// Folio final en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer numero_folios
	Integer numero							// N√∫mero de resoluci√≥n. Valor devuelto por SW de Registro de Resoluciones
	DateTime fechaRegistroResolucion		// Fecha de la resoluci√≥n
	Lista areasResolucion areasResolucion
	Lista tiposResolucion tiposResolucion

	String idSolicitudFirma
	String codigoResolucion

	DateTime fechaFinAceptacion						// Deber√≠a coincidir con la fecha fin reformulacion y fin alegacion si existe
	//DateTime fechaPublicacion						// Fecha de Publicaci√≥n en el tabl√≥n de anuncios de la sede
	//DateTime fechaBOC								// Fecha de Publicaci√≥n en el BOC
	DateTime fechaIncioPreparacion		// Fecha en la que se empieza a preparar la resoluci√≥n y se crean las l√≠neas
	//DateTime fechaFinPreparacion					// Fecha en la que se finaliza la resoluci√≥n
	//Documento docResolucion							// Documento de Resoluci√≥n
	//OneToMany Documento docAnexoResolucion //Anexos individuales de resolucion
	OneToMany Documento docConsultaPortafirmasResolucion //Documentos de Consulta Portafirmas.

	Registro registro
	Boolean copiadoExpedientes default="false" //Indica si el doc de resolucion ha sido copiado en los expedientes
	OneToMany Interesado destinatarios transient
	ExpedienteAed expedienteAed
	
		//Long IDPortafirma					//ID devuelto por el Servicio WEb del Portafirmas
		//String estadoPortafirmas			//??Pendiente de revisar
	SolicitudPortafirmaFAP solicitudFirmaJefeServicio
	SolicitudPortafirmaFAP solicitudFirmaDirector

	//Lista estadoResolucionConcesion estado		//Estado de la resoluci√≥n: 1.0:Creada,2.0:Preparada, 3.0:En_Firma, 4.0:Firmada, 5.0:Registrada, 6.0:Publicada
		//OneToMany LineaResolucion lineasResolucion
		// OneToMany ExpedientesAlegados expedientesAlegados
		// OneToMany ExpedientesNoAceptados expedientesNoAceptados
	//Agente destinatarioPortafirma transient

	Agente hacePeticionPortafirma noConstruct
	//Para firmar por jefe de servicio
	//Firmantes firmantesJefeDeServicio
		////Para poder firmar y crear el el metodo calcular firmantes
		//Solicitante solicitanteJefeServicio transient

}

Entidad SolicitudPortafirmaFAP {
	String codSolicitudPf
	String estadoPf
	String tituloPf
	String descripcionPf
	String solicitantePf
	String destinatarioPf
	String emailNotificacionPf
	String comentarioPf
	String urlCorreoRedireccionPf
	String flujoAnteriorPf
}
	
	

Formulario Principal {
	Pagina ResolucionesFAP titulo="Lista de Resoluciones" {
		Tabla listaResoluciones titulo="Resoluciones" campo=ResolucionFAP paginaCrear=NuevaResolucion paginaEditar=EditarResolucion
		{
			Columna campo=ResolucionFAP.id titulo="ID" ancho="40"
			Columna campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resoluci√≥n"
			Columna campo=ResolucionFAP.tipo titulo="Tipo" ancho="100"
			Columna campo=ResolucionFAP.estado titulo="Estado"
			Columna campo=ResolucionFAP.modalidad titulo="Modalidad"
		}
	}
	
	Pagina NuevaResolucion titulo ="Resoluci√≥n" campo=ResolucionFAP perteneceA="Principal" {
		Accion crear redirigir=EditarResolucion
		Grupo gIniciarTipo titulo="Seleccionar Resoluci√≥n" {
			Combo tipoDefinidoResolucion campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resoluci√≥n"
			Combo campo=ResolucionFAP.tipo titulo="Tipo"
			Texto campo=ResolucionFAP.tituloInterno titulo="T√≠tulo" requerido
			AreaTexto campo=ResolucionFAP.sintesis titulo="S√≠ntesis" requerido
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripci√≥n"
			AreaTexto campo=ResolucionFAP.observaciones titulo="Observaciones"
			Combo areasResolucion campo=ResolucionFAP.areasResolucion titulo="√?rea resoluci√≥n" requerido
			Combo tiposResolucion campo=ResolucionFAP.tiposResolucion titulo="Tipo resoluci√≥n" requerido
			Grupo mostrarBaremacion borde=false mostrarSiExpresion"properties.FapProperties.getBoolean(\"fap.resolucion.baremacion.permitida\") == true"{
				Check campo=ResolucionFAP.conBaremacion titulo="¬øTiene Baremaci√≥n?"
			}
		}
	}

	Pagina EditarResolucion titulo="Resoluci√≥n" campo=ResolucionFAP perteneceA="Principal" noForm {
		
		Grupo titulo="Seleccionar expediente" permiso=seleccionarUnExpediente {
			Tabla tablaExpedientesUnico campo=SolicitudGenerica seleccionable="Crear Resolucion" {
				Columna titulo = "ID" campo=SolicitudGenerica.id ancho = "40"
				Columna titulo = "Expediente" campo=SolicitudGenerica.expedienteAed.idAed
				Columna titulo = "Estado Interno" campo=SolicitudGenerica.estadoValue ancho = "120"
				Columna titulo = "Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
			}
		}
		
		Grupo mostrarSiCampo ResolucionFAP.modalidad="resolucionMultiple" borde=false  {
			Grupo titulo="Expedientes afectados" permiso=seleccionarExpedientes {
			
				Form formSeleccionarExpedientes {
					Grupo titulo="Seleccionar Expedientes"{
						Combo cmbTipo campo=ResolucionFAP.tipoMultiple titulo="Seleccionar" requerido
						Boton btnGuardar titulo="Guardar"
					}
				}
			
				Tabla tablaExpedientes campo=SolicitudGenerica seleccionable="Seleccionar" {
					Columna titulo="ID" campo=SolicitudGenerica.id ancho="40"
					Columna titulo="Expediente" campo=SolicitudGenerica.expedienteAed.idAed
					Columna titulo="Estado Interno" campo=SolicitudGenerica.estadoValue ancho="120"
					Columna titulo="Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
				}
			}
			
			Grupo gCrearResolucion titulo="Preparar la Resoluci√≥n" permiso=noSeleccionarExpedientes {
				Wiki { "Si su resoluci√≥n est√° lista para crearla, pulse el siguiente bot√≥n." }
				Form frmCrearResolucion {
					Boton  btnCrearResolucion titulo="Crear L√≠neas de Resoluci√≥n"
				}
			}
		}
		
    	Grupo gCerrarResolucion titulo="Preparar la Resoluci√≥n" permiso=prepararResolucion {
			Wiki { "Si su resoluci√≥n est√° lista para firmar, pulse el siguiente bot√≥n." }
			Tabla lineasResolucionAntesPreparar campo=ResolucionFAP.lineasResolucion titulo="Lineas Resoluci√≥n" popupEditar=ModificarEstadoExpediente popupLeer=VerExpediente {
				Columna campo=LineaResolucionFAP.id titulo="Id" expandir
				Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente" expandir
				Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud" expandir
				Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante" expandir
				Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante" expandir
				Columna campo=LineaResolucionFAP.estado titulo="Estado" expandir
			}
			Form prepararResolucion {
				Boton  btnPrepararResolucion titulo="Preparar Resoluci√≥n"
			}
		}
		
		Grupo gPrepararSolicitudFirmaPortafirma titulo="Preparar Solicitud de Firma" permiso=solicitudFirmaResolucion {
			
			// Si el Agente es un JefeDeServicio, puede firmar directamente
			Grupo grSolicitarFirmaPorJefeSevicio borde=false permiso=portafirmaJefeServicio {
				Grupo grFirmaJefeServicio borde=false {
	     			Form frmFirmarJefeServicio redirigir=EditarResolucion {
						FirmaSimple firFirmarResolucion titulo="Firmar Resolucion" documento=ResolucionFAP.registro.oficial
							listaFirmantes=ResolucionFAP.registro.firmantes.todos calcularFirmantes="resolucionFAP.calcularFirmantes()"
					}
					Wiki {"Nota: La operaci√≥n de firmar y registrar puede tardar varios minutos."}
				}
			}
			
			Grupo gEnviarFirmaJSPortafirma permiso=firmaJefeServicio borde=false {
				Grupo documentoResolucionGenerado borde=false mostrarSiExpresion "properties.FapProperties.getBoolean(\"fap.resoluciones.generarDocumentoResolucion\")==true"{
					Enlace enlaceDocumentoResolucionNoGenerado titulo="Obtener documento de Resoluci√≥n" campo=ResolucionFAP.registro.borrador.urlDescarga destino="_blank" estilo="btn"
				}
				Grupo documentoResolucionNoGenerado borde=false mostrarSiExpresion "properties.FapProperties.getBoolean(\"fap.resoluciones.generarDocumentoResolucion\")==false"{
					Enlace enlaceDocumentoResolucionGenerado titulo="Obtener documento de Resoluci√≥n" campo=ResolucionFAP.registro.oficial.urlDescarga destino="_blank" estilo="btn"
				}

				Tabla documentosPortafirma titulo="Documentos de Consulta Portafirmas" campo=ResolucionFAP.docConsultaPortafirmasResolucion paginaCrear=DocConsultaPortafirmaCrear paginaEditar=DocConsultaPortafirmaEditar {
					Columna campo=Documento.tipo titulo="Tipo"
					Columna campo=Documento.descripcionVisible titulo="Descripci√≥n" expandir
					Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Ver" ancho="80"
				}
				Form formSelectJefeServicio {
					Combo selectJefeServicio campo=ResolucionFAP.jefeDeServicio titulo="Destinatario Portafirma" requerido
					Combo selectPrioridadFirma campo=ResolucionFAP.prioridadFirma titulo="Prioridad Firma" requerido
					Fecha fechaTopeFirma campo=ResolucionFAP.fechaTopeFirma titulo="Fecha Tope Firma" requerido
					Texto campo=ResolucionFAP.numero_folios titulo="N¬∫ Folios" requerido ayuda = "N√∫mero de folios del documento de la resoluci√≥n" (popover)
					Boton enviarFirmaJSPortafirma titulo="Enviar al portafirma"
				} 
			}
			
			Grupo gEsperaFirmaJSPortafirma permiso=esperaFirmaJefeServicio titulo="Firma del Jefe de Servicio"{
				Grupo permiso=noEditable borde=false {
					Combo selectJefeServicio campo=ResolucionFAP.jefeDeServicio titulo="Jefe de Servicio Seleccionado"
				}
				Wiki { "_El jefe de servicio a√∫n no ha firmado en el portafirma_" } 
			}
			
			Grupo gEnviarFirmaDirectorPortafirma permiso=firmaDirector titulo="Firma del Director" {
				Form firmaDirectorPortafirma {
					Boton enviarFirmaDirectorPortafirma titulo="Solicitar firma al Director"
				}
			}
			
			Grupo gEsperaFirmaDirectorPortafirma permiso=esperaFirmaDirector titulo="Firma Director"{
				Wiki { "_El Director a√∫n no ha firmado en el portafirma. Si quiere comprobar si ya se firm√≥ darle al siguiente bot√≥n._" }
				Form frmComprobarFirmas {
					Boton comprobarFirmado titulo="Comprobar"
				}
			}
		}
		
		Grupo gRegistrarResolucion titulo="Registro de la Resoluci√≥n" permiso=registrarResolucion {
			Grupo borde=false {
				Enlace campo=ResolucionFAP.registro.oficial.urlDescargaFirmado titulo="Obtener Documento Resoluci√≥n" destino="_blank" estilo="btn"
			}
			
			Wiki { "Para proceder a registrar la resoluci√≥n pulse el siguiente bot√≥n: \"Registrar resoluci√≥n\"" }
			
			Form enviarRegistrarResolucion {
      			Boton btnRegistrarResolucion titulo="Registrar Resoluci√≥n"
    		}
		}
		
		Grupo gPublicarResolucion titulo="Publicar la Resoluci√≥n en el tabl√≥n de Anuncios" permiso=publicarResolucion mostrarSiExpresion "(properties.FapProperties.getBoolean(\"fap.resoluciones.publicarTablonAnuncios\")==true)" {
			Form publicacionResolucion {
				Enlace publicarResolucionPasos titulo="Publicar la Resoluci√≥n" pagina=PaginaPublicarResolucion estilo="btn"
			}
		}
		
		Grupo gNotificarResolucion titulo="Notificar la Resoluci√≥n" permiso=notificarResolucion mostrarSiExpresion "(properties.FapProperties.getBoolean(\"fap.resoluciones.notificar\")==true)" {
			Form formNotificarResolucion {
				Enlace notificarResolucionPasos titulo="Notificar la Resoluci√≥n" pagina=PaginaNotificarResolucion estilo="btn"
			}
		}
				
		Grupo gDocumentoDeResolucion borde=false /*titulo="Documento de Resoluci√≥n" */ permiso=verDocumentoResolucion {
			Form copiaExpediente permiso=permisoCopiaExpedientes{
				Grupo grupoCopiaExpediente titulo="Copia de la Resoluci√≥n a los expedientes afectados"{
					Enlace campo=ResolucionFAP.registro.oficial.urlDescargaFirmado titulo="Resoluci√≥n" destino="_blank" estilo="boton"
					Boton btnCopiaExpediente titulo="Copiar" waitPopup
				}
			}
		}
		
		Grupo gFinalizarResolucion titulo="Finalizar la Resoluci√≥n" permiso=finalizarResolucion {
			Form formFinalizarResolucion {
				Enlace enlaceFinalizarResolucion titulo="Finalizar la Resoluci√≥n" pagina=PaginaFinalizarResolucion estilo="btn"
			}
		}
		
		Grupo gDatosResolucion titulo="Datos de la Resoluci√≥n actual" permiso=mostrarDatosResolucion {
			Combo campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resoluci√≥n"
			Combo campo=ResolucionFAP.tipo titulo="Tipo"
			Combo campo=ResolucionFAP.modalidad titulo="Modalidad de la Resoluci√≥n" requerido
			Combo campo=ResolucionFAP.estado titulo="Estado"
			Texto campo=ResolucionFAP.tituloInterno titulo="T√≠tulo" requerido
			AreaTexto campo=ResolucionFAP.sintesis titulo="S√≠ntesis" requerido
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripci√≥n"
			AreaTexto campo=ResolucionFAP.observaciones titulo="Observaciones"
			Combo areasResolucion campo=ResolucionFAP.areasResolucion titulo="√?rea resoluci√≥n" requerido
			Combo tiposResolucion campo=ResolucionFAP.tiposResolucion titulo="Tipo resoluci√≥n" requerido
			Check campo=ResolucionFAP.conBaremacion titulo="¬øTiene Baremaci√≥n?"
			
			Grupo tablaDeDatos permiso=mostrarDatosResolucion borde=false {
			Tabla lineasResolucion campo=ResolucionFAP.lineasResolucion titulo="Lineas Resoluci√≥n" {
				Columna campo=LineaResolucionFAP.id titulo="Id" expandir
				Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente" expandir
				Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud" expandir
				Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante" expandir
				Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante" expandir
				Columna campo=LineaResolucionFAP.estado titulo="Estado" expandir
				Columna campo=LineaResolucionFAP.registro.oficial.enlaceDescargaFirmado titulo="Oficio de remisi√≥n" expandir
				Columna campo=LineaResolucionFAP.registro.justificante.enlaceDescarga titulo="Justificante oficio de remisi√≥n" expandir
			}
			}
			Texto campo=ResolucionFAP.folio_inicio titulo="Folio Inicial"
			Texto campo=ResolucionFAP.folio_final titulo="Folio Final"
			Texto campo=ResolucionFAP.codigoResolucion titulo="Numero"
			Fecha campo=ResolucionFAP.fechaRegistroResolucion titulo="Fecha Resoluci√≥n"
		}
	}
	
	Popup ModificarEstadoExpediente campo=ResolucionFAP.lineasResolucion titulo="Modificar estado del expediente en la resoluci√≥n"{
		Grupo grNoEditable borde=false permiso=noEditable {
			Texto campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
			Texto campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
			Texto campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
			Texto campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante"
		}
		Grupo grEditable borde=false {
			Grupo borde=false permiso=noEditable {
				Texto campo=LineaResolucionFAP.puntuacionBaremacion titulo="Puntuaci√≥n Baremaci√≥n"
			}
			
			Combo comboEstadoLinea campo=LineaResolucionFAP.estado titulo="Estado"

			Grupo gConcedido borde=false mostrarSiCombo comboEstadoLinea = "concedida","concedidaTurismo"{
				Texto campo=LineaResolucionFAP.importeTotal titulo="Importe Total"
				Texto campo=LineaResolucionFAP.importeConcedido titulo="Importe Concedido"
			}
		}
	}
	
	Popup VerExpediente campo=ResolucionFAP.lineasResolucion {
		Texto campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
		Texto campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
		Texto campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
		Texto campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante"
		Combo campo=LineaResolucionFAP.estado titulo="Estado"
	}

	Pagina DocConsultaPortafirmaCrear campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion crear redirigir=EditarResolucion
		Grupo titulo="Aportar Documento"{
			SubirArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
		}
		Grupo titulo="Aportar documento por uri"{
			Texto uriDoc titulo="Uri del documento" campo=Documento.uri
		}
		Enlace volver titulo="Volver" anterior estilo="btn"
	}
	
	Pagina DocConsultaPortafirmaEditar campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion editar redirigir=EditarResolucion
		EditarArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
		Enlace volver titulo="Volver" anterior estilo="btn"
	}
	
	Pagina PaginaPublicarResolucion campo=ResolucionFAP perteneceA="Principal" noForm{
		Grupo grupo titulo="Publicaci√≥n de la Resoluci√≥n" {
			Form cambioEstadoFecha {
				Grupo grupoCambioEstadoFecha titulo="Paso 1: Cambio de estado y actualizacion de datos" permiso=permisoCambioEstadoFecha{
					Fecha campo=ResolucionFAP.fechaFinAceptacion titulo="Fecha Fin de Aceptaci√≥n" requerido
					Boton btnCambioEstadoFecha titulo="Actualizar"
				}
			}

			Grupo grupoDocumentoResolucion titulo="Paso 2: Documento de Baremacion en Resolucion" permiso=permisoGenerarBaremacionResolucion{
				Form generarDocumentoBaremacionResolucion permiso=permisoGenerarDocBaremacion{
					Grupo grupoGenerarDocumento titulo="Paso 2.1: Generaci√≥n y Almacenamiento de Documentos"{
						Boton btnGenerarDocumento titulo="Generar" waitPopup
					}
				}
				Form generarClasificarDocumentos permiso=permisoClasificarDocBaremacion {
					Grupo grupoClasificarDocumentos titulo="Paso 2.2: Clasificar los documentos"{
						Boton btnClasificarDocumentos titulo="Clasificar" waitPopup
					}
				}
				
				Form generarInformeConComentarios permiso=permisoGenerarInformeConComentarios{
					Grupo grupoGenerarInformeConComentarios titulo="Generar Informe con Comentarios de la Administracion"{
						Boton btnGenerarInformeConComentarios titulo="Generar"
					}
				}
				Form clasificarInformeConComentarios permiso=permisoClasificarInformeConComentarios{
					Grupo grupoClasificarInformeConComentarios titulo="Clasificar Informe con Comentarios" {
						Wiki {"El informe ya ha sido generado, debe proceder a su clasificacion"}
						Boton btnClasificarInformeConComentarios titulo="Clasificar"
					}
				}
				Form generarInformeSinComentarios permiso=permisoGenerarInformeSinComentarios{
					Grupo grupoGenerarInformeSinComentarios titulo="Generar Informe sin Comentarios de la Administracion"{
						Boton btnGenerarInformeSinComentarios titulo="Generar"	
					}
				}
				Form clasificarInformeSinComentarios permiso=permisoClasificarInformeSinComentarios{
					Grupo grupoClasificarInformeSinComentarios titulo="Clasificar Informe con Comentarios" {
						Wiki {"El informe ya ha sido generado, debe proceder a su clasificacion"}
						Boton btnClasificarInformeSinComentarios titulo="Clasificar"
					}
				}
				
			}
			Form firmarBaremacion permiso=permisoFirmarDocBaremacionResolucion{
				Grupo grupoFirmarBaremacion titulo="Firmar Documentos de Baremaci√≥n en Resoluci√≥n"{
					Boton btnFirmarBaremacion titulo="Firmar"
				}
			}
			Form volver{
				Grupo grupovolver borde=false {
					Boton btnVolver titulo="Volver" pagina=EditarResolucion 
				}
			}
		}
	}

	Pagina AportarDocumentoResolucion titulo="Documento Resoluci√≥n" campo=ResolucionFAP {
		Accion editar redirigir=EditarResolucion crearSiempre
		Grupo grupoAportarDocumentoResolucion titulo= "Aportar documento de resoluci√≥n" {
			SubirArchivo subirArchivoAportarDocumentoResolucion campo=ResolucionFAP.registro.oficial requerido tramite="Resolucion"
		}
	}
	
	Pagina CambiarDocumentoResolucion titulo="Documento Resoluci√≥n" campo=ResolucionFAP noForm {
		Grupo grupoCambiarDocumentoResolucion titulo="Cambiar documento de resoluci√≥n" {
			Enlace enlaceDocumentoResolucionActual campo=ResolucionFAP.registro.oficial.urlDescarga titulo="Ver documento de resoluci√≥n actual" destino="_blank" estilo="btn"
			Form formCambiarDocumentoResolucion redirigir=EditarResolucion {
				Boton botonSubirNuevoDocumentoResolucion titulo="Subir nuevo documento de resolucion"
			}
		}
		Boton botonVolver titulo="Volver" pagina=EditarResolucion 
	}

	// HTML modificado
	Pagina PaginaNotificarResolucion campo=ResolucionFAP perteneceA="Principal" noForm {
		Grupo grupoNotificarResolucion titulo="Notificaci√≥n de la resoluci√≥n" {
			Grupo grupoOficioRemision permiso=permisoOficioRemision borde=false{
				Grupo grupoGenerarOficioRemision titulo="Generar Oficios de emisi√≥n" permiso=permisoGenerarOficioRemision {
					Form formGenerarOficioRemision {
						Boton botonGenerarOficioRemision titulo="Generar"
					}
				}
				Grupo tablaLineasResolucion titulo="Firmar y Registrar de Salida los Oficios de Remisi√≥n" permiso=permisoFirmarOficioRemision {
					Wiki { "Debe firmar los siguientes oficios de remisi√≥n: "  } 
					Tabla tablaOficiosRemisionSinFirmar campo=ResolucionFAP.lineasResolucion titulo="Lineas de resoluci√≥n" paginaEditar=PaginaFirmarOficioRemision popupLeer=popupOficioNotificacion nombreBotonEditar="Firmar"{
						Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente" expandir
						Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud" expandir
						Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante" expandir
						Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante" expandir
						Columna campo=LineaResolucionFAP.estado titulo="Estado" expandir
						Columna campo=LineaResolucionFAP.registro.fasesRegistro.firmadaVisible titulo="Oficio Firmado" expandir
						Columna campo=LineaResolucionFAP.registro.oficial.enlaceDescarga titulo="Oficio de remisi√≥n" expandir
					}
				}
			}
			Grupo grupoCopiaExpediente borde=false permiso=permisoNotificar {
				Grupo borde=false {
					Enlace campo=ResolucionFAP.registro.oficial.urlDescargaFirmado titulo="Obtener Documento Resoluci√≥n" destino="_blank" estilo="btn"
				}
				Tabla tablaOficiosRemisionFirmados campo=ResolucionFAP.lineasResolucion titulo="Lineas de resoluci√≥n" {
					Columna campo=LineaResolucionFAP.id titulo="Id" expandir
					Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente" expandir
					Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud" expandir
					Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante" expandir
					Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante" expandir
					Columna campo=LineaResolucionFAP.estado titulo="Estado" expandir
					Columna campo=LineaResolucionFAP.registro.id titulo="Id oficio de remisi√≥n" expandir
					Columna campo=LineaResolucionFAP.registro.oficial.enlaceDescargaFirmado titulo="Oficio de remision" expandir
				}
				// Aqu√≠ van los inputs de los plazos asociados a la notificaci√≥n y el bot√≥n notificar
			}
			Grupo grupoVolver borde=false {
				Form formVolver {
					Boton botonVolver titulo="Volver" pagina=EditarResolucion
				}
			}
		}
	}
	
	Popup popupOficioNotificacion campo=ResolucionFAP.lineasResolucion titulo="Lineas de Resoluci√≥n"{
		Grupo titulo="Datos de la L√≠nea de Resoluci√≥n y el Oficio de Remisi√≥n" {
			Texto campo=LineaResolucionFAP.id titulo="Id"
			Texto campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
			Texto campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
			Texto campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante" 
			Texto campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante" 
			Combo campo=LineaResolucionFAP.estado titulo="Estado" 
			Texto campo=LineaResolucionFAP.registro.id titulo="Id Oficio de remisi√≥n" 
			Texto campo=LineaResolucionFAP.registro.fasesRegistro.firmadaVisible titulo="Firmado Oficio de remisi√≥n" 
		}
	}

	Pagina PaginaFirmarOficioRemision campo=ResolucionFAP.lineasResolucion noForm {
		Form formFirmarOficioRemision redirigir=PaginaNotificarResolucion permiso=permisoOficioRemision {
			FirmaSimple firmaFirmarOficioRemision titulo="Firmar y Registrar Oficio de Remisi√≥n" documento=LineaResolucionFAP.registro.oficial
				listaFirmantes=LineaResolucionFAP.registro.firmantes.todos calcularFirmantes="lineaResolucionFAP.calcularFirmantes()"
		}
		Form formVolver {
			Grupo grupoVolver borde=false {
				Boton botonVolver titulo="Volver" pagina=PaginaNotificarResolucion 
			}
		}
	}

	Pagina PaginaFinalizarResolucion campo=ResolucionFAP perteneceA="Principal" noForm {
		Grupo grupoFinalizar titulo="Finalizaci√≥n de la Resoluci√≥n" {
			Grupo borde=false {
				Enlace campo=ResolucionFAP.registro.oficial.urlDescargaFirmado titulo="Obtener Documento Resoluci√≥n" destino="_blank" estilo="btn"
			}
			Grupo tablaDeDatos permiso=mostrarDatosResolucion borde=false {
				Tabla lineasResolucion campo=ResolucionFAP.lineasResolucion titulo="Lineas Resoluci√≥n" {
					Columna campo=LineaResolucionFAP.id titulo="Id" expandir
					Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"  expandir
					Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud" expandir
					Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante" expandir
					Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante" expandir
					Columna campo=LineaResolucionFAP.estado titulo="Estado L√≠nea" expandir
					Columna campo=LineaResolucionFAP.registro.oficial.enlaceDescargaFirmado titulo="Oficio de remisi√≥n" expandir
					Columna campo=LineaResolucionFAP.registro.justificante.enlaceDescarga titulo="Justificante oficio de remision" expandir
				}
			}
			Form formFinalizar {
				Boton btnFinalizar titulo="Finalizar" waitPopup
			}
			Form volver {
				Grupo grupovolver borde=false {
					Boton btnVolver titulo="Volver" pagina=EditarResolucion
				}
			}
		}
	}

}

//Sobreescrito
Permiso permisoGenerarInformeConComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.estadoInformeBaremacionConComentarios = null and
		resolucion.estadoDocBaremacionResolucion = "clasificado"
	return
		editable
}

//Sobreescrito
Permiso permisoGenerarInformeSinComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.estadoInformeBaremacionSinComentarios = null
		and resolucion.estadoDocBaremacionResolucion = "clasificado"
	return 
		editable
}

//Sobreescrito
Permiso permisoClasificarInformeConComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
		and resolucion.estadoInformeBaremacionConComentarios != null
		and resolucion.estadoInformeBaremacionConComentarios = "generado"
	return
		editable
}

//Sobreescrito
Permiso permisoClasificarInformeSinComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
		and resolucion.estadoInformeBaremacionSinComentarios != null
		and resolucion.estadoInformeBaremacionSinComentarios = "generado"
	return 
		editable
}

Permiso permisoClasificarDocBaremacion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoDocBaremacionResolucion != null and
		resolucion.estadoDocBaremacionResolucion = "generado" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoDocBaremacionResolucion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso permisoGenerarDocBaremacion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoDocBaremacionResolucion = null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoDocBaremacionResolucion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso permisoCambioEstadoFecha {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion = null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoPublicacion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso permisoCopiaExpedientes {
	vars
		ResolucionFAP resolucion
	when
		(
			resolucion.estadoPublicacion != null and
			resolucion.estadoPublicacion = "publicada" and
			agente.rolActivo in ("administrador", "gestor", "jefeServicio")
		)
		or(
			agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
			resolucion.estadoNotificacion != null and
			resolucion.estadoNotificacion = "notificada"
		)
		or (
			resolucion.estado != null and
			resolucion.estado = "publicadaYNotificada" and
			agente.rolActivo  in ("administrador", "gestor", "jefeServicio")
		)
	return
		editar
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}


//Sobreescrito para que consulte los m√©todos de si se generan los documentos
Permiso permisoGenerarBaremacionResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion != null and
		resolucion.estadoPublicacion = "publicada" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.conBaremacion = true
	return
		editar
	when
		resolucion.estadoPublicacion != null and
		resolucion.conBaremacion = true
	return 
		visible
}

//Sobreescrito
Permiso permisoFirmarDocBaremacionResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion != null and
		resolucion.estadoPublicacion = "documentoGenerado" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.estadoInformeBaremacionConComentarios = "clasificado" and
		resolucion.estadoInformeBaremacionSinComentarios = "clasificado" and
		resolucion.conBaremacion = true
	return
		editable
}

Permiso sinIniciarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion = null
		or resolucion.tipo = null
	return
		[editar crear]
	when
		resolucion.tipo != null
	return
		visible
}

Permiso mostrarDatosResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso seleccionarExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "borrador" and
		resolucion.tipoMultiple = "parcial" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

Permiso portafirmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo = "jefeServicio"
		and resolucion.firmarJefeServicio = "true"
		and resolucion.estado = "preparada"
	return
		all
}

Permiso seleccionarUnExpediente {
	vars
		ResolucionFAP resolucion
	when
		resolucion.modalidad="resolucionSimple" and
		resolucion.estado = "borrador" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

Permiso noSeleccionarExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "borrador" and
		resolucion.tipoMultiple = "total" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

Permiso solicitudFirmaResolucion {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador", "jefeServicio")
		and resolucion.estado in ("preparada", "pendienteFirmaJefeServicio", "firmadaJefeServicio", "pendienteFirmaDirector")
	return
		all
}

Permiso prepararResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "creada" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

/**
 * Visible el env√≠o a portafirma para el Jefe de Servicio
 */
Permiso firmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("preparada")
		and resolucion.firmarJefeServicio = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("preparada")
		and resolucion.firmarJefeServicio = "true"
	return
		visible
}

Permiso esperaFirmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("pendienteFirmaJefeServicio")
		and resolucion.firmarJefeServicio = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("pendienteFirmaJefeServicio")
		and resolucion.firmarJefeServicio = "true"
	return
		visible
}


/**
 * Visible el env√≠o a portafirma para el Director
 */
Permiso firmaDirector {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("firmadaJefeServicio")
		and resolucion.firmarDirector = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("firmadaJefeServicio")
		and resolucion.firmarDirector = "true"
	return
		visible
}

Permiso esperaFirmaDirector {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("pendienteFirmaDirector")
		and resolucion.firmarDirector = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("pendienteFirmaDirector")
		and resolucion.firmarDirector = "true"
	return
		visible
}

Permiso registrarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("firmada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

Permiso publicarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("registrada", "notificada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

// sobreescrito
Permiso permisoOficioRemision {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador")
	return
		all
}

// sobreescrito
Permiso permisoGenerarOficioRemision {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador")
	return
		all
}

// sobreescrito
Permiso permisoFirmarOficioRemision {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador")
	return
		all
}

// sobreescrito
Permiso permisoNotificar {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador")
	return
		all
}

Permiso notificarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("registrada", "publicada")
		and resolucion.estadoNotificacion = null
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

// Sobreescrito
Permiso finalizarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("publicada", "notificada", "publicadaYNotificada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

Permiso verDocumentoResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("registrada", "publicada", "notificada", "publicadaYNotificada", "finalizada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}