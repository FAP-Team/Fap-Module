Formulario Solicitud campo=SolicitudGenerica {

	Pagina Desistimiento campo=SolicitudGenerica.desistimiento titulo="Desistimiento"{
	Grupo titulo="Solicitud de Desistimiento"  {  
		
			Grupo permiso=editarDesistimiento borde = false { 
				Grupo permiso=prepararDesistimiento borde = false {
					Form frmMotivo autoEnviar  { 
			    		AreaTexto titulo="Motivo del desistimiento" campo=Desistimiento.motivo filas=10
			    		Boton btoGuardar titulo="Guardar trámite" 
					}

            		Grupo gpreparar titulo="Paso 1. Preparar la solicitud de desistimiento" { 
                		Wiki { "Si su solicitud está lista para firmar, pulse el siguiente botón." }                  
                  		Form prepararFirmar permiso=generarBorradorDesistimiento {
                        	Boton botonPrepararFirmar titulo="Preparar para firmar"
                 		}
            		}
            	}

				Grupo titulo="Paso 2. Obtener borrador de la solicitud de desistimiento" permiso=enBorradorDesistimiento {
                	Wiki {"Puede obtener un borrador de la solicitud de desistimiento, no válido para su presentación y comprobar que la información introducida es correcta."}
	                Enlace campo=Desistimiento.registro.borrador.urlDescarga titulo="Obtener borrador" destino="_blank" estilo="boton"
    	            Form deshacer redirigir=Desistimiento permiso=modificarDesistimiento {
        	              Wiki {"Puede realizar cambios antes de su presentación definitiva."}
            	          Boton botonModificar titulo="Modificar desistimiento"
                	}
                  
                  	Grupo mensajeIntermedio permiso=mensajeIntermedioDesistimiento borde=false {
                        Wiki estilo ="box-warn box" {
                             "Se produjo un error en la presentación de la desistimiento. Pulse otra vez el botón 'Firmar y presentar' para intentar finalizar el proceso."
                        }
                  	}
            	}
            
			} 
            
            Grupo grpPresentar titulo="Paso 3. Registrar Desistimiento" permiso=enBorradorDesistimiento {  		
				Form frmPresentar redirigir=DesistimientoPresentar {
		 			Wiki {"Debe registrar el desistimiento para que se considere presentado."}
					Boton botonPresentar titulo="Presentar Desistimiento"
				}
			}
		
			Grupo grpDescargarRecibo titulo="Paso 4. Descargar el recibo" permiso=clasificadaDesistimiento {
				Wiki {"Puede obtener un recibo de la presentación del desistimiento pulsando el enlace 'Obtener recibo'." }
				Enlace campo=Desistimiento.registro.justificante.urlDescarga titulo="Obtener recibo"  destino="_blank" estilo="boton" 	
			}	
		}
	}
		
	Pagina  DesistimientoPresentar   titulo = "Presentar Desistimiento" noForm campo=SolicitudGenerica.desistimiento{
		Grupo mensajeIntermedio permiso=mensajeIntermedioDesistimiento borde=false {
			Wiki estilo ="box-warn box" {
				"Se produjo un error en la presentación del desistimiento. Pulse otra vez el botón 'Firmar y presentar' para intentar finalizar el proceso."
			}
		}

		//Firma persona física	
		Grupo titulo="Paso 1. Firmar y Registrar el Desistimiento" mostrarSiCampo SolicitudGenerica.solicitante.tipo = "fisica"  //permiso=presentacionFirmar 
		{			
			Form formFirmaPF permiso=firmarRegistrarDesistimiento { //redirigir=AceptarRenunciar
		    	Wiki {"El desistimiento será firmado y registrado." }
				FirmaSimple firmarRegistrarNif titulo="Firmar y registrar" documento=Desistimiento.registro.oficial listaFirmantes=SolicitudGenerica.registro.firmantes.todos calcularFirmantes=SolicitudGenerica.solicitante
			}
			Wiki {"Nota: La operación de firmar y registrar puede tardar varios minutos."}			
		}			
						
		//Firma Persona jurídica
		Grupo titulo="Paso 1. Firmar y Registrar el Desistimiento" mostrarSiCampo SolicitudGenerica.solicitante.tipo = "juridica"{
			Wiki {"Debe indicar si se firmará el desistimiento con el certificado electrónico de la empresa o con el certificado electrónico de los representates, si opta por esta última opción y la representación es mancomunada la aceptación/renuncia debe firmarse por todos los representantes mancomunados"}			
			Form tipoCertificadoFirma autoEnviar {
				Combo certificadoFirma titulo="Certificado que se usará en la firma" campo=SolicitudGenerica.desistimiento.registro.tipoFirma
			}												
			Grupo borde = false mostrarSiCampo Desistimiento.registro.tipoFirma = "representantes" {

				Grupo titulo="Paso 1.1. Firmar el Desistimiento"  {						
					Wiki {"El desistimiento será firmado. Cuando todos los representantes hayan firmado podrá registrarla y obtener el recibo de la presentación del desistimiento."}
					Form formFirmaRepresentante permiso=firmarDesistimiento {
						FirmaSimple firmarRepresentante titulo="Firmar" documento=Desistimiento.registro.oficial listaFirmantes=SolicitudGenerica.registro.firmantes.todos calcularFirmantes=SolicitudGenerica.solicitante
					}
					Tabla tablaFirmantesHecho campo=Desistimiento.registro.firmantes.todos recargarPagina  titulo="Representantes que ya han firmado" {
						Columna campo=Firmante.idvalor titulo="NIP"
						Columna campo=Firmante.nombre titulo="Nombre" expandir
						Columna campo=Firmante.fechaFirma titulo="Fecha de firma"
					}
				
					Tabla tablaFirmantesEspera campo=Desistimiento.registro.firmantes.todos recargarPagina  titulo="Representantes que no han firmado" {
						Columna campo=Firmante.idvalor titulo="NIP"
						Columna campo=Firmante.nombre titulo="Nombre" expandir
					}
											
				}
				
				Grupo titulo="Paso 1.2. Registrar el Desistimiento" permiso=registrarDesistimiento
				{
					Wiki {"Su desistimiento ha sido firmado correctamente. Ahora puede presentarlo. Una vez presentado podrá obtener el recibo."}
					Form frmRegistrar redirigir=Desistimiento {
						Boton botonRegistrar titulo="Registrar"
					}								
					Wiki {"Nota: La operación de registro puede tardar varios minutos."}
				}	
													
			}								
 
			Grupo borde=false mostrarSiCampo Desistimiento.registro.tipoFirma = "cif"
			{
				Wiki {"El desistimiento será firmado y registrado. "}			
				Form formFirmaCif redirigir=Desistimiento permiso=firmarRegistrarDesistimiento {
					FirmaSimple firmarRegistrarCif titulo="Firmar y Registrar" documento=Desistimiento.registro.oficial listaFirmantes=SolicitudGenerica.registro.firmantes.todos calcularFirmantes=SolicitudGenerica.solicitante
				}
			}			
		}		
	}
	
	Popup PopUpDocDesistimiento titulo="Documentación" campo=SolicitudGenerica.desistimiento.documentos {
		EditarArchivo documentoPresentado campo=Documento requerido
	}	
	
}