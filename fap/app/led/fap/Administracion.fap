Formulario Administracion permiso=administrador{
	Menu {
		Enlace "Mis Solicitudes" pagina=Principal.Solicitudes
		
		Grupo "Administración" {
			Enlace "Usuarios" pagina=Usuarios
			Enlace "Aed" pagina=Aed
			Enlace "Requerimientos" pagina=Requerimientos
			Enlace "Tabla de tablas" pagina=TablaDeTablas
			Enlace "Emails" pagina=Emails
			Enlace "Logs" accion="fap.LoggerController.index"
			Enlace "Informe" accion="fap.InformeController.index"
			//			Enlace "Plantilla PDF" pagina="PlantillaPdf"			
		}
		
		Enlace "Ayuda" pagina=Soporte.Soporte
		Enlace "Salir" accion="fap.SecureController.logout"
    }
	
	Pagina Usuarios {

		Wiki {"Para asignar roles seleccione el usuario y pulse en editar"}

		Tabla listaUsuarios titulo="Usuarios" campo=Agente 
			popup=PopupUsuario 
		{
			Columna campo=Agente.id titulo="Id" ancho="25"
			Columna campo=Agente.username titulo="Usuario" expandir
			Columna campo=Agente.name titulo="Nombre completo" expandir
			Columna campo=Agente.roles titulo="Roles" expandir
			Columna campo=Agente.email titulo="Email" ancho="150"
			Columna campo=Agente.funcionario titulo="Funcionario" ancho="80"
		}
	}
	
	Popup PopupUsuario campo=Agente modificar crear borrar titulo="Usuario" {
		Texto campo=Agente.username titulo="Username" requerido
		Combo campo=Agente.roles titulo="Roles" multiple busqueda requerido 
		Texto campo=Agente.email titulo="Email" requerido
		Check campo=Agente.funcionario titulo="Funcionario habilitado"
	}
		
	Pagina Aed noForm permiso=administrador  {
		Grupo titulo="Tipos de documentos" {
			Wiki {"Busca en el archivo electrónico los tipos de documento y los almacena en la base de datos. Los tipos almacenados en la base de datos serán los que aparecen en el combo de la página de documentación."}
			Form tiposDeDocumentos {
				Boton titulo="Actualizar tipos de documentos desde el AED"
			}
		}
	}
	
	Pagina Requerimientos {
		Grupo configQuartz titulo="Configuración del quartz" {
			Wiki {"El quartz es el proceso que se encarga de comprobar los accesos a la sede cuando se emiten los requerimientos y asignar correctamente las fechas de acceso y de vencimiento de los requerimientos"}
			
			Check campo=Quartz.execute titulo="Permitir ejecutar el quartz"
			Check campo=Quartz.mostrarTodasSolicitudes titulo="Mostrar los datos de todas las solicitudes (para pruebas)"
			Check campo=Quartz.mostrarSolicitudesRequeridas titulo="Mostrar información de las solicitudes en estado REQUERIDA"
			Check campo=Quartz.ejecutarCambioDeFecha titulo="Permitir comprobar si el usuario ha entrado en la sede y ejecutar el cambio de fecha de vencimiento"
			Check campo=Quartz.cambiarEstadoPlazoVencido titulo="Permitir el cambio de estado a PLAZO_VENCIDO de las solicitudes fuera de plazo"
			Check campo=Quartz.sendMail titulo="Habilitar el envio de mail a falta de 5 y 2 días para la fecha de vencimiento"
			Boton ejecutarQuartz titulo="Ejecutar Quartz"
		}
	}


	Pagina TablaDeTablas noForm titulo="Tabla de tablas" permiso=administrador

	{

	       Wiki {"La tabla de tablas almacena la información de la listas. "}



	    Tabla tabladetablas titulo="Tabla de tablas" campo=TableKeyValue popup=PopupTablaDeTablas {
	        Columna campo=TableKeyValue.table titulo="Tabla"
	        Columna campo=TableKeyValue.key titulo="Clave"
	        Columna campo=TableKeyValue.value titulo="Valor" expandir
	    }

	    Grupo titulo="Actualizar desde fichero" {
	        Form actualizarDesdeFichero {
                Wiki {"
'''Perderá todas las modificaciones''' que ha hecho y recuperará los valores puestos en la generación.

'''IMPORTANTE''' : Los tipos de documentos no se recuperan de valores puesto de la generación, se recuperan a partir del AED.
Después de pulsar este botón vaya a Administración/Aed/Actualizar tipos de documentos desde el AED.
                "}

                Boton titulo="Borrar todas y leer desde ficheros"
	        }
	    }
	}

	Popup PopupTablaDeTablas campo=TableKeyValue {
	    Texto campo=TableKeyValue.table titulo="Tabla"
	    Texto campo=TableKeyValue.key titulo="Clave"
	    Texto campo=TableKeyValue.value titulo="Valor"
	}

//	Pagina PlantillaPdf noForm {
//	    Tabla  titulo="Plantillas de PDF" campo="pdf" popup=popupPlantillaPdf{
//	        Columna campo="pdf.idPdf" titulo="Identificador"
//	        Columna campo="pdf.title" titulo="Titulo" expandir
//	    }
//	    Form descargarpdf {
//	    	Boton titulo="Descargar PDF"
//	    }
//	}
	
//	Popup popupPlantillaPdf titulo="Plantilla PDF"{
//	    Texto campo="pdf.idPdf" titulo="Identificador"
//	    Texto campo="pdf.title" titulo="Titulo"
//	    AreaTexto campo="pdf.content" titulo="Contenido"
//	    Texto campo="pdf.header" titulo="Cabecera"
//	    Texto campo="pdf.footer" titulo="Pie de Página"
//	}

    Pagina Emails noForm{
        Tabla emails titulo="Plantillas de emails" campo=Mail popup=PopupEmail {
            Columna campo=Mail.idMail titulo="Identificador"
            Columna campo=Mail.content titulo="Contenido" expandir
        }
        
         Grupo titulo="Actualizar desde fichero" {
	        Form actualizarDesdeFichero {
                Wiki {"'''Perderá todas las modificaciones''' que ha hecho y recuperará los valores puestos en los ficheros .yaml situados en: 'app/emails/initial-data/'."}
                Boton titulo="Borrar todos y leer desde ficheros"
	        }
	    }
    }

    Popup PopupEmail campo=Mail titulo="Email" {
        Wiki {"
Dentro de los campos puede utilizar variables con ${nombreVariable} y otros elementos que puede consultar en la [http://www.playframework.org/documentation/1.2.1/templates#syntax documentación]

En los campos '''Para''' y '''CC''' puede poner varios destinatarios separados por \",\".
        "}

        Texto campo=Mail.idMail titulo="Identificador" requerido
        Texto campo=Mail.sender titulo="De"
        Texto campo=Mail.sendTo titulo="Para" requerido
        Texto campo=Mail.bcc titulo="CC"
        Texto campo=Mail.subject titulo="Asunto" requerido
        AreaTexto campo=Mail.content titulo="Mensaje" requerido
		AreaTexto campo=Mail.footer titulo="Pie de mensaje"

    }
}