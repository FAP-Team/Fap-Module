Formulario Solicitud {

	Pagina Aportacion noForm permiso=aportacion {
		
		Enlace titulo="Consultar documentos ya aportados" pagina=AportacionAportados estilo="boton"
		Enlace titulo="Consultar recibos de aportaciones" pagina=AportacionRecibos estilo="boton"
		Grupo titulo="Paso 1. Agregar Nuevos Documentos"{
			Wiki{"A partir de las plantillas que se adjuntan en la convocatoria, y una vez rellenadas correctamente, deberá añadirlas como documentación indicando el tipo de documento correspondiente."}
			Tabla documentos 
			    titulo="Documentos nuevos pendientes de aportar" 
		    	campo=SolicitudGenerica.aportaciones.actual.documentos
				popupBorrar=PopUpDocProcesos
				popupModificar=PopUpDocProcesos  
				popupVer=PopUpDocProcesos
			{
				Columna campo=Documento.fechaSubida titulo="Fecha Subida" ancho="80"
				Columna campo=Documento.fechaRegistro titulo="Fecha Registro" ancho="80"
				Columna campo=Documento.descripcion titulo="Tipo" expandir
				Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
			}
		
			Form addDocumento campo=SolicitudGenerica.aportaciones.actual.documentos {		
				Grupo titulo="Añadir documentación" permiso=usuario {
					SubirArchivoAed fileAportacion campo=Documento requerido aportadoPor="CIUDADANO"
					Boton subirDocumento titulo="Añadir documentación"
				}
			}
			
			Form addDocumentoGestor campo=SolicitudGenerica.aportaciones.actual.documentos {		
				Grupo titulo="Añadir documentación por Gestor" permiso=adminOrGestor {
					SubirArchivoAed fileAportacionGestor campo=Documento requerido
					Boton subirDocumentoGestor titulo="Añadir documentación"
				}
			}
		}
		
		Grupo titulo="Paso 1b. Registrar los documentos"{
			Form presentar redirigir=AportacionPresentar {
				Wiki {"Debe registrar los documentos para que se consideren aportados a la solicitud."}
				Boton botonPresentar titulo="Preparar para registrar"
			}
		}
		
	}
	
	Pagina AportacionAportados {		
		Tabla documentos titulo="Documentos Presentados" 
		    campo=SolicitudGenerica.documentacion.documentos
			popupVer=PopUpDocPresentados
		{
			Columna campo=Documento.fechaSubida titulo="Fecha" ancho="80" 
			Columna campo=Documento.descripcion titulo="Tipo" expandir
			Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
		}
		
		Enlace titulo="Volver" pagina=Aportacion estilo="boton"
	}
	
	
	Pagina AportacionRecibos {
			
		Tabla recibosAportados titulo="Recibos de las aportaciones" campo=SolicitudGenerica.documentacionAportada.documentos permiso=aportacionNoNull
			popupVer=PopUpDocAportados
		{
			Columna campo=Documento.fechaSubida titulo="Fecha" expandir
			Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
		}
		
		Enlace titulo="Volver" pagina=Aportacion estilo="boton"
	}
	
	Popup PopUpDocPresentados ver titulo="Documentación" campo=SolicitudGenerica.documentacion.documentos {
		EditarArchivoAed documentoPresentado campo=Documento requerido
	}
	
	Popup PopUpDocProcesos modificar borrar ver titulo="Documentación" campo=SolicitudGenerica.aportaciones.actual.documentos {
		EditarArchivoAed documentoProceso campo=Documento requerido
	}
	
	Popup PopUpDocAportados ver titulo="Documentación" campo=SolicitudGenerica.documentacionProceso.documentos {
		EditarArchivoAed documentoAportado campo=Documento requerido
	}
		
	Pagina AportacionPresentar noForm {
	  	Grupo titulo="Presentar Aportación de Documentos" {
			Wiki {"Puede obtener un borrador de la solicitud de aportación de documentación, no válido para su presentación y comprobar que la información introducida es correcta."}

			Enlace campo=SolicitudGenerica.aportaciones.actual.borrador.urlDescarga titulo="Obtener borrador" destino="_blank" estilo="boton"
		
			Form modificarBorrador redirigir=Aportacion permiso=aportacionModificar{
				Wiki {"Puede realizar cambios antes de su presentación definitiva."}
				Boton botonModificarBorrador titulo="Modificar datos"
			}
			
			Grupo mensajeIntermedio permiso=aportacionMensajeIntermedio visible=false {
				Wiki estilo ="box-warn box" {
					"Se produjo un error en la presentación de la aportación. Pulse otra vez el botón 'Firmar y presentar' para intentar finalizar el proceso."
				}
			}
			
			
			Wiki {"La solicitud de aportación será firmada y registrada. Puede obtener un recibo de la presentación de la solicitud pulsando el enlace 'Obtener recibo'." }
			
			Grupo titulo="Paso 2. Presentar los Documentos"{
				Wiki{"**NOTA: Debe finalizar el Paso 2 pulsando en el botón 'Firmar y presentar' para que los Nuevos Documentos se consideren presentados. Cuando el proceso finalice los documentos nuevos desaparecerán del Paso 1 y se incorporarán a la lista de Documentos Presentados.**"}
				Wiki{"La solicitud será firmada y registrada. Puede obtener un recibo de la presentación de la solicitud una vez 'Firmar y Presentar' se haya realizado con éxito"}
				Form presentar redirigir=AportacionRecibos {
					FirmaSimple firmarRepresentante titulo="Firmar y presentar" documento=SolicitudGenerica.aportaciones.actual.oficial
				}
			}
			
			Grupo titulo="Incorporar Documentos sin Registro de Entrada" permiso=adminGestorRevisor {
				Form presentarSinRegistrar redirigir=AportacionRecibos {
					Fecha fechaAportacionSinRegistrar titulo="Fecha Incorporación" campo=SolicitudGenerica.aportaciones.actual.fechaAportacionSinRegistro requerido
					Boton aportarSinRegistrar titulo="Incorporar"
				}
			}
			

			Wiki {"Nota: La operación de firmar y presentar puede tardar varios minutos."}
		}
	}
	

	/***************************************************************************************************/
	
	Pagina AccesoVerificaciones titulo="Verificaciones" noForm permiso=accederSiSolicituNoBorrador{
		Grupo fantasma permiso=noVisible{
			Tabla verificacionesFinalizadas titulo="Verificaciones Finalizadas" campo=SolicitudGenerica.verificaciones
			popupVer=PopUpVerificacionVer
			{
				Columna titulo="Expediente" campo=Verificacion.expediente ancho="150"
				Columna titulo="Motivo Exclusión" campo=Verificacion.requerimiento.motivo ancho="150" 
				Columna titulo="Fecha creación" campo=Verificacion.fechaCreacion ancho="150"
				Columna titulo="Fecha finalización" campo=Verificacion.fechaUltimaActualizacion ancho="150"
				Columna titulo="Estado" campo=Verificacion.estado expandir
			}
		
		Grupo gVerificacionActual titulo="Acceder a la verificacion actual" permiso=hayVerificacionEnCurso {
			Form verificacionActual {
				Wiki {"Pulse el botón para acceder a la verificación que está actualmente en curso."}
				Boton botonIraVerificacion titulo="Ir a verificación" pagina=Verificacion
			}
		}
		}
		Grupo gVerificacionNueva titulo="Crear una nueva verificación" permiso=noHayVerificacionEnCurso{
			Form verificacionNueva {
				Wiki {"Pulse el botón para crear una nueva verificación."}
				Boton botonIniciarVerificacion titulo="Nueva verificación"
			}
		}
	}
	
	Popup PopUpVerificacionVer ver titulo="Verificación" campo=SolicitudGenerica.verificaciones {
		Texto expediente titulo="Expediente" campo=Verificacion.expediente
		Texto motivoReq titulo="Motivo Requerimiento" campo=Verificacion.requerimiento.motivo
		Fecha fechaCreacion titulo="Fecha creación" campo=Verificacion.fechaCreacion
		Fecha fechaActualizacion titulo="Fecha finalización" campo=Verificacion.fechaUltimaActualizacion
		Combo estado titulo="Estado" campo=Verificacion.estado
		Tabla documentosVerificacion titulo="Documentación" campo=Verificacion.documentos
			popupVer=PopUpDocumentacionVerificacionVer  
			{
				Columna titulo="Tipo" campo=VerificacionDocumento.uriTipoDocumento ancho="150"
				Columna titulo="Descripción" campo=VerificacionDocumento.descripcion ancho="150"
				Columna titulo="Fecha de presentación" campo=VerificacionDocumento.fechaPresentacion ancho="150" 
				Columna titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion expandir
			}
	}
	
	Popup PopUpDocumentacionVerificacionVer ver titulo="Documentación" campo=Verificacion.documentos {
		Texto titulo="Tipo" campo=VerificacionDocumento.uriTipoDocumento
		Texto titulo="Descripción" campo=VerificacionDocumento.descripcion
		Fecha titulo="Fecha de presentación" campo=VerificacionDocumento.fechaPresentacion 
		Combo titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion
		Tabla requerimientosVerificacion titulo="Requerimientos" campo=VerificacionDocumento.codigosRequerimiento
			popupVer=PopUpRequerimientosVerificacionVer  
			{
				Columna titulo="Código" campo=CodigoRequerimiento.codigo ancho="150"
				Columna titulo="Descripción" campo=CodigoRequerimiento.descripcionCorta expandir
			}
	}
	
	Popup PopUpRequerimientosVerificacionVer ver titulo="Documentación" campo=VerificacionDocumento.codigosRequerimiento {
		Texto codigo titulo="Código" campo=CodigoRequerimiento.codigo
		Texto descripcionCorta titulo="Descripción Corta" campo=CodigoRequerimiento.descripcionCorta
		AreaTexto descripcion titulo="Descripción" campo=CodigoRequerimiento.descripcion
	}
	
	/***************************************************************************************************/
	

	Pagina Verificacion titulo="Verificación de la documentación" noForm  permiso=accederSiSolicituNoBorrador{
		
		Grupo datosEstadoSolicitud titulo="Estado de la Solicitud" permiso=noEditable{
			Combo estado titulo="Estado" campo=SolicitudGenerica.estadoValue
		}
		
		Grupo giniciarVerificacion titulo="Iniciar la verificación" permiso=iniciarVerificacion {
			Form iniciarVerificacion {
				Combo getTramitesCombo titulo="Seleccione el trámite" campo=SolicitudGenerica.verificacion.uriTramite requerido
				Wiki {"Si la solicitud está lista para iniciar la verificación, pulse el siguiente botón."}
				Boton botonIniciarVerificacion titulo="Iniciar la verificación"
			}
		}
		
		Form nuevosDocumentos permiso=hayNuevaDocumentacionVerificacion{
			Wiki{"Existen nuevos documentos aportados por el solicitante y que no están incluídos en la verificación actual.
				  Por favor, pulse el siguiente botón para añadirlos a la verificación que está actualmente en curso."}
			Boton adjuntarNuevosDocumentos titulo="Añadir nuevos documentos"
		}
		
		Grupo tiposProceso titulo="Verificación de Tipos de Documentos" permiso=verificarTiposDocumentos {
			Tabla verificacionTipos titulo="Documentación Aún No Verificada" campo=SolicitudGenerica.documentacion.documentos
				//permiso=documentosNoVerificados
				popupModificar=PopUpDocTipos popupVer=PopUpDocTipos {
				Columna campo=Documento.fechaSubida titulo="Fecha de Subida" ancho="100"
				Columna campo=Documento.fechaRegistro titulo="Fecha de Registro" ancho="100"
				Columna campo=Documento.descripcion titulo="Tipo" expandir
				Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
			}
			Form verificaTipos {
				Boton finalizaVerificarTipos titulo="Finaliza verificación de tipos"
			}
		}
		
		Grupo verificacionProceso titulo="Verificación de la documentación" permiso=verificarDocumentos { 
		
			Grupo botonDescargaReciboSolitud visible=false{
				Enlace campo=SolicitudGenerica.registro.justificante.urlDescarga titulo="Descargar Recibo Solicitud" destino="_blank" estilo="boton"
			}	
		
			Tabla verificacionDocumentos titulo="Documentación" campo=SolicitudGenerica.verificacion.documentos
				popupVer=PopUpDocumentoVerificacionVer 
				popupModificar=PopUpDocumentoVerificacionEditar 
			{
				//Columna titulo="Id" campo=VerificacionDocumento.id ancho="40"
				Columna titulo="Fecha Presentación" campo=VerificacionDocumento.fechaPresentacion ancho="80"
				Columna titulo="Descripción" campo=VerificacionDocumento.descripcion expandir
				Columna titulo="Tipo documento" campo=VerificacionDocumento.nombreTipoDocumento
				Columna titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion ancho="100"
				Columna titulo="Multiplicidad" campo=VerificacionDocumento.identificadorMultiple ancho="80"
				Columna campo=VerificacionDocumento.linkUrlDescarga titulo="Descargar" ancho="100"
				
			}
			
			Form guardarMotivoRequerimientoGeneral {
				AreaTexto motivoGeneralRequerimiento titulo="Motivo General de Requerimiento" campo=SolicitudGenerica.verificacion.requerimiento.motivo
				Boton guardarRequerimientoMotivoRequerimientoGeneralbtn titulo="Guardar Motivo Requerimiento"
			}
			
		
			Form gnuevoRequerimientoBorradorPreliminar destino="_blank"{ 
				Boton obtenerBorradorPreliminar titulo="Obtener Borrador Requerimiento"
			} 
			

//			// Borrar
//			Form todosNoProcede {
//				Boton docsNoProcede titulo="Todos No Procede"
//			}
			
//			Form finalizarVerificacion {
//				Boton verificacionFinalizar titulo="Finalizar la verificación"
//				Wiki {"
//Nota: Finalizar la verificación es irreversible y cambiará el estado de la solicitud.
//					"}
//			}
		}
		
		
		Grupo crearNuevoRequerimiento titulo="Requerimiento" permiso=verificacionCrearRequerimiento {
			Form gCrearRequerimiento {
				Boton crearRequerimientoBoton titulo="Crear Requerimiento"
			}
		}
		
		Grupo nuevosDocumentosTipos titulo="Verificación de Tipos de Documentos" permiso=verificacionConNuevosDoc {
			Tabla verificacionTiposNuevosDoc titulo="Documentación Aún No Verificada" campo=SolicitudGenerica.verificacion.nuevosDocumentos
				popupModificar=PopUpDocNuevosTipos popupVer=PopUpDocNuevosTipos {
				Columna campo=Documento.fechaSubida titulo="Fecha de Subida" ancho="100"
				Columna campo=Documento.fechaRegistro titulo="Fecha de Registro" ancho="100"
				Columna campo=Documento.descripcion titulo="Tipo" expandir
				Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Descargar" ancho="100"
			}
			Form verificaTiposNuevosDoc {
				Boton finalizaVerificarTipos titulo="Finaliza verificación de tipos"
			}
		}
		

		Grupo grequerimientos titulo="Requerimiento" permiso=noVisible /*permiso=verificacionRequerimientos*/ {				
				Form grequerimientoGuardar { 
					Grupo titulo="Añadir Motivo General del Requerimiento" {
						AreaTexto requerimientoCrearMotivo titulo="Motivo del requerimiento" campo=SolicitudGenerica.verificacion.requerimiento.motivo
						Boton requerimientoGuardar titulo="Guardar motivo del requerimiento"
					}
				}
				
				// Esto debe aparecer al darle al boton finalizar REQUERIMIENTO
                Form requerimientoBorrador permiso=borradorRequerimiento {
                	Grupo titulo="Obtener borrador del Requerimiento"  { 
                		Enlace campo=SolicitudGenerica.verificacion.requerimiento.borrador.urlDescarga titulo="Obtener Borrador" destino="_blank" estilo="boton"
                	}
                }
				
				Form frequerimientoSolicitaFirma permiso=solicitarFirmaRequerimiento {
					Grupo titulo="Solicitar firma del Requerimiento"  { 
						Combo requerimientoCrearFirmante titulo="Persona que firma el requerimiento" campo=SolicitudGenerica.verificacion.requerimiento.firmante requerido
						Boton requerimientoCrearSolicitarFirma titulo="Solicitar firma requerimiento"
					}
				}

				Form grequerimientoFirmarRequerimiento permiso=firmarRequerimiento {
					Grupo titulo="Firma del Requerimiento"  { 
						FirmaSimple firmarRequerimiento titulo="Firmar, registrar y notificar" documento=SolicitudGenerica.verificacion.requerimiento.oficial
					}
				}
			}
			
	}
	
	Popup PopUpDocTipos modificar ver titulo="Documentación" campo=SolicitudGenerica.documentacion.documentos {
		EditarArchivoAed documentoProceso campo=Documento requerido
	}
	
	Popup PopUpDocNuevosTipos modificar ver titulo="Documentación" campo=SolicitudGenerica.verificacion.nuevosDocumentos {
		EditarArchivoAed documentoProceso campo=Documento requerido
	}
	
	Popup PopUpDocumentoVerificacionEditar modificar titulo="Editar Documento" campo=SolicitudGenerica.verificacion.documentos {
		Grupo botonDescarga visible=false mostrarSiExpresion "verificacionDocumento.uriDocumento != null"{
			Enlace campo=VerificacionDocumento.urlDescarga titulo="Descargar" destino="_blank" estilo="boton"
		}
		Grupo noEditable permiso=noEditable visible=false{
			Texto titulo="Descripción" campo=VerificacionDocumento.descripcion ancho="420px"
			Texto tipo titulo="Tipo documento" campo=VerificacionDocumento.nombreTipoDocumento ancho="420px"
			Texto tipoUri titulo="Uri tipo documento" campo=VerificacionDocumento.uriTipoDocumento ancho="420px"
			Fecha fechaPresentacion titulo="Fecha de presentación" campo=VerificacionDocumento.fechaPresentacion
			//Texto version titulo="Versión" campo=VerificacionDocumento.version	
		}
		Combo estado titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion
		Grupo requerimientosNoValido titulo="Requerimiento/s del documento" mostrarSiCombo estado="noValido","noPresentado"{
			Grupo requerimientoTexto visible=false mostrarSiCombo estado="noValido","noPresentado"{
				AreaTexto requerimientoTexto titulo="Motivo requerimiento" campo=VerificacionDocumento.motivoRequerimiento
			}
			Grupo requerimientoCodigos visible=false mostrarSiCombo estado="noValido" {
				Tabla requerimientosDocumento titulo="Requerimientos" campo=VerificacionDocumento.codigosRequerimiento
					popupCrear=PopUpDocumentoRequerimientosVerificacionModificar
					popupModificar=PopUpDocumentoRequerimientosVerificacionModificar
					popupVer=PopUpDocumentoRequerimientosVerificacion
					popupBorrar=PopUpDocumentoRequerimientosVerificacion
				{
					Columna titulo="Código" campo=CodigoRequerimiento.codigo ancho="150"
					Columna titulo="Descripción" campo=CodigoRequerimiento.descripcionCorta expandir
				}
			}
		}
	}
	
	Popup PopUpDocumentoVerificacionVer ver titulo="Ver Documento" campo=SolicitudGenerica.verificacion.documentos {
		Grupo botonDescarga visible=false mostrarSiExpresion "verificacionDocumento.uriDocumento != null"{
			Enlace campo=VerificacionDocumento.urlDescarga titulo="Descargar" destino="_blank" estilo="boton"
		}
		Texto titulo="Descripción" campo=VerificacionDocumento.descripcion ancho="420px"
		Texto tipo titulo="Tipo documento" campo=VerificacionDocumento.nombreTipoDocumento ancho="420px"
		Texto tipoUri titulo="Uri tipo documento" campo=VerificacionDocumento.uriTipoDocumento ancho="420px"
		Fecha fechaPresentacion titulo="Fecha de presentación" campo=VerificacionDocumento.fechaPresentacion
		//Texto version titulo="Versión" campo=VerificacionDocumento.version	
		Combo estado titulo="Estado" campo=VerificacionDocumento.estadoDocumentoVerificacion
		Grupo requerimientosNoValido titulo="Requerimiento/s del documento" mostrarSiCombo estado="noValido","noPresentado"{
			Grupo requerimientoTexto visible=false mostrarSiCombo estado="noValido","noPresentado"{
				AreaTexto requerimientoTexto titulo="Motivo requerimiento" campo=VerificacionDocumento.motivoRequerimiento
			}
			Grupo requerimientoCodigos visible=false mostrarSiCombo estado="noValido" {
				Tabla requerimientosDocumento titulo="Requerimientos" campo=VerificacionDocumento.codigosRequerimiento
					popupVer=PopUpDocumentoRequerimientosVerificacion
				{
					Columna titulo="Código" campo=CodigoRequerimiento.codigo ancho="150"
					Columna titulo="Descripción" campo=CodigoRequerimiento.descripcionCorta expandir
				}
			}
		}
	}
	
	Popup PopUpDocumentoRequerimientosVerificacion borrar ver titulo="Requerimiento" campo=VerificacionDocumento.codigosRequerimiento {
		Texto codigo titulo="Código" campo=CodigoRequerimiento.codigo
		Texto descripcionCorta titulo="Descripción Corta" campo=CodigoRequerimiento.descripcionCorta
		AreaTexto descripcion titulo="Descripción" campo=CodigoRequerimiento.descripcion
	}
	
	Popup PopUpDocumentoRequerimientosVerificacionModificar crear modificar titulo="Requerimiento" campo=VerificacionDocumento.codigosRequerimiento {
		Combo descripcionCorta titulo="Descripción Corta" campo=CodigoRequerimiento.descripcionCorta requerido
		Grupo descripciones visible=false permiso=noEditable{
			Texto codigo titulo="Código" campo=CodigoRequerimiento.codigo
			AreaTexto descripcion titulo="Descripción" campo=CodigoRequerimiento.descripcion
		}
	}
	
	Pagina Exclusion noForm permiso=accederSiSolicituNoBorrador{
		Tabla exclusion titulo="Codigos de Exclusión" campo=SolicitudGenerica.exclusion.codigos
		popupVer=PopUpExclusion
		popupCrear=PopUpExclusion
		popupBorrar=PopUpExclusion
		{
			Columna titulo="Código" campo=CodigoExclusion.codigo
			Columna titulo="Descripción corta" campo=CodigoExclusion.tipoCodigo.descripcionCorta expandir
		}
		
		Form finalizarExclusion {
			Boton finalizarExclusionBoton titulo="Finalizar Exclusión"
		} 
	}
	
	Popup PopUpExclusion crear ver borrar campo=SolicitudGenerica.exclusion.codigos {

		Combo descripcionCorta campo=CodigoExclusion.tipoCodigo.descripcionCorta titulo="Descripción Corta"
		Grupo permiso=noEditable {
			Texto codigo campo=CodigoExclusion.codigo titulo="Código"
			AreaTexto descripcion campo=CodigoExclusion.tipoCodigo.descripcion titulo="Descripcion" 
		}
	}
	
}