Formulario Solicitud {
	
    Menu {
    	Enlace "Volver" pagina=Principal.Solicitudes
    	
    	Grupo "Combos" {
			Enlace "Combos por defecto" pagina=Combos
			Enlace "Combos sobreescritos" pagina=CombosOverwrite
			Enlace "Valores por defecto" pagina=ValoresPorDefecto
			Enlace "ErrorCombos" pagina=PaginaCombosError
		}
		
		Grupo "Solicitante" {
			Enlace "Solicitante" pagina=Solicitante
		}
		
		Grupo "Direccion"{
			Enlace "Direccion" pagina=Direcciones
			Enlace "Fecha" pagina=Fechas
		}
		
		Grupo "Tablas y PopUps" {
			Enlace "Tablas Simples" pagina=TablasSimples
		}
	
		Grupo "SubirArchivo" {
			Enlace "SubirArchivoAed" pagina=SubirArchivoAedTest
		}
		
		Grupo "Pagina del módulo"{
			Enlace "Titulo" pagina=Solicitud.PCEconomicos
		}	

		Grupo "Test Grupos" {
			Enlace "Ejemplos Grupos I" pagina=EjemplosdeGrupos
		}
		
		Grupo "Test Grupos" {
			Enlace "Ejemplos Grupos I" pagina=EjemplosdeGrupos
		}
		
		Grupo "Tipos de Ayuda" {
			Enlace "Ayuda Texto" pagina=TipoDeAyudaTexto
			Enlace "Ayuda Popover" pagina=TipoDeAyudaPopover
		}

		Grupo "Permisos" {
			Enlace "Permiso pagina" pagina=PermisoPagina permiso=administrador
			Enlace "Permiso grupo" pagina=PermisoPagina permiso=administrador
			Enlace "Permiso no editable" pagina=PermisoNoEditable permiso=noEditable 
		}

		
		Enlace "Salir" accion="fap.SecureController.logout"
    }
		
	Pagina Fechas campo=Solicitud{
		Fecha campo=Solicitud.fechas.fechaRequerida titulo="Fecha Obligatoria" requerido
		Fecha campo=Solicitud.fechas.fecha titulo="Fecha" requerido
	}
	
	Pagina Direcciones campo=Solicitud{
		Direccion campo=Solicitud.direccionTest.direccion
	}
	
	Pagina Combos inicial campo=Solicitud{

		Grupo titulo="Campos sin sobreescribir los controladores manuales" {
			Combo campo=Solicitud.comboTest.list titulo="Combo de Tabla de Tablas"
			Combo campo=Solicitud.comboTest.listMultiple titulo="Combo Múltiple de Tabla de Tablas"
			Combo campo=Solicitud.comboTest.ref comboTexto=nombre titulo="Combo de Referencias"
			Combo campo=Solicitud.comboTest.refMultiple comboTexto=nombre titulo="Combo de Referencias Múltiples"
		}
				
		Tabla comboTestRef campo=ComboTestRef popup=ComboTestRef titulo="Referencias de combos (Los combos no se actualizan automáticamente)"{
			Columna campo=ComboTestRef.id
			Columna campo=ComboTestRef.nombre expandir
		}
	}

	Pagina CombosOverwrite campo=Solicitud{
		
		Grupo titulo="Combos sobreescritos en el controlador manual" {
			Combo lista campo=Solicitud.comboTest.listOverwrite titulo="Combo manual (String)"
			Combo listaLong campo=Solicitud.comboTest.listOverwriteLong titulo="Combo manual (Long)"
			Combo listaMultiple campo=Solicitud.comboTest.listMultipleOverwrite titulo="Combo múltiple manual (Set<String>)"
			Combo listaMultipleLong campo=Solicitud.comboTest.listMultipleOverwriteLong titulo="Combo múltiple manual (Long<Long>)"
			Combo referencia campo=Solicitud.comboTest.refOverwrite comboTexto=nombre titulo="Combo de Referencias (Filtrando las que tienen en el nombre a)"
			Combo referenciaMultiple campo=Solicitud.comboTest.refMultipleOverwrite comboTexto=nombre titulo="Combo de Referencias Múltiples (Filtrando las que tienen en el nombre a)"
			
			Combo wsjson campo=Solicitud.comboTest.wsjson titulo="Valor del combo obtenido desde un servicio web en JSON"
			Combo wsxml campo=Solicitud.comboTest.wsxml titulo="Valor del combo obtenido desde un servicio web en XML"
		}
				
		Tabla comboTestRef campo=ComboTestRef popup=ComboTestRef titulo="Referencias de combos (Los combos no se actualizan automáticamente)"{
			Columna campo=ComboTestRef.id
			Columna campo=ComboTestRef.nombre expandir
			
		}
	}

	Popup ComboTestRef campo=ComboTestRef {
		Texto campo=ComboTestRef.nombre
		Combo campo=ComboTestRef.list titulo="Combo"
		Texto campo=ComboTestRef.nombre
	}
	
	Pagina PaginaCombosError campo=Solicitud{
		Wiki { "Error en los combos. Debe autocompletar los combos elegidos desde flash al estar el campo de texto vacío." }
		Texto campo=Solicitud.solicitante.telefonoFijo titulo="Telefono" requerido
		Combo campo=Solicitud.comboError comboTexto=nombre titulo="Error del combo"
		Combo nombre campo=Solicitud.comboErrorMany comboTexto=nombre titulo="Error del combo Many"
	}
	
	Pagina ValoresPorDefecto campo=Solicitud{
		Texto mString campo=Solicitud.valoresPorDefectoTest.mString titulo="String"
		Texto mLong campo=Solicitud.valoresPorDefectoTest.mLong titulo="Long"
		Texto mInteger campo=Solicitud.valoresPorDefectoTest.mInteger titulo="Integer"
		Check mBoolean campo=Solicitud.valoresPorDefectoTest.mBoolean titulo="Boolean"
		Texto mDouble campo=Solicitud.valoresPorDefectoTest.mDouble titulo="Double"
		Texto mLongText campo=Solicitud.valoresPorDefectoTest.mLongText titulo="LongText"
		
		Texto mLongText2 campo=Solicitud.valoresPorDefectoTest.mLongText titulo="LongText 3" password
		
		Texto mEmbeddable campo=Solicitud.valoresPorDefectoTest.mEmbebida.algo titulo="String embeddable" 
	}
	
	Pagina TablasSimples campo=Solicitud{
		Tabla tablaNombres campo=Solicitud.tablaDeNombres titulo="Solicitantes"
		popupCrear=popupNombre
		popupVer=popupNombre
		popupModificar=popupNombre
		popupBorrar=popupNombre
		columnasAutomaticas exclude=id, fecha
		{
		}
		Tabla tablaNombres2 campo=Solicitud.peta titulo="Solicitantes"
		popupCrear=popupNombre2
		popupVer=popupNombre2
		popupModificar=popupNombre2
		popupBorrar=popupNombre2
		columnasAutomaticas
		{
		}
		
		
	}
	
	Popup popupNombre2 campo=Solicitud.peta {
		Texto campo=Peta.n titulo="Nombre"
		Texto campo=Peta.d titulo="Apellido"
		Texto campo=Peta.otro titulo="Otro contacto"
		Combo campo=Peta.petador titulo="Petador"
		Fecha campo=Peta.fecha titulo="Fecha"
	}
	
	
	Popup popupNombre campo=Solicitud.tablaDeNombres {
		Texto campo=TablaDeNombres.nombre titulo="Nombre"
		Texto campo=TablaDeNombres.apellido titulo="Apellido"
	}
	
	Pagina Documentos campo=Solicitud.documentacion.documentos  permiso=aportacionDocumentos {
		Accion crear redirigir=SubirArchivoAedTest	
		SubirArchivoAed fileAportacion campo=Documento requerido tramite="Solicitud" aportadoPor="CIUDADANO"
	}
	
	Pagina SubirArchivoAedTest campo=Solicitud{
		Tabla documentos campo=Solicitud.documentacion.documentos paginaCrear=Documentos{
			Columna campo=Documento.id
		}	
	}
	
	Pagina EjemplosdeGrupos campo=Solicitud{
		Grupo testMostrarSiExpresion titulo="Grupo 1: Mostrar Si Expression" mostrarSiExpresion "4 == 4"{
			Texto ifTexto titulo="Texto" campo=Solicitud.testGrupo.nombre
 		}
 		
 		Grupo test2MostrarSiExpresion titulo="Grupo 1: Mostrar Campo" mostrarSiCampo Solicitud.solicitante.email = "pepe@pepe.com", "juan@juan.com" {
			Texto if2Texto titulo="Texto" campo=Solicitud.testGrupo.nombre
 		}
 		
 		Combo comboMostrarSi campo=Solicitud.comboTest.list titulo="Prueba para mostrarSiCombo"
 		
 		Grupo titulo="Grupo 1: Mostrar Si Combo" mostrarSiCombo comboMostrarSi = "a"{
 			Wiki {"El combo tiene un valor seleccionado igual a 'A'"}
 		}
	}


	Pagina PermisoPagina campo=Solicitud permiso=administrador{
		Wiki {"Si puedes ver esta página tienes que ser administrador."}
	}
	
	Pagina PermisoGrupo campo=Solicitud permiso=administrador {
		Wiki {"La página tiene un grupo que es sólo visible para administradores"}
		Grupo permiso=administrador {
			Wiki {"Este grupo es sólo visible para administradores"}	
		}
	}
	
	Pagina PermisoNoEditable campo=Solicitud permiso=noEditable {
		Wiki {"Esta página es no editable"}	
		Texto ifTexto titulo="Texto" campo=Solicitud.solicitante.nombreCompleto
		Boton noHagoNada titulo="No hago Nada"
	}
	
	Pagina TipoDeAyudaTexto {
		Grupo ayudasDeTexto titulo="Ayudas de tipo texto simples"{
			Texto campo=Solicitud.solicitante.telefonoFijo titulo="Texto" requerido ayuda="Ésto es una ayudita"
			Fecha campo=Solicitud.fechas.fechaRequerida titulo="Fecha Obligatoria" ayuda="Ayuda de fecha"
			Combo campo=Solicitud.comboTest.list titulo="Combo de Tabla de Tablas" ayuda="Ayuda del combo simple"
			Combo campo=Solicitud.comboTest.listMultiple titulo="Combo Múltiple de Tabla de Tablas" ayuda="Ayuda del otro combo" requerido
			Boton noHagoNada titulo="Guardar desde aquí" ayuda="Ayuda del botón"
		}
	}
	
	Pagina TipoDeAyudaPopover {
		Grupo ayudasDePopover titulo="Ayudas de tipo popover"{
			Texto campo=Solicitud.solicitante.telefonoMovil titulo="Texto" requerido ayuda="Titulo de la Ayuda. Ésto es un ejemplo de la ayuda de tipo popover, la cual puede ser todo lo larga que quieras. Pruébala!." (propover)
			Fecha campo=Solicitud.fechas.fechaRequerida titulo="Fecha Obligatoria" ayuda="Titulo de la Ayuda de fecha.  Ésto es un ejemplo de la ayuda de tipo popover, la cual puede ser todo lo larga que quieras. Pruébala!." (propover)
			Combo campo=Solicitud.comboTest.list titulo="Combo de Tabla de Tablas" ayuda="Ayuda del combo simple.  Ésto es un ejemplo de la ayuda de tipo popover, la cual puede ser todo lo larga que quieras. Pruébala!." (propover)
			Combo campo=Solicitud.comboTest.listMultiple titulo="Combo Múltiple de Tabla de Tablas" ayuda="Ayuda del otro combo.  Ésto es un ejemplo de la ayuda de tipo popover, la cual puede ser todo lo larga que quieras. Pruébala!." (propover) requerido
			Boton noHagoNada titulo="Guardar desde aquí" ayuda="Titulo de la Ayuda del botón.  Ésto es un ejemplo de la ayuda de tipo popover, la cual puede ser todo lo larga que quieras. Pruébala!." (propover)
		}
	}
}
