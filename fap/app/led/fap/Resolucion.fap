Lista resolucionesDefinidas crearEnumerado {
	simpleTotal: "Simple Total"
	multipleTotal: "Múltiple Todos Expedientes"
	multipleParcialExpedientes: "Multiple Seleccionando Expedientes"
}

Lista tipoResolucion crearEnumerado {
	provisional: "Provisional"
	definitiva: "Definitiva"
}

Lista modalidadResolucion crearEnumerado {
	resolucionSimple: "Simple"
	resolucionMultiple: "Multiple"
}

Lista areasResolucion crearEnumerado {
	
}

Lista tiposResolucion crearEnumerado {
	
}

// Estados por los que va pasando la resolución
Lista estadoResolucion crearEnumerado {
	borrador: "Borrador"	// Ha sido creada, y establecido su tipo y modalidad
	creada: "Creada"
	preparada: "Preparada"
	pendienteFirmaJefeServicio: "Pendiente firma Jefe Servicio"
	firmadaJefeServicio: "Firmada Jefe Servicio"
	pendienteFirmaDirector: "Pendiente firma Director"
	firmada: "Firmada"
	registrada: "Registrada"
	publicada: "Publicada"
	finalizada: "Finalizada"
}

Lista estadosDocBaremacion crearEnumerado{
	generado: "Documentos Generado"
	clasificado: "Documentos Clasificado"
}

Lista estadoTipoMultiple crearEnumerado {
	parcial: "Selecionar manualmente los expedientes"
	total: "Todos los expedientes"
}

Lista estadoLineaResolucion crearEnumerado {
	concedida: "Concedida"
	excluida: "Excluida"
	denegada: "Denegada"
}

Lista prioridadesFirmaEnPortafirma crearEnumerado {
	baja: "Baja"
	media: "Media"
	alta: "Alta"
}

Lista estadoPortafirma crearEnumerado {
	Borrador: "Borrador"
	Validandofirmas: "Validando firmas"
	Pendiente: "Pendiente"
	Enviada: "Enviada"
	Firmadayfinalizada: "Firmada y finalizada"
	Rechazada: "Rechazada"
	Cerrada: "Cerrada"
	Pendientevalidacionfirmas: "Perndiente de validación de firmas"
}

Lista estadoResolucionPublicacion crearEnumerado {
	publicada: "Publicada"
	documentoGenerado: "Documento Resolución Generado"
	
	estadoFecha: "Publicacion: Estado y Fecha"
	//copiaExpedientes: "Publicacion: Copiados Expedientes" ES publicada
	firmaBaremacionResolucion: "Firmados Documentos de Baremación"
	
}

// Una línea de resolución afecta a un expediente (o Solicitud)
Entidad LineaResolucionFAP {
	SolicitudGenerica solicitud noConstruct
	Lista estadoLineaResolucion estado
	Double puntuacionBaremacion
	Moneda importeTotal
	Moneda importeConcedido
	Documento docBaremacion // Documento visible para el solicitante de la Baremación del Expediente
	Documento docEvaluacionCompleto//docBaremacionOficialConComentarios
	Documento docEvaluacionCompletoConComentarios //docBaremacionOficialSinComentarios
}

// Entidades de la resolución
Entidad ResolucionFAP {
	/// Tipo de los posibles entre los definidos de la resolución
	Lista resolucionesDefinidas tipoDefinidoResolucion
	
	Lista modalidadResolucion modalidad		// Simple, Multiple
	Lista tipoResolucion tipo				// Provisional, Definitiva
	Lista estadoTipoMultiple tipoMultiple	// Parcial, Total

	Boolean firmarJefeServicio				// Debe ser firmada por el Jefe de Servicio
	String jefeDeServicio					// Jefe de Servicio Solicitado
	Boolean firmarDirector					// Debe ser firmada por el Director
	Boolean permitirPortafirma
	Boolean permitirRegistrar
	Boolean permitirPublicar
	
	String prioridadFirma
	DateTime fechaTopeFirma
	
	Lista estadoResolucion estado			// borrador, creada, .... finalizada
	Lista estadoResolucionPublicacion estadoPublicacion
	Lista estadosDocBaremacion estadoDocBaremacionResolucion
	Lista estadosDocBaremacion estadoInformeBaremacionConComentarios
	Lista estadosDocBaremacion estadoInformeBaremacionSinComentarios
	
	// Descripción de la Resolución
	String descripcion length=2500
	
	// modalidad=Simple   --> Una sola línea de resolución
	// modalidad=Múltiple --> Varias líneas de resolución
	OneToMany LineaResolucionFAP lineasResolucion
	
	Boolean conBaremacion default = 'false'
	Boolean oficialEvaluacionCompleto default = 'false'

	String tituloInterno					// Obligatorio.
	String sintesis
	String observaciones
	Integer folio_inicio					// Folio de inicio en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer folio_final						// Folio final en el libro de resoluciones. Valor devuelto por SW de Registro de Resoluciones
	Integer numero_folios
	Integer numero							// Número de resolución. Valor devuelto por SW de Registro de Resoluciones
	DateTime fechaRegistroResolucion		// Fecha de la resolución
	Lista areasResolucion areasResolucion
	Lista tiposResolucion tiposResolucion

	String idSolicitudFirma
	String codigoResolucion

	DateTime fechaFinAceptacion						// Debería coincidir con la fecha fin reformulacion y fin alegacion si existe
	//DateTime fechaPublicacion						// Fecha de Publicación en el tablón de anuncios de la sede
	//DateTime fechaBOC								// Fecha de Publicación en el BOC
	DateTime fechaIncioPreparacion		// Fecha en la que se empieza a preparar la resolución y se crean las líneas
	//DateTime fechaFinPreparacion					// Fecha en la que se finaliza la resolución
	//Documento docResolucion							// Documento de Resolución
	//OneToMany Documento docAnexoResolucion //Anexos individuales de resolucion
	OneToMany Documento docConsultaPortafirmasResolucion //Documentos de Consulta Portafirmas.

	Registro registro
	OneToMany Interesado destinatarios transient
	ExpedienteAed expedienteAed
	
		//Long IDPortafirma					//ID devuelto por el Servicio WEb del Portafirmas
		//String estadoPortafirmas			//??Pendiente de revisar
	SolicitudPortafirmaFAP solicitudFirmaJefeServicio
	SolicitudPortafirmaFAP solicitudFirmaDirector

	//Lista estadoResolucionConcesion estado		//Estado de la resolución: 1.0:Creada,2.0:Preparada, 3.0:En_Firma, 4.0:Firmada, 5.0:Registrada, 6.0:Publicada
		//OneToMany LineaResolucion lineasResolucion
		// OneToMany ExpedientesAlegados expedientesAlegados
		// OneToMany ExpedientesNoAceptados expedientesNoAceptados
	//Agente destinatarioPortafirma transient

	Agente hacePeticionPortafirma noConstruct
	//Para firmar por jefe de servicio
	//Firmantes firmantesJefeDeServicio
		////Para poder firmar y crear el el metodo calcular firmantes
		//Solicitante solicitanteJefeServicio transient

}

Entidad SolicitudPortafirmaFAP {
	String codSolicitudPf
	String estadoPf
	String tituloPf
	String descripcionPf
	String solicitantePf
	String destinatarioPf
	String emailNotificacionPf
	String comentarioPf
	String urlCorreoRedireccionPf
	String flujoAnteriorPf
}
	
	

Formulario Principal {
	Pagina ResolucionesFAP titulo="Lista de Resoluciones" {
		Tabla listaResoluciones titulo="Resoluciones" campo=ResolucionFAP paginaCrear=NuevaResolucion paginaEditar=EditarResolucion
		{
			Columna campo=ResolucionFAP.id titulo="ID" ancho="40"
			Columna campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resolución"
			Columna campo=ResolucionFAP.tipo titulo="Tipo" ancho="100"
			Columna campo=ResolucionFAP.estado titulo="Estado"
			Columna campo=ResolucionFAP.modalidad titulo="Modalidad"
		}
	}
	
	Pagina NuevaResolucion titulo ="Resolución" campo=ResolucionFAP perteneceA="Principal" {
		Accion crear redirigir=EditarResolucion
		Grupo gIniciarTipo titulo="Seleccionar Resolución" {
			Combo tipoDefinidoResolucion campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resolución"
			Combo campo=ResolucionFAP.tipo titulo="Tipo"
			Texto campo=ResolucionFAP.tituloInterno titulo="Título" requerido
			AreaTexto campo=ResolucionFAP.sintesis titulo="Síntesis" requerido
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripción"
			AreaTexto campo=ResolucionFAP.observaciones titulo="Observaciones"
			Combo areasResolucion campo=ResolucionFAP.areasResolucion titulo="Área resolución" requerido
			Combo tiposResolucion campo=ResolucionFAP.tiposResolucion titulo="Tipo resolución" requerido
			Check campo=ResolucionFAP.conBaremacion titulo="¿Tiene Baremación?"
		}
	}

	Pagina EditarResolucion titulo="Resolución" campo=ResolucionFAP perteneceA="Principal" noForm {
		
		Grupo titulo="Seleccionar expediente" permiso=seleccionarUnExpediente {
			Tabla tablaExpedientesUnico campo=SolicitudGenerica seleccionable="Crear Resolucion" {
				Columna titulo = "ID" campo=SolicitudGenerica.id ancho = "40"
				Columna titulo = "Expediente" campo=SolicitudGenerica.expedienteAed.idAed
				Columna titulo = "Estado Interno" campo=SolicitudGenerica.estadoValue ancho = "120"
				Columna titulo = "Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
			}
		}
		
		Grupo mostrarSiCampo ResolucionFAP.modalidad="resolucionMultiple" borde=false  {
			Grupo titulo="Expedientes a Resolver" permiso=seleccionarExpedientes {
			
				Form formSeleccionarExpedientes {
					Grupo titulo="Seleccionar Expedientes"{
						Combo cmbTipo campo=ResolucionFAP.tipoMultiple titulo="Seleccionar" requerido
						Boton btnGuardar titulo="Guardar"
					}
				}
			
				Tabla tablaExpedientes campo=SolicitudGenerica seleccionable="Seleccionar" {
					Columna titulo="ID" campo=SolicitudGenerica.id ancho="40"
					Columna titulo="Expediente" campo=SolicitudGenerica.expedienteAed.idAed
					Columna titulo="Estado Interno" campo=SolicitudGenerica.estadoValue ancho="120"
					Columna titulo="Nombre Solicitante" campo=SolicitudGenerica.solicitante.nombreCompleto expandir
				}
			}
			
			Grupo gCrearResolucion titulo="Preparar la Resolución" permiso=noSeleccionarExpedientes {
				Wiki { "Si su resolución está lista para crearla, pulse el siguiente botón." }
				Form frmCrearResolucion {
					Boton  btnCrearResolucion titulo="Crear Líneas de Resolución"
				}
			}
		}
		
    	Grupo gCerrarResolucion titulo="Preparar la Resolución" permiso=prepararResolucion {
			Wiki { "Si su resolución está lista para firmar, pulse el siguiente botón." }
			Tabla lineasResolucionAntesPreparar campo=ResolucionFAP.lineasResolucion titulo="Lineas Resolución" popupEditar=ModificarEstadoExpediente popupLeer=VerExpediente {
				Columna campo=LineaResolucionFAP.id titulo="Id" ancho="40"
				Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
				Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
				Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
				Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante"
				Columna campo=LineaResolucionFAP.estado titulo="Estado"
			}
			Form prepararResolucion {
				Boton  btnPrepararResolucion titulo="Preparar Resolución"
			}
		}
		
		Grupo gPrepararSolicitudFirmaPortafirma titulo="Preparar Solicitud de Firma" permiso=solicitudFirmaResolucion {
			
			// Si el Agente es un JefeDeServicio, puede firmar directamente
			Grupo grSolicitarFirmaPorJefeSevicio borde=false permiso=portafirmaJefeServicio {
				Grupo grFirmaJefeServicio borde=false {
	     			Form frmFirmarJefeServicio redirigir=EditarResolucion {
						FirmaSimple firFirmarResolucion titulo="Firmar Resolucion" documento=ResolucionFAP.registro.oficial
							listaFirmantes=ResolucionFAP.registro.firmantes.todos calcularFirmantes="resolucionFAP.calcularFirmantes()"
					}
					Wiki {"Nota: La operación de firmar y registrar puede tardar varios minutos."}
				}
			}
			
			Grupo gEnviarFirmaJSPortafirma permiso=firmaJefeServicio borde=false {
				
				Enlace titulo="Obtener documento de Resolución" estilo="btn" campo=ResolucionFAP.registro.borrador.urlDescarga destino="_blank"
				Tabla documentosPortafirma titulo="Documentos de Consulta Portafirmas" campo=ResolucionFAP.docConsultaPortafirmasResolucion paginaCrear=DocConsultaPortafirmaCrear paginaEditar=DocConsultaPortafirmaEditar {
					Columna campo=Documento.tipo titulo="Tipo"
					Columna campo=Documento.descripcionVisible titulo="Descripción" expandir
					Columna funcion="<a href=\"${Documento.urlDescarga}\" target=\"_blank\">Descargar</a>" titulo="Ver" ancho="80"
				}

				Form formSelectJefeServicio {
					Combo selectJefeServicio campo=ResolucionFAP.jefeDeServicio titulo="Destinatario Portafirma" requerido
					Combo selectPrioridadFirma campo=ResolucionFAP.prioridadFirma titulo="Prioridad Firma" requerido
					Fecha fechaTopeFirma campo=ResolucionFAP.fechaTopeFirma titulo="Fecha Tope Firma" requerido
					Texto campo=ResolucionFAP.numero_folios titulo="Nº Folios" requerido ayuda = "Número de folios del documento de la resolución" (popover)
					Boton enviarFirmaJSPortafirma titulo="Enviar al portafirma"
				} 
			}
			
			Grupo gEsperaFirmaJSPortafirma permiso=esperaFirmaJefeServicio titulo="Firma del Jefe de Servicio"{
				Grupo permiso=noEditable borde=false {
					Combo selectJefeServicio campo=ResolucionFAP.jefeDeServicio titulo="Jefe de Servicio Seleccionado"
				}
				Wiki { "_El jefe de servicio aún no ha firmado en el portafirma_" } 
			}
			
			Grupo gEnviarFirmaDirectorPortafirma permiso=firmaDirector titulo="Firma del Director" {
				Form firmaDirectorPortafirma {
					Boton enviarFirmaDirectorPortafirma titulo="Solicitar firma al Director"
				}
			}
			
			Grupo gEsperaFirmaDirectorPortafirma permiso=esperaFirmaDirector titulo="Firma Director"{
				Wiki { "_El Director aún no ha firmado en el portafirma. Si quiere comprobar si ya se firmó darle al siguiente botón._" }
				Form frmComprobarFirmas {
					Boton comprobarFirmado titulo="Comprobar"
				}
			}
		}
		
		Grupo gRegistrarResolucion titulo="Registro de la Resolución" permiso=registrarResolucion {
			Grupo borde=false {
				Enlace campo=ResolucionFAP.registro.oficial.urlDescargaFirmado titulo="Obtener Documento Resolución" destino="_blank" estilo="btn"
			}
			
			Wiki { "Para proceder a registrar la resolución pulse el siguiente botón: \"Registrar resolución\"" }
			
			Form enviarRegistrarResolucion {
      			Boton btnRegistrarResolucion titulo="Registrar Resolución"
    		}
		}
		
		Grupo gDocumentoDeResolucion titulo="Documento de Resolución" permiso=verDocumentoResolucion {
			Enlace campo=ResolucionFAP.registro.oficial.urlDescargaFirmado titulo="Resolución" destino="_blank" estilo="boton"
		}
		
		Grupo gPublicarResolucion titulo="Publicar la Resolución en el tablón de Anuncios" permiso=publicarResolucion {
			Form publicacionResolucion{
				Enlace publicarResolucionPasos titulo="Publicar la Resolucion" pagina=PaginaPublicarResolucion estilo="btn"
			}
		}
		
		Grupo gFinalizarResolucion titulo="Finalizar la Resolución" permiso=finalizarResolucion {
			Wiki { "Finalizar resolución" }
		}
		
		Grupo gDatosResolucion titulo="Datos de la Resolución actual" permiso=mostrarDatosResolucion {
			Combo campo=ResolucionFAP.tipoDefinidoResolucion titulo="Resolución"
			Combo campo=ResolucionFAP.tipo titulo="Tipo"
			Combo campo=ResolucionFAP.modalidad titulo="Modalidad de la Resolución" requerido
			Combo campo=ResolucionFAP.estado titulo="Estado"
			Texto campo=ResolucionFAP.tituloInterno titulo="Título" requerido
			AreaTexto campo=ResolucionFAP.sintesis titulo="Síntesis" requerido
			AreaTexto campo=ResolucionFAP.descripcion titulo="Descripción"
			AreaTexto campo=ResolucionFAP.observaciones titulo="Observaciones"
			Combo areasResolucion campo=ResolucionFAP.areasResolucion titulo="Área resolución" requerido
			Combo tiposResolucion campo=ResolucionFAP.tiposResolucion titulo="Tipo resolución" requerido
			Check campo=ResolucionFAP.conBaremacion titulo="¿Tiene Baremación?"
			
			Grupo tablaDeDatos permiso=mostrarDatosResolucion borde=false {
			Tabla lineasResolucion campo=ResolucionFAP.lineasResolucion titulo="Lineas Resolución" {
				Columna campo=LineaResolucionFAP.id titulo="Id"
				Columna campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
				Columna campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
				Columna campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
				Columna campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante"
				Columna campo=LineaResolucionFAP.estado titulo="Estado"
			}
			}
			Texto campo=ResolucionFAP.folio_inicio titulo="Folio Inicial"
			Texto campo=ResolucionFAP.folio_final titulo="Folio Final"
			Texto campo=ResolucionFAP.codigoResolucion titulo="Numero"
			Fecha campo=ResolucionFAP.fechaRegistroResolucion titulo="Fecha Resolución"
			Wiki { " Faltan más cosas ................ " }
		}
	}
	
	Popup ModificarEstadoExpediente campo=ResolucionFAP.lineasResolucion titulo="Modificar estado del expediente en la resolución"{
		Grupo grNoEditable borde=false permiso=noEditable {
			Texto campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
			Texto campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
			Texto campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
			Texto campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante"
		}
		Grupo grEditable borde=false {
			Grupo borde=false permiso=noEditable {
				Texto campo=LineaResolucionFAP.puntuacionBaremacion titulo="Puntuación Baremación"
			}
			
			Combo comboEstadoLinea campo=LineaResolucionFAP.estado titulo="Estado"

			Grupo gConcedido borde=false mostrarSiCombo comboEstadoLinea = "concedida","concedidaTurismo"{
				Texto campo=LineaResolucionFAP.importeTotal titulo="Importe Total"
				Texto campo=LineaResolucionFAP.importeConcedido titulo="Importe Concedido"
			}
		}
	}
	
	Popup VerExpediente campo=ResolucionFAP.lineasResolucion {
		Texto campo=LineaResolucionFAP.solicitud.expedienteAed.idAed titulo="Expediente"
		Texto campo=LineaResolucionFAP.solicitud.estado titulo="Estado Solicitud"
		Texto campo=LineaResolucionFAP.solicitud.solicitante.numeroId titulo="Nif/Cif Solicitante"
		Texto campo=LineaResolucionFAP.solicitud.solicitante.nombreCompleto titulo="Solicitante"
		Combo campo=LineaResolucionFAP.estado titulo="Estado"
	}

	Pagina DocConsultaPortafirmaCrear campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion crear redirigir=EditarResolucion
		SubirArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
	}
	
	Pagina DocConsultaPortafirmaEditar campo=ResolucionFAP.docConsultaPortafirmasResolucion {
		Accion editar redirigir=EditarResolucion
		EditarArchivo file campo=Documento requerido /*tramite="ESTANCIAS"*/
	}
	
	Pagina PaginaPublicarResolucion campo=ResolucionFAP perteneceA="Principal" noForm{
		Grupo grupo titulo="Publicación de la Resolución" {
			Form cambioEstadoFecha {
				Grupo grupoCambioEstadoFecha titulo="Paso 1: Cambio de estado y actualizacion de datos" permiso=permisoCambioEstadoFecha{
					Fecha campo=ResolucionFAP.fechaFinAceptacion titulo="Fecha Fin de Aceptación" requerido
					Boton btnCambioEstadoFecha titulo="Actualizar"
				}
			}
			Form copiaExpediente permiso=permisoCopiaExpedientes{
				Grupo grupoCopiaExpediente titulo="Paso 2: Copia del Registro de Resolucion"{
					Boton btnCopiaExpediente titulo="Copiar" waitPopup
				}
			}
			Grupo grupoDocumentoResolucion titulo="Paso 3: Documento de Baremacion en Resolucion" permiso=permisoGenerarBaremacionResolucion{
				Form generarDocumentoBaremacionResolucion permiso=permisoGenerarDocBaremacion{
					Grupo grupoGenerarDocumento titulo="Paso 3.1: Generación y Almacenamiento de Documentos"{
						Boton btnGenerarDocumento titulo="Generar" waitPopup
					}
				}
				Form generarClasificarDocumentos permiso=permisoClasificarDocBaremacion {
					Grupo grupoClasificarDocumentos titulo="Paso 3.2: Clasificar los documentos"{
						Boton btnClasificarDocumentos titulo="Clasificar" waitPopup
					}
				}
				
				Form generarInformeConComentarios permiso=permisoGenerarInformeConComentarios{
					Grupo grupoGenerarInformeConComentarios titulo="Generar Informe con Comentarios de la Administracion"{
						Boton btnGenerarInformeConComentarios titulo="Generar"
					}
				}
				Form clasificarInformeConComentarios permiso=permisoClasificarInformeConComentarios{
					Grupo grupoClasificarInformeConComentarios titulo="Clasificar Informe con Comentarios" {
						Wiki {"El informe ya ha sido generado, debe proceder a su clasificacion"}
						Boton btnClasificarInformeConComentarios titulo="Clasificar"
					}
				}
				Form generarInformeSinComentarios permiso=permisoGenerarInformeSinComentarios{
					Grupo grupoGenerarInformeSinComentarios titulo="Generar Informe sin Comentarios de la Administracion"{
						Boton btnGenerarInformeSinComentarios titulo="Generar"	
					}
				}
				Form clasificarInformeSinComentarios permiso=permisoClasificarInformeSinComentarios{
					Grupo grupoClasificarInformeSinComentarios titulo="Clasificar Informe con Comentarios" {
						Wiki {"El informe ya ha sido generado, debe proceder a su clasificacion"}
						Boton btnClasificarInformeSinComentarios titulo="Clasificar"
					}
				}
				
			}
			Form firmarBaremacion permiso=permisoFirmarDocBaremacionResolucion{
				Grupo grupoFirmarBaremacion titulo="Firmar Documentos de Baremación en Resolución"{
					Boton btnFirmarBaremacion titulo="Firmar"
				}
			}
			Form volver{
				Grupo grupovolver borde=false {
					Boton btnVolver titulo="Volver" pagina=EditarResolucion 
				}
			}
		}
	}
}

//Sobreescrito
Permiso permisoGenerarInformeConComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.estadoInformeBaremacionConComentarios = null and
		resolucion.estadoDocBaremacionResolucion = "clasificado"
	return
		editable
}

//Sobreescrito
Permiso permisoGenerarInformeSinComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.estadoInformeBaremacionSinComentarios = null
		and resolucion.estadoDocBaremacionResolucion = "clasificado"
	return 
		editable
}

//Sobreescrito
Permiso permisoClasificarInformeConComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
		and resolucion.estadoInformeBaremacionConComentarios != null
		and resolucion.estadoInformeBaremacionConComentarios = "generado"
	return
		editable
}

//Sobreescrito
Permiso permisoClasificarInformeSinComentarios {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
		and resolucion.estadoInformeBaremacionSinComentarios != null
		and resolucion.estadoInformeBaremacionSinComentarios = "generado"
	return 
		editable
}

Permiso permisoClasificarDocBaremacion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoDocBaremacionResolucion != null and
		resolucion.estadoDocBaremacionResolucion = "generado" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoDocBaremacionResolucion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso permisoGenerarDocBaremacion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoDocBaremacionResolucion = null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoDocBaremacionResolucion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso permisoCambioEstadoFecha {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion = null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoPublicacion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso permisoCopiaExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion != null and
		resolucion.estadoPublicacion = "estadoFecha" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		editar
	when
		resolucion.estadoPublicacion != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}


//Sobreescrito para que consulte los métodos de si se generan los documentos
Permiso permisoGenerarBaremacionResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion != null and
		resolucion.estadoPublicacion = "publicada" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.conBaremacion = true
	return
		editar
	when
		resolucion.estadoPublicacion != null and
		resolucion.conBaremacion = true
	return 
		visible
}

//Sobreescrito
Permiso permisoFirmarDocBaremacionResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estadoPublicacion != null and
		resolucion.estadoPublicacion = "documentoGenerado" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio") and
		resolucion.estadoInformeBaremacionConComentarios = "clasificado" and
		resolucion.estadoInformeBaremacionSinComentarios = "clasificado" and
		resolucion.conBaremacion = true
	return
		editable
}

Permiso sinIniciarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion = null
		or resolucion.tipo = null
	return
		[editar crear]
	when
		resolucion.tipo != null
	return
		visible
}

Permiso mostrarDatosResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado != null and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return 
		visible
}

Permiso seleccionarExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "borrador" and
		resolucion.tipoMultiple = "parcial" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

Permiso portafirmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo = "jefeServicio"
		and resolucion.firmarJefeServicio = "true"
		and resolucion.estado = "preparada"
	return
		all
}

Permiso seleccionarUnExpediente {
	vars
		ResolucionFAP resolucion
	when
		resolucion.modalidad="resolucionSimple" and
		resolucion.estado = "borrador" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

Permiso noSeleccionarExpedientes {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "borrador" and
		resolucion.tipoMultiple = "total" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

Permiso solicitudFirmaResolucion {
	vars
		ResolucionFAP resolucion
	when
		agente.rolActivo in ("gestor", "administrador", "jefeServicio")
		and resolucion.estado in ("preparada", "pendienteFirmaJefeServicio", "firmadaJefeServicio", "pendienteFirmaDirector")
	return
		all
}

Permiso prepararResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado = "creada" and
		agente.rolActivo in ("administrador", "gestor", "jefeServicio")
	return
		all
}

/**
 * Visible el envío a portafirma para el Jefe de Servicio
 */
Permiso firmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("preparada")
		and resolucion.firmarJefeServicio = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("preparada")
		and resolucion.firmarJefeServicio = "true"
	return
		visible
}

Permiso esperaFirmaJefeServicio {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("pendienteFirmaJefeServicio")
		and resolucion.firmarJefeServicio = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("pendienteFirmaJefeServicio")
		and resolucion.firmarJefeServicio = "true"
	return
		visible
}


/**
 * Visible el envío a portafirma para el Director
 */
Permiso firmaDirector {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("firmadaJefeServicio")
		and resolucion.firmarDirector = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("firmadaJefeServicio")
		and resolucion.firmarDirector = "true"
	return
		visible
}

Permiso esperaFirmaDirector {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("pendienteFirmaDirector")
		and resolucion.firmarDirector = "true"
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
	when
		resolucion.estado in ("pendienteFirmaDirector")
		and resolucion.firmarDirector = "true"
	return
		visible
}

Permiso registrarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("firmada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

Permiso publicarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("registrada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

Permiso finalizarResolucion {
	vars
		ResolucionFAP resolucion
	when
		resolucion.estado in ("publicada")
		and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}

Permiso verDocumentoResolucion {
	vars
		ResolucionFAP resolucion
	when
	resolucion.estado in ("registrada", "publicada", "finalizada")
	and agente.rolActivo in ("gestor", "administrador", "jefeServicio")
	return
		all
}