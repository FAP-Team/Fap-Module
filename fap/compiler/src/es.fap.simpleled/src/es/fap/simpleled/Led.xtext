grammar es.fap.simpleled.Led with org.eclipse.xtext.common.Terminals

generate led "http://www.fap.es/simpleled/Led"

Model:
	(
		(entidades += Entity) |
	 	(formularios += Formulario) |
	 	(listas += Lista) |
	 	(permisosGrafico += PermisoGrafico) | 
	 	(permisosAcceso += PermisoAcceso)
	 ) *
;

QN: // Qualified Name
	IDS ('.' IDS)*
;

IDS:  
	ID | "Solicitud" | "Nip" | "Direccion" | "Persona" | "PersonaFisica" |
	"PersonaJuridica" | "provincia" | "pais" | "valor" | "Solicitante" |
	"agente" | "documento" | "password" | "aportadoPor" | "texto"
;

Entity:
	elemento='Entidad' name=IDS
		(
		("extends" extends=[Entity|IDS])?
		& ('tableName' '=' tableName=STRING)? 
		& (nonPersist?="nonPersist")?
		& (embedded?="embeddable")?
		& (inheritance?="inheritance" "=" "joined")?
		& (superClass?="superClass")?
		& (noConstruct?="noConstruct")?
		& (auditable?="auditable" | noAuditable?="noAuditable")?
		)
	'{'
		attributes+=Attribute*
	'}'
;

Attribute:
	type=Type name=IDS
		(
		("default" "=" defaultValue=STRING)?
		& (isTransient?="transient")?
		& (hasLength?="length" "=" length=INT)?
		& (required?="requerido")?	// NOTA: No se comprobará en la Entidad, sino en los "validators"
		& (noConstruct?="noConstruct")? // No crea el new para ese atributo
		& (noCascade?="noCascade")?
		& ("checkWith" "=" checkWith=STRING)? // Manual validator
		& ("column" "=" column=STRING)?
		& ("validate" "=" validate=STRING)?  // validacion por regexp
		)
;

Type:
	simple=SimpleType | special=SpecialType | compound=CompoundType
;

SimpleType:
	type = ("String" | "Long" | "Integer" | "Boolean" | "Double" | "LongText")
;

SpecialType:
	type = ("Cif" | "Telefono" | "Email" | "Moneda" | "DateTime")
;

CompoundType:
	((tipoReferencia=AttributeReferences)? entidad=[Entity|IDS])
	| ('Lista' (multiple?="multiple")? lista=[Lista|IDS])
	| (collectionType=tipoColeccion "<" collectionReferencia=SimpleType ">")
;

tipoColeccion:
	type= ("Set" | "List")
;	

AttributeReferences:
	type= ("OneToOne" | "OneToMany" | "ManyToOne" | "ManyToMany")
; 

Formulario:
	elemento='Formulario' name=IDS
	(
		("campo" "=" campo=Campo)?
		& ("permiso" "=" permiso=[PermisoAcceso])?
		& (inicial?="inicial")?
	)
	'{'
		(menu = Menu)?
		(paginas += Pagina  |  popups += Popup)*
	'}'
;

Menu:
	elemento='Menu' '{'
		elementos +=MenuElemento+
	'}'
;

MenuElemento:
	MenuGrupo | MenuEnlace
;	

MenuGrupo:
	elemento='Grupo' titulo=STRING 
	("permiso" "=" permiso=[PermisoGrafico])?  '{'
		elementos +=MenuElemento*
	'}'
;

MenuEnlace:
	elemento='Enlace' titulo=STRING
	(
		("permiso" "=" permiso=[PermisoGrafico])?
		& (
			('pagina' '=' pagina=PaginaAccion) |
			('popup' '=' popup=PopupAccion) |
			('accion' '=' accion=STRING) |
			('url' '=' url=STRING) |
			(anterior?="anterior")
		)?
	)
;

PaginaAccion:
	pagina=[Pagina|IDS] accion=("leer" | "crear" | "editar" | "borrar")?
;

PopupAccion:
	popup=[Popup|IDS] accion=("leer" | "crear" | "editar" | "borrar")?
;

Accion:
	elemento="Accion" name=("crear"  | "editar" | "borrar")
	(
		("boton" "=" boton=STRING)?
		& ('redirigir' '=' (anterior?="anterior" | redirigir=PaginaAccion | redirigirUrl=STRING))?
		& ("mensajeOk" "=" mensajeOk=STRING)?
		& (noBorrarEntidad?='noBorrarEntidad')?
		& (crearSiempre?="crearSiempre")?
	)
;

Pagina:
	elemento='Pagina' name=IDS
	(
		("titulo" "=" titulo=STRING)?
		& ("campo" "=" campo=Campo)?
		& ("permiso" "=" permiso=[PermisoAcceso])?
		& (noForm?='noForm')?
		& (noAutenticar?='noAutenticar')?
		& (guardarParaPreparar?="guardarParaPreparar")?
		& (inicial?="inicial")?
	)
	'{'
		elementos+=Elemento*
	'}'
;

Popup:
	elemento='Popup' name=ID
	(
		('titulo' '=' titulo=STRING)?
		& ("campo" "=" campo=Campo)
		& ("permiso" "=" permiso=[PermisoAcceso])?
	)
	'{'
		elementos+=Elemento*
	'}'
;

Grupo:
	elemento='Grupo' (name=ID)? (
		('titulo' '=' titulo=STRING)? &
		('mostrarSiCombo' siCombo=[Combo|QN] '=' siComboValues=Values)? &
		('mostrarSiCheck' siCheck=[Check|QN] '=' siCheckValues=("true" | "false"))? &
		('mostrarSiCampo' campo=Campo '=' siCampoValues=Values)? &
		('mostrarSiExpresion' siExpresion=STRING)? &
		('borde' '=' borde=("true" | "false"))? & 
		("permiso" "=" permiso=[PermisoGrafico])?
	)
	'{'
		elementos+=Elemento*
	'}'
;	

AgruparCampos:
	elemento='AgruparCampos' '{'
		elementos+=Elemento*
	'}'
;	

Values:
	values+=STRING (',' values+=STRING)*
;

Elemento:
	Grupo | AgruparCampos | 
	Texto | Fecha | Combo | Tabla | SubirArchivo | Boton | Check | AgrupaBotones |
	Nip | Solicitante | PersonaFisica | PersonaJuridica | Persona  | Direccion | Wiki |
	AreaTexto | Enlace | Form |
	SubirArchivoAed | EditarArchivoAed | FirmaPlatinoSimple | 
	EntidadAutomatica | Accion
;

Campo:
	entidad=[Entity|IDS] (atributos=CampoAtributos)?
;

CampoAtributos:
	"." atributo=[Attribute|IDS] (atributos=CampoAtributos)?
;

TipoDeAyuda:
	'(' type=('texto' | 'propover') ')'
;

Texto:
	elemento='Texto' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('ancho' '=' ancho=STRING)?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& (requerido?='requerido')?
		& (duplicar?='duplicar')?
		& (password?='password')?
	)
;

AreaTexto:
	elemento='AreaTexto' (name=ID)? (
		  ('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('valor' '=' valor=STRING)?
		& ('filas' '=' filas=INT)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& (requerido?='requerido')?
	)
;

Check:
	elemento='Check' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
	)
;	


Enlace:
	elemento='Enlace' (name=ID)? (
		(('pagina' '=' pagina=PaginaAccion) | ('popup' '=' popup=PopupAccion) | ('campo' '=' campo=Campo) | ('url' '=' url=STRING) | (anterior?="anterior")) 
		& ('titulo' '=' titulo=STRING)?
		& ('destino' '=' destino=STRING)?
		& ('estilo' '=' estilo=STRING)?
	)
;

Wiki:
	elemento='Wiki' ('estilo' '=' estilo=STRING)? '{'
		wikiData+=STRING+
	'}'
;

AgrupaBotones:
	'AgrupaBotones' (type=('normal' | 'well'))?
	'{'
		botones+=Boton+
	'}'
;

Boton:
	elemento='Boton' (name=ID) (
	 	('titulo' '=' titulo=STRING)
	    & ('ancho' '=' ancho=STRING)? 
	    & ('pagina' '=' pagina=PaginaAccion)?
	    & ('popup' '=' popup=PopupAccion)?
	    & (anterior?="anterior")?
		& (waitPopup?="waitPopup")?	
		& ('type' '=' type=('default' | 'primary' | 'info' | 'success' | 'danger'))?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
	)
;

Fecha:
	elemento='Fecha' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& (requerido?='requerido')?
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
	)
;

Combo:
	elemento='Combo' (name=ID)? (
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
		& (requerido?='requerido')?
		& (busqueda?="busqueda")?
		& (mostrarClave?="mostrarClave")?
		& ('ancho' '=' ancho=STRING)? 
		& ('anchoTitulo' '=' anchoTitulo=STRING)?
		& ('comboTexto' '=' comboTexto=[Attribute|IDS])? 
		& ('comboValor' '=' comboValor=[Attribute|IDS])? 
		& ('ayuda' '=' ayuda=STRING (tipoAyuda=TipoDeAyuda)?)?
		& ('dependeDe' '=' dependeDe=[Combo])?
	)
;

Form:
	elemento='Form' (name=ID) (
		(autoEnviar?="autoEnviar")?
		& ("permiso" "=" permiso=[PermisoGrafico])?
		& ('redirigir' '=' redirigir=PaginaAccion)?
	)
	'{'
		elementos+=Elemento+
	'}'
;	

Tabla:
	elemento='Tabla' name=ID
	(
		('campo' '=' campo=Campo)
		& ('titulo' '=' titulo=STRING)?
        & ('alto' '=' alto=STRING)? 
        & (columnasAutomaticas?="columnasAutomaticas" (('exclude' '=' exclude=ListaAtributos) | ('include' '=' include=ListaAtributos))?)?
        & ("permiso" "=" permiso=[PermisoGrafico])?
		& (recargarPagina?="recargarPagina")?
		& ('seleccionable' "=" seleccionable=STRING)?
	    & ('popup' '=' popup=[Popup|IDS])?
	    & ('popupCrear' '=' popupCrear=[Popup|IDS])?
	   	& ('popupBorrar' '=' popupBorrar=[Popup|IDS])?
	   	& ('popupEditar' '=' popupEditar=[Popup|IDS])?
	   	& ('popupLeer' '=' popupLeer=[Popup|IDS])?
	   	& ('pagina' '=' pagina=[Pagina|IDS])?
	    & ('paginaCrear' '=' paginaCrear=[Pagina|IDS])?
	   	& ('paginaBorrar' '=' paginaBorrar=[Pagina|IDS])?
	   	& ('paginaEditar' '=' paginaEditar=[Pagina|IDS])?
	   	& ('paginaLeer' '=' paginaLeer=[Pagina|IDS])?
	)
	'{'
		columnas += Columna*
	'}'
;

ListaAtributos:
	atributos+=[Attribute|IDS] (',' atributos+= [Attribute|IDS])*
;

Columna:
	elemento='Columna' ( 
	       ( ('campo' '=' campo=Campo) | ('funcion' '=' funcion=STRING) )
	       & ('titulo' '=' titulo=STRING)? 
	       & ('ancho' '=' ancho=STRING)? 
		   & ("permiso" "=" permiso=[PermisoGrafico])?
	       & (expandir?='expandir')?
	)
;


SubirArchivo:
	elemento='SubirArchivo' name=ID (
		(requerido?='requerido')?
	)
;	

SubirArchivoAed:
	elemento='SubirArchivoAed' name=ID (
		('campo' '=' campo=Campo)
		& (requerido?='requerido')?
		& ('tramite' '=' tramite=STRING)?		
		& ('aportadoPor' '=' aportadoPor=STRING)?	// ¿Cambiar el tipo?
	)
;

EditarArchivoAed:
	elemento='EditarArchivoAed' name=ID (
		('campo' '=' campo=Campo)
		& (requerido?='requerido')?
	)
;

FirmaPlatinoSimple:
	elemento="FirmaSimple" (name=ID)
		"titulo" "=" titulo=STRING
		(
			"documento" "=" campo=Campo
		)
;

Direccion:
	elemento="Direccion" (name=ID)? (
		('campo' '=' campo=Campo)
		& ("titulo" "=" titulo=STRING)?
		& (provincia?="provincia")?
		& (pais?="pais")?
		& (requerido?="requerido")?
	    & ('ancho' '=' ancho=STRING)?
	)
;

Nip:
	elemento='Nip' (name=ID)? (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING) &
		(requerido?='requerido')?
	)
;

PersonaFisica:
	elemento='PersonaFisica' (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING)? &
		(requerido?='requerido')?
	)
;

PersonaJuridica:
	elemento='PersonaJuridica' (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING) &
		(requerido?='requerido')? &
		("permiso" "=" permiso=[PermisoGrafico])?
	)
;

Persona:
	elemento='Persona' (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING)? &
		(requerido?='requerido')? &
		("permiso" "=" permiso=[PermisoGrafico])?
	)
;

Solicitante:
	elemento=('Solicitante' | 'SolicitantePersonaFisica' | 'SolicitantePersonaJuridica') (name=ID) (
		('campo' '=' campo=Campo) & 
		('titulo' '=' titulo=STRING)? &
		(requerido?='requerido')? &
		("permiso" "=" permiso=[PermisoGrafico])?
		& (noRepresentante?='noRepresentante')?
	)
;

EntidadAutomatica:
	elemento='EntidadAutomatica' (name=ID) (	
		('campo' '=' campo=Campo) & 
		("permiso" "=" permiso=[PermisoGrafico])?
	)
;

Lista:
	elemento='Lista' name=ID (enumerado?='crearEnumerado')? '{'
		elementos+=ElementoLista*
	'}'
;

ElementoLista:
	value=STRING |
	key=ElementoListaKey (':' value=STRING)? |
	key=ElementoListaKey ':' '{'
		elementosDependientes+=ElementoListaDependiente*
	'}'
;

ElementoListaDependiente:
	value=STRING |
	key=ElementoListaKey (':' value=STRING)?
;

ElementoListaKey:
	first=IdOrInt (("." resto+=IdOrInt)*)
;

IdOrInt:
	ID | INT
;

PermisoGrafico:
	elemento='PermisoGrafico' name=ID "{"
		('mensaje' '=' mensaje=STRING)?
		(varSection = PermisoVarsSection)?
		(
			("return" ret = PermisoReturnGrafico) 	
			|
			(
				whens += PermisoWhenGrafico+
				("else" "return" else=PermisoReturnGrafico)? 			
			)
		)
	"}"
;

PermisoWhenGrafico:
	'when'
		rule = PermisoRule
	'return'
		ret = PermisoReturnGrafico
;

PermisoReturnGrafico:
	ret = ("editable" | "visible" | "none")
;

PermisoAcceso:
	elemento='PermisoAcceso' name=ID "{"
		('mensaje' '=' mensaje=STRING)?
		(varSection = PermisoVarsSection)?
		(
			("return" ret = PermisoReturnAcceso) 	
			|
			(
				whens += PermisoWhenAcceso+
				("else" "return" else=PermisoReturnAcceso)? 			
			)
		)
	"}"
;

PermisoWhenAcceso:
	'when'
		rule = PermisoRule
	'return'
		ret = PermisoReturnAcceso
;

PermisoReturnAcceso:
	all?= "all" | (acceso?= "{" (acciones+="leer"? & acciones+="editar"? & acciones+="crear"? & acciones+="borrar"?) "}")
;

PermisoVarsSection:
	'vars'
		vars+=PermisoVar+
;

PermisoVar:
	tipo=[Entity|IDS] name=IDS
		(':' 'sql' '(' sql=STRING (',' sqlParams=PermisoVarSqlParameters)? ')') ?
;

PermisoVarSqlParameters:
	sqlParams+=CampoPermiso (',' sqlParams+=CampoPermiso)*
;

CampoPermiso:
	(variable=[PermisoVar] | action?="accion" | agente?="agente") (atributos=CampoPermisoAtributos)?
;

CampoPermisoAtributos:
	"." atributo=[Attribute|IDS] (atributos=CampoPermisoAtributos)?
;

PermisoRule:
	PermisoRuleOr
;	

PermisoRuleOr returns PermisoRule:
	PermisoRuleAnd ({PermisoRuleOr.left=current} 'or' right=PermisoRuleAnd)*
;

PermisoRuleAnd returns PermisoRule:
	PermisoPrimary ({PermisoRuleAnd.left=current} 'and' right=PermisoPrimary)*
;

PermisoPrimary returns PermisoRule:
	PermisoRuleCheck |
	 '(' PermisoRuleOr ')' {PermisoPrimary.left=current}
;

PermisoRuleCheck:
	left=CampoPermiso (
		(simpleOp=PermisoRuleCheckSimpleOp right=PermisoRuleCheckRight)
		| (groupOp=PermisoRuleCheckGroupOp '(' rightGroup+=PermisoRuleCheckRight (',' rightGroup+=PermisoRuleCheckRight)* ')')
	)
	| 
	((not?="!")?
		(
			(permisoGrafico=[PermisoGrafico] "is" result=("editable" | "visible" | "none" | "_permiso"))
			| (permisoAcceso=[PermisoAcceso] "contains" result=("leer" | "editar" | "crear" | "borrar" | "accion"))
		)
	)
;

PermisoRuleCheckSimpleOp:
	'=' | '!='
;

PermisoRuleCheckGroupOp:
	'in' | 'not in'
;

PermisoRuleCheckRight:
	campo=CampoPermiso | str=STRING  | nulo?='null' | action=("leer" | "editar" | "crear" | "borrar")
;