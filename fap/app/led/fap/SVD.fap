Formulario SolicitudTransmision campo=SolicitudGenerica {
	
	Menu{
		Enlace "Volver" anterior
		Grupo "Solicitud"  {
		      Enlace "Documentos" pagina=DocumentacionFAP
		      Enlace "Cesiones de datos" pagina=CesionDatosExpedienteSVD
		      Enlace "Autorizaciones Cesión de datos" pagina=CesionDatos
	    }
		
		Enlace "Ayuda" pagina=Soporte crear
		Enlace "Salir" accion=logout	
	}

	Pagina CesionDatosExpedienteSVD titulo="Cesión de datos" campo=SolicitudGenerica permiso=adminOrGestor {
		
		Combo tipoServicioSVD titulo="Seleccione el tipo de Servicio: " campo=SolicitudGenerica.nombreServicio requerido
		
		Grupo GrupoTablaIdentidad borde=false mostrarSiCombo tipoServicioSVD="identidad" {
			Tabla solicitudesPeticionIdentidad titulo="Solicitudes de Transmisión Identidad" campo=SolicitudTransmisionSVDFAP
			 seleccionable="Enviar Solicitudes Identidad" paginaLeer=EditarSolicitudTransmisionSVDIdentidad paginaCrear=EditarSolicitudTransmisionSVDIdentidad paginaEditar=EditarSolicitudTransmisionSVDIdentidad paginaBorrar=EditarSolicitudTransmisionSVDIdentidad
			{
				Columna campo=SolicitudTransmisionSVDFAP.id titulo="Id" ancho="40"
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.nombreSolicitante titulo="Solicitante" expandir
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.unidadTramitadora titulo="Unidad tramitadora" expandir
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.idExpediente titulo="Expediente" expandir
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.consentimiento titulo="Consentimiento" expandir
				Columna campo=SolicitudTransmisionSVDFAP.fechaCreacion titulo="Fecha de creación" expandir
				Columna campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.fechaGeneracion titulo="Fecha de respuesta" expandir
				Columna campo=SolicitudTransmisionSVDFAP.estado titulo="Estado" expandir
			}
		}
		
		Grupo GrupoTablaResidencia borde=false mostrarSiCombo tipoServicioSVD="residencia"{
			Tabla solicitudesPeticionResidencia titulo="Solicitudes de Transmisión Residencia" campo=SolicitudTransmisionSVDFAP
			seleccionable="Enviar Solicitudes Residencia" paginaLeer=EditarSolicitudTransmisionSVDIdentidad paginaCrear=EditarSolicitudTransmisionSVDResidencia paginaEditar=EditarSolicitudTransmisionSVDResidencia paginaBorrar=EditarSolicitudTransmisionSVDResidencia
			{
				Columna campo=SolicitudTransmisionSVDFAP.id titulo="Id" ancho="40"
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.nombreSolicitante titulo="Solicitante" expandir
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.unidadTramitadora titulo="Unidad tramitadora" expandir
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.idExpediente titulo="Expediente" expandir
				Columna campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.consentimiento titulo="Consentimiento" expandir
				Columna campo=SolicitudTransmisionSVDFAP.fechaCreacion titulo="Fecha de creación" expandir
				Columna campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.fechaGeneracion titulo="Fecha de respuesta" expandir
				Columna campo=SolicitudTransmisionSVDFAP.estado titulo="Estado" expandir
			}
		}
	}
	
	Pagina EditarSolicitudTransmisionSVDIdentidad titulo="Solicitud de Transmisión - Identidad" campo=SolicitudTransmisionSVDFAP permiso=adminOrGestor {
		Grupo datosGenericosSolicitudTransmision titulo="Datos genéricos de la Solicitud" {
			Grupo datosSolicitante titulo="Datos del solicitante"{
				Texto titulo="CIF del Solicitante" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.identificadorSolicitante requerido
			  	Texto titulo="Nombre del Solicitante" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.nombreSolicitante
			  	Texto titulo="Finalidad" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.finalidad
			  	Texto titulo="Unidad tramitadora" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.unidadTramitadora
			  	Texto titulo="Código del Procedimiento" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.procedimiento.codigoProcedimiento
			  	Texto titulo="Nombre del procedimiento" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.procedimiento.nombreProcedimiento
			  	Combo titulo="Consentimiento" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.consentimiento
			  	Grupo datosFuncionario titulo="Datos del funcionario" {
			  		Texto titulo="Nombre completo" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.funcionario.nombreCompletoFuncionario
			  		//Texto titulo="NIF" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.funcionario.nifFuncionario
			  	}
			}
		}
		Grupo datosEspecificosSolicitudTransmision titulo="Datos específicos de la Solicitud" {
			Texto titulo="Tipo de solicitante de datos" campo=SolicitudTransmisionSVDFAP.datosEspecificos.tipoSolicitante
			Grupo datosResidencia titulo="Residencia" {
				Combo ComboComunidadAutonoma titulo="Comunidad Autónoma" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.comunidadAutonoma busqueda
				Combo ComboProvincia titulo="Provincia" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.provincia busqueda dependeDe=ComboComunidadAutonoma
				Combo ComboIsla titulo="Isla" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.isla busqueda dependeDe=ComboProvincia
				Combo ComboMunicipio titulo="Municipio" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.municipio busqueda dependeDe=ComboIsla
				Combo ComboTitulo titulo="Español" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.espanol
			}
			Grupo datosNacimiento titulo="Nacimiento" {
				Fecha titulo="Fecha de nacimiento" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.fecha
				Combo ComboProvinciaNacimiento titulo="Provincia" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.provincia
				Combo ComboMunicipioNacimiento titulo="Municipio" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.municipio dependeDe=ComboProvinciaNacimiento
			}

		}
		
		Grupo respuesta titulo="Respuesta" mostrarSiExpresion "(solicitudTransmisionSVDFAP.estado == \"recibida\")" permiso=soloLeer{
			Texto titulo="idTransmision" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.idTransmision
			Fecha titulo="Fecha generación" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.fechaGeneracion
			Texto titulo="Estado" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.estado.literalError
			Combo titulo="Tipo documentación" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.tipoDocumentacion
			Texto titulo="Documentación" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.documentacion
			Grupo respuestaIdentidad titulo="Identidad" {
				Texto titulo="Nombre completo" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.nombreCompleto
				Texto titulo="Apellido 1" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.apellido1
				Texto titulo="Apellido 2" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.apellido2
				Texto titulo="Nacionalidad" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.nacionalidad
				Texto titulo="Nombre del padre" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.nombrePadre
				Texto titulo="Nombre de la madre" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.nombreMadre
				Fecha titulo="Fecha de caducidad" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.fechacaducidad
				Texto titulo="Sexo" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.sexo.nombre
				Grupo datosNacimiento titulo="Datos nacimiento" {
					Fecha titulo="Fecha de nacimiento" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosNacimiento.fecha
					Combo ComboProvinciaNacimiento titulo="Provincia" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosNacimiento.provincia
					Combo ComboLocalidadNacimiento titulo="Localidad" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosNacimiento.municipio
				}
				Grupo datosDireccion titulo="Datos dirección" {
					Combo titulo="Provincia" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosDireccion.provincia
					Texto titulo="Localidad" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosDireccion.localidad
					Texto titulo="Tipo de vía" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosDireccion.datosVia.tipo
					Texto titulo="Vía" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.datosTitular.datosDireccion.datosVia.nombre
				}
			}			
		}
		Enlace volverVerificacionDatos titulo="Volver" pagina=CesionDatosExpedienteSVD estilo="btn" 	
	}
	
	
	Pagina EditarSolicitudTransmisionSVDResidencia titulo="Solicitud de Transmisión - Residencia" campo=SolicitudTransmisionSVDFAP permiso=adminOrGestor {
		Grupo datosGenericosSolicitudTransmision titulo="Datos genéricos de la Solicitud" {
			Grupo datosSolicitante titulo="Datos del solicitante"{
				Texto titulo="CIF del Solicitante" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.identificadorSolicitante requerido
			  	Texto titulo="Nombre del Solicitante" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.nombreSolicitante
			  	Texto titulo="Finalidad" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.finalidad
			  	Texto titulo="Unidad tramitadora" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.unidadTramitadora
			  	Texto titulo="Código del Procedimiento" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.procedimiento.codigoProcedimiento
			  	Texto titulo="Nombre del procedimiento" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.procedimiento.nombreProcedimiento
			  	Combo titulo="Consentimiento" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.consentimiento
			  	Grupo datosFuncionario titulo="Datos del funcionario" {
			  		Texto titulo="Nombre completo" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.funcionario.nombreCompletoFuncionario
			  		//Texto titulo="NIF" campo=SolicitudTransmisionSVDFAP.datosGenericos.solicitante.funcionario.nifFuncionario
			  	}
			}
		}
		Grupo datosEspecificosSolicitudTransmision titulo="Datos específicos de la Solicitud" {
			Texto titulo="Tipo de solicitante de datos" campo=SolicitudTransmisionSVDFAP.datosEspecificos.tipoSolicitante
			Grupo solicitud titulo="Residencia" {
				Combo ComboComunidadAutonoma titulo="Comunidad Autónoma" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.comunidadAutonoma busqueda
				Combo ComboProvincia titulo="Provincia" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.provincia busqueda dependeDe=ComboComunidadAutonoma
				Combo ComboIsla titulo="Isla" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.isla busqueda dependeDe=ComboProvincia
				Combo ComboMunicipio titulo="Municipio" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.residencia.municipio busqueda dependeDe=ComboIsla
				Combo ComboTitulo titulo="Español" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.espanol
			}
			Grupo datosNacimiento titulo="Nacimiento" {
				Fecha FechaNacimiento titulo="Fecha" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.fecha
				Combo ComboComunidadAutonomaNacimiento titulo="Comunidad Autónoma" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.comunidadAutonoma busqueda
				Combo ComboProvinciaNacimiento titulo="Provincia" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.provincia busqueda dependeDe=ComboComunidadAutonomaNacimiento
				Combo ComboIslaNacimiento titulo="Isla" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.isla busqueda dependeDe=ComboProvinciaNacimiento
				Combo ComboMunicipioNacimiento titulo="Municipio" campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.solicitudNacimiento.municipio busqueda dependeDe=ComboIslaNacimiento
			}
		}
		
		Grupo respuesta titulo="Respuesta" mostrarSiExpresion "(solicitudTransmisionSVDFAP.estado == \"recibida\")" permiso=soloLeer {
			Texto titulo="idTransmision" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.idTransmision
			Fecha titulo="Fecha generación" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.fechaGeneracion
			Texto titulo="Estado" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.estado.literalError
			Combo titulo="Tipo documentación" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.tipoDocumentacion
			Texto titulo="Documentación" campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.titular.documentacion
		
			Grupo respuestaResidencia titulo="Residencia" {
				Grupo datosDomicilio titulo="Domicilio" {
					Combo titulo="Provincia" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.provincia
					Combo titulo="Municipio" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.municipio
					Texto titulo="Entidad colectiva" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.entColectiva.nombre
					Texto titulo="Entidad singular" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.entSingular.nombre
					Texto titulo="Núcleo" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.nucleo.nombre
					Texto titulo="Unidad poblacional" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.codUnidadPoblacional
				}
				Grupo datosDireccion titulo="Dirección" {
					Texto titulo="Tipo de vía" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.via.tipo
					Texto titulo="Vía" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.via.nombre
					Texto titulo="Número" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.numero.valor
					Texto titulo="Número superior" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.numeroSuperior.valor
					Texto titulo="Kmt" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.kmt
					Texto titulo="Hmt" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.hmt
					Texto titulo="Bloque" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.bloque
					Texto titulo="Portal" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.portal
					Texto titulo="Escalera" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.escalera
					Texto titulo="Planta" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.planta
					Texto titulo="Puerta" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.puerta
					Texto titulo="Código Postal" campo=SolicitudTransmisionSVDFAP.respuesta.datosEspecificos.domicilio.direccion.codigoPostal
				}
			}
			
		}
		Enlace volverVerificacionDatos titulo="Volver" pagina=CesionDatosExpedienteSVD estilo="btn" 	
	}
	
}


//CESIONES DE DATOS ASÍNCRONAS
Formulario CesionDatos {
    
    Menu {
        Enlace "Volver" anterior
        Grupo "Cesiones de datos"  {
              Enlace "Crear peticiones" pagina=CesionDatosSVDCrear
              Enlace "Listar peticiones" pagina=CesionDatosSVDListar
        }
        
        Enlace "Ayuda" pagina=Soporte crear
        Enlace "Salir" accion=logout    
    }
    
    Pagina CesionDatosSVDCrear titulo="Crear Peticiones de Cesión de Datos" permiso=adminOrGestor {
    
        Grupo grupoCrearCesiones {
           Combo tipoServicioSVD titulo="Seleccione el tipo de Servicio: " campo=ComboNombreServicioSVDFAP.nombreServicio requerido
           
           Grupo GrupoTablaSolicitudesAutorizadasIdentidad mostrarSiCombo tipoServicioSVD="identidad" {
                Tabla tablaSolicitudesIdentidadAutorizadas titulo="Solicitudes de Identidad Autorizadas" campo=SolicitudGenerica
                 seleccionable="Crear peticion Identidad" paginaLeer=LeerSolicitud {
                    Columna campo=SolicitudGenerica.id titulo="ID" ancho="30"
                    Columna campo=SolicitudGenerica.expedienteAed.idAed titulo="Expediente" ancho="150"
                    Columna campo=SolicitudGenerica.estado titulo="Estado Interno" ancho="80" permiso=noVisibleUsuario
                    Columna campo=SolicitudGenerica.estadoValue titulo="Estado" ancho="80" permiso=noVisibleUsuario
                    Columna campo=SolicitudGenerica.estadoUsuario titulo="Estado Usuario" ancho="80"
                    Columna campo=SolicitudGenerica.solicitante.numeroId titulo="ID Solicitante" expandir
                    Columna campo=SolicitudGenerica.solicitante.nombreCompleto titulo="Nombre Solicitante" expandir   
                    Columna campo=SolicitudGenerica.estadoPeticionSVD titulo="Petición SVD" ancho="80"    
                 }
           }     
           
          Grupo GrupoTablaSolicitudesAutorizadasResidencia mostrarSiCombo tipoServicioSVD="residencia" {
                Tabla tablaSolicitudesResidenciaAutorizadas titulo="Solicitudes de Residencia Autorizadas" campo=SolicitudGenerica
                 seleccionable="Crear peticion Residencia" paginaLeer=LeerSolicitud {
                    Columna campo=SolicitudGenerica.id titulo="ID" ancho="30"
                    Columna campo=SolicitudGenerica.expedienteAed.idAed titulo="Expediente" ancho="150"
                    Columna campo=SolicitudGenerica.estado titulo="Estado Interno" ancho="80" permiso=noVisibleUsuario
                    Columna campo=SolicitudGenerica.estadoValue titulo="Estado" ancho="80" permiso=noVisibleUsuario
                    Columna campo=SolicitudGenerica.estadoUsuario titulo="Estado Usuario" ancho="80"
                    Columna campo=SolicitudGenerica.solicitante.numeroId titulo="ID Solicitante" expandir
                    Columna campo=SolicitudGenerica.solicitante.nombreCompleto titulo="Nombre Solicitante" expandir   
                    Columna campo=SolicitudGenerica.estadoPeticionSVD titulo="Petición SVD" ancho="80"      
                 }
           }   
       }
    }
    
    
    Pagina CesionDatosSVDListar titulo="Peticiones de Cesión de Datos" permiso=adminOrGestor {
        
        Grupo grupoListadoCesiones {
            Combo tipoServicioSVD titulo="Seleccione el tipo de Servicio: " campo=ComboNombreServicioSVDFAP.nombreServicio requerido
            
            Grupo GrupoTablaCesionesIdentidad mostrarSiCombo tipoServicioSVD="identidad" {
                Tabla tablaCesionesIdentidad titulo="Peticiones de Identidad" campo=PeticionSVDFAP
                 seleccionable="Enviar peticiones Identidad" paginaLeer=EditarPeticionSVDFAP paginaEditar=EditarPeticionSVDFAP {
                     Columna campo=PeticionSVDFAP.id titulo="ID" ancho="40"
                     Columna campo=PeticionSVDFAP.estadoPeticion titulo="Estado" expandir
                     Columna campo=PeticionSVDFAP.atributos.numElementos titulo="Num Elementos" expandir
                }      
            }
            
            Grupo GrupoTablaCesionesResidencia mostrarSiCombo tipoServicioSVD="residencia" {
                Tabla tablaCesionesResidencia titulo="Peticiones de Residencia" campo=PeticionSVDFAP
                 seleccionable="Enviar peticiones Residencia" paginaLeer=EditarPeticionSVDFAP paginaEditar=EditarPeticionSVDFAP  {
                     Columna campo=PeticionSVDFAP.id titulo="ID" ancho="40"
                     Columna campo=PeticionSVDFAP.estadoPeticion titulo="Estado" expandir
                     Columna campo=PeticionSVDFAP.atributos.numElementos titulo="Num Elementos" expandir
                }   
            }   
         }
    }
    
    
    Pagina EditarPeticionSVDFAP titulo="Petición de Cesión de Datos" campo=PeticionSVDFAP permiso=adminOrGestor {
        
        Grupo GrupoPeticionDatos titulo="Datos de la Petición" permiso=soloLeer {
            Texto titulo="ID Petición" campo=PeticionSVDFAP.id
            Combo titulo="Estado" campo=PeticionSVDFAP.estadoPeticion
            Texto titulo="Num Elementos" campo=PeticionSVDFAP.atributos.numElementos
        }
        
        Grupo grupoTablaSolicitudesIdentidad mostrarSiExpresion "(peticionSVDFAP.nombreServicio == \"identidad\")" {
            Tabla tablaSolicitudesTransmisionIdentidad titulo="Solicitudes de Transmisión" campo=SolicitudTransmisionSVDFAP
            paginaLeer=EditarSolicitudTransmisionSVDIdentidad paginaEditar=EditarSolicitudTransmisionSVDIdentidad {
                Columna campo=SolicitudTransmisionSVDFAP.id titulo="ID" ancho="30"
                Columna campo=SolicitudTransmisionSVDFAP.nombreServicio titulo="Tipo" ancho="100"
                Columna campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.id titulo="ID Expediente" ancho="90"
                Columna campo=SolicitudTransmisionSVDFAP.fechaCreacion titulo="Fecha de creación" ancho="100"
                Columna campo=SolicitudTransmisionSVDFAP.estado titulo="Estado" ancho="80"
                Columna campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.fechaGeneracion titulo="Fecha de respuesta" ancho="120"
                Columna campo=SolicitudTransmisionSVDFAP.descargarPDF titulo="Descargar PDF Respuesta" expandir
            }
            Enlace prepararPeticion titulo="Preparar petición" estilo="btn" pagina=CesionDatosSVDListar
        }
        
        Grupo grupoTablaSolicitudesResidencia mostrarSiExpresion "(peticionSVDFAP.nombreServicio == \"residencia\")" {
            Tabla tablaSolicitudesTransmisionResidencia titulo="Solicitudes de Transmisión" campo=SolicitudTransmisionSVDFAP
            paginaLeer=EditarSolicitudTransmisionSVDResidencia paginaEditar=EditarSolicitudTransmisionSVDResidencia {
                Columna campo=SolicitudTransmisionSVDFAP.id titulo="ID" ancho="30"
                Columna campo=SolicitudTransmisionSVDFAP.nombreServicio titulo="Tipo" ancho="100"
                Columna campo=SolicitudTransmisionSVDFAP.datosEspecificos.solicitud.id titulo="ID Expediente" ancho="90"
                Columna campo=SolicitudTransmisionSVDFAP.fechaCreacion titulo="Fecha de creación" ancho="100"
                Columna campo=SolicitudTransmisionSVDFAP.estado titulo="Estado" ancho="80"
                Columna campo=SolicitudTransmisionSVDFAP.respuesta.datosGenericos.transmision.fechaGeneracion titulo="Fecha de respuesta" ancho="120"
                Columna campo=SolicitudTransmisionSVDFAP.descargarPDF titulo="Descargar PDF Respuesta" expandir
            }
            Enlace prepararPeticion titulo="Preparar petición" estilo="btn" pagina=CesionDatosSVDListar
        }
    }
}