module workflow.LedGenerator

import org.eclipse.emf.mwe.utils.*

var fileEncoding = "UTF-8"

var modelPath
var fapModelPath
var targetPath
var solicitud

Workflow {

	component = org.eclipse.xtext.mwe.Reader {
		// lookup all resources on the classpath
		useJavaClassPath = true

		

		// or define search scope explicitly
		path = modelPath
		path = fapModelPath

		// this class will be generated by the xtext generator 
		register = es.fap.simpleled.LedStandaloneSetup {}

		load = {
			slot = "entidades"
			type = "Entity"
		}
		
		load = {
			slot = "listas"
			type = "Lista"
		}
		
		load = {
			slot = "formularios"
			type = "Formulario"
		}
		
		load = {
			slot = "permisosGrafico"
			type = "PermisoGrafico"
		}
		
		load = {
			slot = "permisosAcceso"
			type = "PermisoAcceso"
		}
		
	}

	component = wfcomponent.Start {
		path = modelPath
		target = targetPath
		createSolicitud = solicitud
	}

	//Expande los formularios
	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH formularios"
		outlet = {
			path = targetPath
		}
		
		fileEncoding = fileEncoding
	}

	//Expande las entidades
	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH entidades"
		outlet = {
			path = targetPath	
		}
		
		fileEncoding = fileEncoding
	}
	
	//Expande las listas
	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH listas"
		outlet = {
			path = targetPath	
		}
		
		fileEncoding = fileEncoding
	}
	
	//Expande los permisos graficos
	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH permisosGrafico"
		outlet = {
			path = targetPath
		}
		
		fileEncoding = fileEncoding
	}
	
	//Expande los permisos de acceso
	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOREACH permisosAcceso"
		outlet = {
			path = targetPath
		}
		
		fileEncoding = fileEncoding
	}
	
	component = wfcomponent.End {
		modelPath = modelPath
		targetDir = targetPath
		createSolicitud = solicitud
	}
	
}
